Перем мУдалятьДвижения;

Перем мВалютаРегламентированногоУчета Экспорт;

Перем мУчетнаяПолитикаБУ Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда

// Функция формирует табличный документ с печатной формой накладной,
// разработанной методистами
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПечатьПереоценки()

	ВестиПартионныйУчетПоСкладамРегл = глЗначениеПеременной("ПараметрыПартионногоУчета").ВестиПартионныйУчетПоСкладамРегл;
	
	ЗапросПоЗапасам = Новый Запрос;
	ЗапросПоЗапасам.УстановитьПараметр("ТекущийДокумент", ЭтотОбъект.Ссылка);
	ЗапросПоЗапасам.УстановитьПараметр("ДатаОстатков", Новый Граница(КонецМесяца(ЭтотОбъект.ПериодРегистрации), ВидГраницы.Включая));

	ИспользуетсяРАУЗ = УправлениеЗапасами.ИспользуетсяРасширеннаяАналитикаУчета(КонецМесяца(ПериодРегистрации));
	Если ИспользуетсяРАУЗ Тогда
		ЗапросПоЗапасам.Текст =
		"ВЫБРАТЬ
		|	УчетЗатратРегл.АналитикаВидаУчета,
		|	УчетЗатратРегл.АналитикаУчетаЗатрат,
		|	УчетЗатратРегл.АналитикаУчетаПартий,
		|	УчетЗатратРегл.АналитикаРаспределенияЗатрат,
		|	РегистрСведенийАналитикаУчетаЗатрат.Затрата КАК Номенклатура,
		|	РегистрСведенийАналитикаУчетаЗатрат.ХарактеристикаЗатраты КАК ХарактеристикаНоменклатуры,
		|	РегистрСведенийАналитикаУчетаЗатрат.СерияЗатраты КАК СерияНоменклатуры,
		|	РегистрСведенийАналитикаВидаУчета.СчетУчета,
		|	РегистрСведенийАналитикаВидаУчета.НалоговоеНазначение,
		|	УчетЗатратРегл.Стоимость КАК ИзменениеСтоимости
		|ПОМЕСТИТЬ ДвиженияДокумента
		|ИЗ
		|	РегистрНакопления.УчетЗатратРегл КАК УчетЗатратРегл
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаВидаУчета КАК РегистрСведенийАналитикаВидаУчета
		|		ПО УчетЗатратРегл.АналитикаВидаУчета = РегистрСведенийАналитикаВидаУчета.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаЗатрат КАК РегистрСведенийАналитикаУчетаЗатрат
		|		ПО УчетЗатратРегл.АналитикаУчетаЗатрат = РегистрСведенийАналитикаУчетаЗатрат.Ссылка
		|ГДЕ
		|	УчетЗатратРегл.Регистратор = &ТекущийДокумент
		|	И РегистрСведенийАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.МПЗ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияДокумента.Номенклатура КАК Номенклатура,
		|	ДвиженияДокумента.Номенклатура.Код КАК Код,
		|	ДвиженияДокумента.Номенклатура.Артикул КАК Артикул,
		|	ДвиженияДокумента.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
		|	ДвиженияДокумента.ХарактеристикаНоменклатуры КАК Характеристика,
		|	ДвиженияДокумента.СерияНоменклатуры КАК Серия,
		|	УчетЗатратРеглОстатки.КоличествоОстаток КАК Количество,
		|	УчетЗатратРеглОстатки.СтоимостьОстаток,
		|	ДвиженияДокумента.ИзменениеСтоимости,
		|	УчетЗатратРеглОстатки.СтоимостьОстаток - ДвиженияДокумента.ИзменениеСтоимости КАК СтараяСтоимость,
		|	(УчетЗатратРеглОстатки.СтоимостьОстаток - ДвиженияДокумента.ИзменениеСтоимости) / УчетЗатратРеглОстатки.КоличествоОстаток КАК СтараяЦена,
		|	УчетЗатратРеглОстатки.СтоимостьОстаток КАК НоваяСтоимость,
		|	УчетЗатратРеглОстатки.СтоимостьОстаток / УчетЗатратРеглОстатки.КоличествоОстаток КАК НоваяЦена
		|ИЗ
		|	ДвиженияДокумента КАК ДвиженияДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.УчетЗатратРегл.Остатки(
		|				&ДатаОстатков,
		|				(АналитикаВидаУчета, АналитикаУчетаЗатрат, АналитикаУчетаПартий, АналитикаРаспределенияЗатрат) В
		|					(ВЫБРАТЬ
		|						ДвиженияДокумента.АналитикаВидаУчета,
		|						ДвиженияДокумента.АналитикаУчетаЗатрат,
		|						ДвиженияДокумента.АналитикаУчетаПартий,
		|						ДвиженияДокумента.АналитикаРаспределенияЗатрат
		|					ИЗ
		|						ДвиженияДокумента КАК ДвиженияДокумента)) КАК УчетЗатратРеглОстатки
		|		ПО ДвиженияДокумента.АналитикаВидаУчета = УчетЗатратРеглОстатки.АналитикаВидаУчета
		|			И ДвиженияДокумента.АналитикаУчетаЗатрат = УчетЗатратРеглОстатки.АналитикаУчетаЗатрат
		|			И ДвиженияДокумента.АналитикаУчетаПартий = УчетЗатратРеглОстатки.АналитикаУчетаПартий
		|			И ДвиженияДокумента.АналитикаРаспределенияЗатрат = УчетЗатратРеглОстатки.АналитикаРаспределенияЗатрат
		|АВТОУПОРЯДОЧИВАНИЕ";			
	Иначе
		ЗапросПоЗапасам.УстановитьПараметр("Организация", ЭтотОбъект.Организация);
		ЗапросПоЗапасам.Текст =
		"ВЫБРАТЬ
		|	ПартииТоваровНаСкладахБухгалтерскийУчет.Номенклатура,
		|	ПартииТоваровНаСкладахБухгалтерскийУчет.СчетУчета,
		|	ПартииТоваровНаСкладахБухгалтерскийУчет.НалоговоеНазначение,
		|	ПартииТоваровНаСкладахБухгалтерскийУчет.Склад,
		|	ПартииТоваровНаСкладахБухгалтерскийУчет.ДокументОприходования,
		|	ПартииТоваровНаСкладахБухгалтерскийУчет.ХарактеристикаНоменклатуры,
		|	ПартииТоваровНаСкладахБухгалтерскийУчет.СерияНоменклатуры,
		|	ПартииТоваровНаСкладахБухгалтерскийУчет.Заказ,
		|	ПартииТоваровНаСкладахБухгалтерскийУчет.Качество,
		|	ВЫБОР
		|		КОГДА ПартииТоваровНаСкладахБухгалтерскийУчет.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ПартииТоваровНаСкладахБухгалтерскийУчет.Стоимость
		|		ИНАЧЕ -ПартииТоваровНаСкладахБухгалтерскийУчет.Стоимость
		|	КОНЕЦ КАК ИзменениеСтоимости
		|ПОМЕСТИТЬ ДвиженияДокумента
		|ИЗ
		|	РегистрНакопления.ПартииТоваровНаСкладахБухгалтерскийУчет КАК ПартииТоваровНаСкладахБухгалтерскийУчет
		|ГДЕ
		|	ПартииТоваровНаСкладахБухгалтерскийУчет.Регистратор = &ТекущийДокумент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииТовароБухОстатки.Номенклатура КАК Номенклатура,
		|	ПартииТовароБухОстатки.Номенклатура.Код КАК Код,
		|	ПартииТовароБухОстатки.Номенклатура.Артикул КАК Артикул,
		|	ПартииТовароБухОстатки.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
		|	ПартииТовароБухОстатки.ХарактеристикаНоменклатуры КАК Характеристика,
		|	ПартииТовароБухОстатки.СерияНоменклатуры КАК Серия,
		|	ПартииТовароБухОстатки.ДокументОприходования КАК ДокументОприходования,
		|	ПартииТовароБухОстатки.КоличествоОстаток КАК Количество,
		|	ПартииТовароБухОстатки.СтоимостьОстаток,
		|	ДвиженияДокумента.ИзменениеСтоимости,
		|	ПартииТовароБухОстатки.СуммаУценкиОстаток,
		|	ПартииТовароБухОстатки.СтоимостьОстаток - ДвиженияДокумента.ИзменениеСтоимости КАК СтараяСтоимость,
		|	(ПартииТовароБухОстатки.СтоимостьОстаток - ДвиженияДокумента.ИзменениеСтоимости) / ПартииТовароБухОстатки.КоличествоОстаток КАК СтараяЦена,
		|	ПартииТовароБухОстатки.СтоимостьОстаток + ПартииТовароБухОстатки.СуммаУценкиОстаток КАК ПервоначальнаяСтоимость,
		|	(ПартииТовароБухОстатки.СтоимостьОстаток + ПартииТовароБухОстатки.СуммаУценкиОстаток) / ПартииТовароБухОстатки.КоличествоОстаток КАК ПервоначальнаяЦена,
		|	ПартииТовароБухОстатки.СтоимостьОстаток КАК НоваяСтоимость,
		|	ПартииТовароБухОстатки.СтоимостьОстаток / ПартииТовароБухОстатки.КоличествоОстаток КАК НоваяЦена
		|ИЗ
		|	ДвиженияДокумента КАК ДвиженияДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладахБухгалтерскийУчет.Остатки(
		|				&ДатаОстатков,
		|				Организация = &Организация
		|					И Номенклатура В
		|						(ВЫБРАТЬ
		|							ДвиженияДокумента.Номенклатура
		|						ИЗ
		|							ДвиженияДокумента)) КАК ПартииТовароБухОстатки
		|		ПО ДвиженияДокумента.Номенклатура = ПартииТовароБухОстатки.Номенклатура
		|			И ДвиженияДокумента.СчетУчета = ПартииТовароБухОстатки.СчетУчета
		|			И ДвиженияДокумента.НалоговоеНазначение = ПартииТовароБухОстатки.НалоговоеНазначение
		|			И ДвиженияДокумента.Склад = ПартииТовароБухОстатки.Склад
		|			И ДвиженияДокумента.ДокументОприходования = ПартииТовароБухОстатки.ДокументОприходования
		|			И ДвиженияДокумента.ХарактеристикаНоменклатуры = ПартииТовароБухОстатки.ХарактеристикаНоменклатуры
		|			И ДвиженияДокумента.СерияНоменклатуры = ПартииТовароБухОстатки.СерияНоменклатуры
		|			И ДвиженияДокумента.Заказ = ПартииТовароБухОстатки.Заказ
		|			И ДвиженияДокумента.Качество = ПартииТовароБухОстатки.Качество
		|АВТОУПОРЯДОЧИВАНИЕ";	
	КонецЕсли;

	ЗапросЗапасы = ЗапросПоЗапасам.Выполнить().Выгрузить();
	
	ЗапросПоШапке = Новый Запрос;
	ЗапросПоШапке.УстановитьПараметр("ТекущийДокумент", ЭтотОбъект.Ссылка);

	ЗапросПоШапке.Текст =
	"ВЫБРАТЬ
	|	Номер,
	|	КонецПериода(ПериодРегистрации, Месяц) КАК Дата,
	|	Организация"+?(ВестиПартионныйУчетПоСкладамРегл,
	",
	|	Склад	                     КАК Получатель ,
	|	Склад.Представление          КАК ПредставлениеСклада","")
	+"
	|ИЗ
	|	Документ.РегламентнаяПереоценкаЗапасов КАК РегламентнаяПереоценкаЗапасов
	|
	|ГДЕ
	|	РегламентнаяПереоценкаЗапасов.Ссылка = &ТекущийДокумент";
	
	Шапка = ЗапросПоШапке.Выполнить().Выбрать();
	Шапка.Следующий();
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РегламентнаяПереоценкаЗапасов_РегламентнаяПереоценкаЗапасов";
	Макет = ПолучитьМакет("РегламентнаяПереоценкаЗапасов");
	
	// печать производится на языке, указанном в настройках пользователя
	КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм(УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "РежимФормированияПечатныхФорм"));
	Макет.КодЯзыкаМакета = КодЯзыкаПечать;

	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = ОбщегоНазначения.СформироватьЗаголовокДокумента(Шапка, НСтр("ru='Переоценка запасов';uk='Переоцінка запасів'",КодЯзыкаПечать),КодЯзыкаПечать);
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("Организация");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата,,,КодЯзыкаПечать), "ПолноеНаименование,",,КодЯзыкаПечать);
	ТабДокумент.Вывести(ОбластьМакета);

	Если ВестиПартионныйУчетПоСкладамРегл Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Склад");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли; 

	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ВыводитьКоды = Истина;
		Колонка = "Артикул";
	ИначеЕсли ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Код Тогда
		ВыводитьКоды = Истина;
		Колонка = "Код";
	Иначе
		ВыводитьКоды = Ложь;
	КонецЕсли;
	
	ОбластьНомера = Макет.ПолучитьОбласть("ШапкаТаблицы|НомерСтроки");
	ОбластьКодов  = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");
	ОбластьДанных = Макет.ПолучитьОбласть("ШапкаТаблицы|Данные");
	ОбластьСуммыСтарая  		= Макет.ПолучитьОбласть("ШапкаТаблицы|Старая");
	//Если Не ИспользуетсяРАУЗ Тогда
		ОбластьСуммыПервоначальная	= Макет.ПолучитьОбласть("ШапкаТаблицы|Первоначальная");
	//КонецЕсли;
	ОбластьСуммыНовая		  	= Макет.ПолучитьОбласть("ШапкаТаблицы|Новая");
	
	ТабДокумент.Вывести(ОбластьНомера);
	Если ВыводитьКоды Тогда
		ОбластьКодов.Параметры.ИмяКолонкиКодов = Колонка;
		ТабДокумент.Присоединить(ОбластьКодов);
	КонецЕсли;
	
	ТабДокумент.Присоединить(ОбластьДанных);
	ТабДокумент.Присоединить(ОбластьСуммыСтарая);       	
	Если Не ИспользуетсяРАУЗ Тогда
		ТабДокумент.Присоединить(ОбластьСуммыПервоначальная);	
	КонецЕсли;
	ТабДокумент.Присоединить(ОбластьСуммыНовая);       	

	ОбластьКолонкаТовар = Макет.Область("Товар");
	Если Не ВыводитьКоды Тогда
		ОбластьКолонкаТовар.ШиринаКолонки = ОбластьКолонкаТовар.ШиринаКолонки + 
											Макет.Область("КолонкаКодов").ШиринаКолонки;
	КонецЕсли;
	Если ИспользуетсяРАУЗ Тогда
		ОбластьШапкаТаблицыПервоначальная = Макет.Область("ШапкаТаблицы|Первоначальная");
		Для сч = ОбластьШапкаТаблицыПервоначальная.Лево По ОбластьШапкаТаблицыПервоначальная.Право Цикл
			ОбластьКолонкаТовар.ШиринаКолонки = ОбластьКолонкаТовар.ШиринаКолонки + 
			                                    Макет.Область(ОбластьШапкаТаблицыПервоначальная.Верх, сч, ОбластьШапкаТаблицыПервоначальная.Верх, сч).ШиринаКолонки;	
		КонецЦикла;
	КонецЕсли;

	ОбластьНомера = Макет.ПолучитьОбласть("Строка|НомерСтроки");
	ОбластьКодов  = Макет.ПолучитьОбласть("Строка|КолонкаКодов");
	ОбластьДанных = Макет.ПолучитьОбласть("Строка|Данные");
	ОбластьСуммыСтарая  		= Макет.ПолучитьОбласть("Строка|Старая");
	Если Не ИспользуетсяРАУЗ Тогда
		ОбластьСуммыПервоначальная	= Макет.ПолучитьОбласть("Строка|Первоначальная");
	КонецЕсли;
	ОбластьСуммыНовая		  	= Макет.ПолучитьОбласть("Строка|Новая");

	Для каждого ВыборкаСтрокЗапасов из ЗапросЗапасы Цикл 
	
		Если НЕ ЗначениеЗаполнено(ВыборкаСтрокЗапасов.Номенклатура) Тогда
			Сообщить("В одной из строк не заполнено значение номенклатуры - строка при печати пропущена.", СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;

		ОбластьНомера.Параметры.НомерСтроки = ЗапросЗапасы.Индекс(ВыборкаСтрокЗапасов) + 1;
		ТабДокумент.Вывести(ОбластьНомера);

		Если ВыводитьКоды Тогда
			Если Колонка = "Артикул" Тогда
				ОбластьКодов.Параметры.Артикул = ВыборкаСтрокЗапасов.Артикул;
			Иначе
				ОбластьКодов.Параметры.Артикул = ВыборкаСтрокЗапасов.Код;
			КонецЕсли;
			ТабДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;

		ОбластьДанных.Параметры.Заполнить(ВыборкаСтрокЗапасов);
		ОбластьДанных.Параметры.Номенклатура = СокрП(ВыборкаСтрокЗапасов.Номенклатура) + ФормированиеПечатныхФорм.ПредставлениеСерий(ВыборкаСтрокЗапасов);
		ТабДокумент.Присоединить(ОбластьДанных);

		ОбластьСуммыСтарая.Параметры.Заполнить(ВыборкаСтрокЗапасов);
		ТабДокумент.Присоединить(ОбластьСуммыСтарая);
		Если Не ИспользуетсяРАУЗ Тогда
			ОбластьСуммыПервоначальная.Параметры.Заполнить(ВыборкаСтрокЗапасов);
			ТабДокумент.Присоединить(ОбластьСуммыПервоначальная);
		КонецЕсли;
		ОбластьСуммыНовая.Параметры.Заполнить(ВыборкаСтрокЗапасов);
		ТабДокумент.Присоединить(ОбластьСуммыНовая);
		
	КонецЦикла;

	// Вывести Сумму прописью
	ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьМакета.Параметры.ИтоговаяСтрока = НСтр("ru='Всего наименований ';uk='Всього найменувань '",КодЯзыкаПечать) + 	ЗапросЗапасы.Количество() + ".";
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;

КонецФункции // ПечатьПереоценкиТоваровВРознице()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если Не Проведен Тогда
		Предупреждение("Документ можно распечатать только после его проведения");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не УправлениеЗапасами.ИспользуетсяРасширеннаяАналитикаУчета(КонецМесяца(ПериодРегистрации))
	   И Последовательности.ПартионныйУчетБУ.ПолучитьГраницу(Новый Структура("Организация", Организация)).Дата < КонецМесяца(ПериодРегистрации) Тогда
		Предупреждение("Документ находится за пределами последовательности ""Партионный учет (бухгалтерский)"".
		|Печать может быть выполнена по неактуальным данным.
		|
		|Рекомендуется: 
		| 1. Восстановить последовательность документов ""Партионный учет (бухгалтерский)"" (обработка ""Проведение по партиям"")
		| 2. Выполнить окончательный расчет себестоимости выпущенной продукции (документ ""Расчет себестоимости выпуска"" )
		| 3. Перепровести документ ""Регламентная переоценка запасов""
		| 4. Окончательно восстановить последовательность до конца месяца (обработка ""Проведение по партиям"")");
	КонецЕсли; 
	
	// Получить экземпляр документа на печать
	Если ИмяМакета = "РегламентнаяПереоценкаЗапасов" тогда
		// Получить экземпляр документа на печать
		ТабДокумент = ПечатьПереоценки();
		
	ИначеЕсли ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда
			
		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
			
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
			
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать()

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Вовращаемое значение:
//  Струткура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт

	СтруктПечатныхФорм = Новый Структура;
	
	СтруктПечатныхФорм.Вставить("РегламентнаяПереоценкаЗапасов", "Переоценка запасов");

	Возврат СтруктПечатныхФорм;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

// Процедура выполняет заполнение табличной части. Если передан документ основание то
//  заполнение производится по документу основанию, иначе по всем.
//
Процедура ЗаполнитьТовары() Экспорт

	// Остатки берем на конец месяца, т.к. документ РасчетСебестоимостиВыпуска, после которого
	// проводится РегламентнаяПереоценкаЗапасов, делает движения на конец месяца
	ДатаОстатков = КонецМесяца(ПериодРегистрации);
	
	Если УправлениеЗапасами.ИспользуетсяРасширеннаяАналитикаУчета(ДатаОстатков) Тогда
		ЗаполнитьТоварыРасширеннаяАналитика(ДатаОстатков);
		Возврат;
	КонецЕсли;	
	
	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("Организация", Организация);
	Если глЗначениеПеременной("ПараметрыПартионногоУчета").ВестиПартионныйУчетПоСкладамРегл Тогда
		Запрос.УстановитьПараметр("Склад", Склад);
	КонецЕсли; 
	Запрос.УстановитьПараметр("ДатаОстатков", ДатаОстатков);
			
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПартииТоваров.Номенклатура,
	|	ПартииТоваров.ХарактеристикаНоменклатуры,
	|	ПартииТоваров.СерияНоменклатуры,
	|	ПартииТоваров.СчетУчета,
	|	ПартииТоваров.Качество,
	|	ПартииТоваров.НалоговоеНазначение,
	|	СУММА(ПартииТоваров.СтоимостьОстаток) КАК СтоимостьОстаток,
	|	СУММА(ПартииТоваров.СуммаУценкиОстаток) КАК СуммаУценкиОстаток
	|ИЗ
	|	РегистрНакопления.ПартииТоваровНаСкладахБухгалтерскийУчет.Остатки(&ДатаОстатков, Организация = &Организация
	|		И НЕ СчетУчета.Забалансовый ";
	
	Если глЗначениеПеременной("ПараметрыПартионногоУчета").ВестиПартионныйУчетПоСкладамРегл Тогда
		ТекстЗапроса = ТекстЗапроса + "И Склад = &Склад"
	КонецЕсли; 
	
	ТекстЗапроса = ТекстЗапроса + ") КАК ПартииТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваров.Номенклатура,
	|	ПартииТоваров.ХарактеристикаНоменклатуры,
	|	ПартииТоваров.СерияНоменклатуры,
	|	ПартииТоваров.Качество,
	|	ПартииТоваров.СчетУчета,
	|	ПартииТоваров.НалоговоеНазначение
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		СтрокаТабличнойЧасти = Товары.Добавить();

		СтрокаТабличнойЧасти.Номенклатура = Выборка.Номенклатура;
		СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
		СтрокаТабличнойЧасти.СерияНоменклатуры 			= Выборка.СерияНоменклатуры;
		СтрокаТабличнойЧасти.СчетУчетаБУ				= Выборка.СчетУчета;
		СтрокаТабличнойЧасти.Качество                   = Выборка.Качество;
		СтрокаТабличнойЧасти.НалоговоеНазначение        = Выборка.НалоговоеНазначение;

	КонецЦикла;

КонецПроцедуры // ЗаполнитьТовары()

Процедура ЗаполнитьТоварыРасширеннаяАналитика(ДатаОстатков)
	
	ПараметрыПартионногоУчета = глЗначениеПеременной("ПараметрыПартионногоУчета");
	ВестиПартионныйУчетПоСкладам = ПараметрыПартионногоУчета.ВестиПартионныйУчетПоСкладамРегл;
		
	Если ВестиПартионныйУчетПоСкладам Тогда
		ВремСклад = Склад;
	Иначе
		ВремСклад = Справочники.Склады.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегАналитикаУчетаЗатрат.Затрата КАК Номенклатура,
	|	РегАналитикаУчетаЗатрат.ХарактеристикаЗатраты КАК ХарактеристикаНоменклатуры,
	|	РегАналитикаУчетаЗатрат.СерияЗатраты КАК СерияНоменклатуры,
	|	РегАналитикаВидаУчета.СчетУчета,
	|	РегАналитикаВидаУчета.НалоговоеНазначение,
	|	СУММА(УчетЗатратРеглОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	|	СУММА(УчетЗатратРеглОстатки.СтоимостьОстаток) КАК СтоимостьОстаток
	|ИЗ
	|	РегистрНакопления.УчетЗатратРегл.Остатки(
	|			&ДатаОстатков,
	|			АналитикаВидаУчета В
	|				(ВЫБРАТЬ
	|					АналитикаВидаУчета.Ссылка
	|				ИЗ
	|					РегистрСведений.АналитикаВидаУчета КАК АналитикаВидаУчета
	|				ГДЕ
	|					АналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.МПЗ)
	|					И АналитикаВидаУчета.Склад = &Склад
	|					И АналитикаВидаУчета.Организация = &Организация)) КАК УчетЗатратРеглОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаВидаУчета КАК РегАналитикаВидаУчета
	|		ПО УчетЗатратРеглОстатки.АналитикаВидаУчета = РегАналитикаВидаУчета.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаЗатрат КАК РегАналитикаУчетаЗатрат
	|		ПО УчетЗатратРеглОстатки.АналитикаУчетаЗатрат = РегАналитикаУчетаЗатрат.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РегАналитикаУчетаЗатрат.Затрата,
	|	РегАналитикаУчетаЗатрат.ХарактеристикаЗатраты,
	|	РегАналитикаУчетаЗатрат.СерияЗатраты,
	|	РегАналитикаВидаУчета.СчетУчета,
	|	РегАналитикаВидаУчета.НалоговоеНазначение
	|АВТОУПОРЯДОЧИВАНИЕ";
		
	Запрос.УстановитьПараметр("ДатаОстатков", ДатаОстатков);		
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Склад",        ВремСклад);
    Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		СтрокаТабличнойЧасти = Товары.Добавить();

		СтрокаТабличнойЧасти.Номенклатура = Выборка.Номенклатура;
		СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
		СтрокаТабличнойЧасти.СерияНоменклатуры 			= Выборка.СерияНоменклатуры;
		СтрокаТабличнойЧасти.СчетУчетаБУ				= Выборка.СчетУчета;
		СтрокаТабличнойЧасти.НалоговоеНазначение        = Выборка.НалоговоеНазначение;
		СтрокаТабличнойЧасти.Качество                   = Неопределено;  // Не используется в БУ РАУЗ
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Процедура формирует структуру шапки документа и дополнительных полей.
//
// Параметры
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//	Отказ - Булево - Признак отказа от проведения документа
//	Заголовок - Строка - Текст представления документа 
//
//
Процедура ПодготовитьСтруктуруШапкиДокумента(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	// Данная операция имеет отношенин только к БУ, и никак не отражается по налогу на прибыль
	Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
		СтруктураШапкиДокумента.Вставить("НалоговоеНазначениеДоходовИЗатрат", Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность);
	Иначе
		СтруктураШапкиДокумента.Вставить("НалоговоеНазначениеДоходовИЗатрат", Справочники.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка());		
	КонецЕсли;
	
	// В "Периоде" движений документа "Расчет себестоимости выпуска" всегда конец месяца.
	// Данный документ может проводится только после расчета себестоимости
	СтруктураШапкиДокумента.Вставить("Дата", КонецМесяца(ПериодРегистрации));	
	
	// ВНИМАНИЕ! Данное условие неообходимо для нужд РАУЗ. Аналогичное условие находится в общем модуле "Управление запасами партионный учет" для проведения в традиционном режиме
	ПриходоватьВ = УправлениеЗапасамиПартионныйУчет.ПолучитьНаправлениеСписанияПоКодуОперации(Перечисления.КодыОперацийПартииТоваров.ПереоценкаКупленных, СтруктураШапкиДокумента.СтатьяЗатрат);
	ОтражатьПоЗатратам = (ПриходоватьВ = "ОбщепроизводственныеРасходы") ИЛИ
	                     (ПриходоватьВ = "АдминистративныеРасходы") ИЛИ
						 (ПриходоватьВ = "РасходыНаСбыт") ИЛИ
						 (ПриходоватьВ = "ПрочиеОперационныеРасходы");
	СтруктураШапкиДокумента.Вставить("ОтражатьПоЗатратам", ОтражатьПоЗатратам);	
	
КонецПроцедуры

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
// Параметры: 
//  РезультатЗапросаПоТоварам - результат запроса по табличной части "Товары",
//  СтруктураШапкиДокумента   - выборка по результату запроса по шапке документа.
//
// Возвращаемое значение:
//  Сформированная таблиица значений.
//
Функция ПодготовитьТаблицуТоваров(РезультатЗапросаПоТоварам, СтруктураШапкиДокумента)

	ТаблицаТоваров = РезультатЗапросаПоТоварам.Выгрузить();
	
	ТаблицаТоваров.Колонки.Добавить("НалоговоеНазначениеДоходовИЗатрат");
	ТаблицаТоваров.Колонки.Добавить("ВидНалоговойДеятельностиДоходовИЗатрат");
	ТаблицаТоваров.Колонки.Добавить("ВидДеятельностиНДСДоходовИЗатрат");
	
	ТаблицаТоваров.ЗаполнитьЗначения(СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат, "НалоговоеНазначениеДоходовИЗатрат");
	ТаблицаТоваров.ЗаполнитьЗначения(СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат.ВидНалоговойДеятельности, "ВидНалоговойДеятельностиДоходовИЗатрат");	
	ТаблицаТоваров.ЗаполнитьЗначения(СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат.ВидДеятельностиНДС      , "ВидДеятельностиНДСДоходовИЗатрат");	

	Возврат ТаблицаТоваров;

КонецФункции // ПодготовитьТаблицуТоваров()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Организация, ПериодРегистрации, СчетУчетаЗатрат, СчетУчетаДоходов");
	
	Если СтруктураШапкиДокумента.ВестиПартионныйУчетПоСкладамБух Тогда
		СтруктураОбязательныхПолей.Вставить("Склад");
	КонецЕсли;

	// Теперь позовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
	
	СписокДопустимыхЗатрат = Новый Соответствие;
	
	СписокДопустимыхЗатрат.Вставить(Перечисления.ХарактерЗатрат.Прочие,Перечисления.ХарактерЗатрат.Прочие);
	СписокДопустимыхЗатрат.Вставить(Перечисления.ХарактерЗатрат.ПрочиеОперационныеРасходы,Перечисления.ХарактерЗатрат.ПрочиеОперационныеРасходы);
	
	Если СписокДопустимыхЗатрат[СтруктураШапкиДокумента.СтатьяЗатрат.ХарактерЗатрат] = Неопределено Тогда
		ОбщегоНазначения.СообщитьОбОшибке("В качестве затрат могут выступать только статьи затрат, имеющие характер 
		|""Прочие"" или ""Прочие операционные затраты"" (закладка ""Бухгалтерский учет"")!", Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения строк табличной части "Товары".
//
// Параметры:
// Параметры: 
//  ТаблицаПоТоварам        - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении.
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиТовары(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок)

	ИмяТабличнойЧасти = "Товары";

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Номенклатура, Цена");
	Если Не СтруктураШапкиДокумента.ИспользоватьРАУЗ Тогда
		СтруктураОбязательныхПолей.Вставить("Качество");
	КонецЕсли;

	// Теперь позовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Товары", СтруктураОбязательныхПолей, Отказ, Заголовок);

	// Здесь услуг быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетУслуг(ЭтотОбъект, "Товары", ТаблицаПоТоварам, Отказ, Заголовок);

	// Здесь наборов быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетНаборов(ЭтотОбъект, "Товары", ТаблицаПоТоварам, Отказ, Заголовок);
	
	//Счета учета номенклатуры и затрат
	СчетаУчетаВДокументах.ПроверитьСчетаУчетаТабличнойЧасти("Товары", ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок);
	
	
КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// Процедура определяет параметры учетной политики
//
Процедура ПодготовитьПараметрыУчетнойПолитики(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	ПодготовитьПараметрыУчетнойПолитикиРегл(СтруктураШапкиДокумента, Отказ, Заголовок);
	
КонецПроцедуры // ПодготовитьПараметрыУчетнойПолитики()


// Процедура определяет параметры регл. учетной политики
//
Процедура ПодготовитьПараметрыУчетнойПолитикиРегл(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	// Прежде всего, проверим заполнение реквизита Организация в шапке документа
	СтруктураОбязательныхПолей = Новый Структура("Организация");
	// Теперь позовем общую процедуру проверки
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
	// Организация не заполнена, получать учетную политику нет смысла
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Бухгалтерский учет
	мУчетнаяПолитикаБУ = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(КонецМесяца(ПериодРегистрации), Организация, , Заголовок);
	Если НЕ Отказ Тогда
		СтруктураШапкиДокумента.Вставить("ВестиПартионныйУчетПоСкладамБух", глЗначениеПеременной("ПараметрыПартионногоУчета").ВестиПартионныйУчетПоСкладамРегл);
	КонецЕсли;
		
КонецПроцедуры // ПодготовитьПараметрыУчетнойПолитикиРегл()

Процедура ДвиженияПоРегиструСписанныеТовары(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок)

	Если СтруктураШапкиДокумента.ИспользоватьРАУЗ Тогда 
		Возврат; 
	КонецЕсли;	
	
	// ТОВАРЫ ПО РЕГИСТРУ СписанныеТовары.

	НаборДвижений = Движения.СписанныеТовары;

	// Получим таблицу значений, совпадающую со структурой набора записей регистра.
	ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();

	// Поступление с новой стоимостью (колонка "СтоимостьПоступление")
	ТаблицаПоТоварам.Колонки.Цена.Имя = "СтоимостьПоступлениеБУ"; // сюда будем записыввать новую цену 

	// Заполним таблицу движений.
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоТоварам, ТаблицаДвижений);

	// Недостающие поля.
	Инд = 0;
	Для каждого Строка Из ТаблицаДвижений Цикл
		Инд = Инд+1;
		Строка.НомерСтрокиДокумента = Инд;
	КонецЦикла;

	ТаблицаДвижений.ЗаполнитьЗначения(Склад,"Склад");

	ТаблицаДвижений.ЗаполнитьЗначения(КонецМесяца(ПериодРегистрации),"Период");
	ТаблицаДвижений.ЗаполнитьЗначения(Ссылка,"Регистратор");
	ТаблицаДвижений.ЗаполнитьЗначения(Истина,"Активность");

	
	ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийПартииТоваров.ПереоценкаКупленных,"КодОперацииПартииТоваров");
	ТаблицаДвижений.ЗаполнитьЗначения(Организация,    "Организация");
	
	ТаблицаДвижений.ЗаполнитьЗначения(ОтражатьВБухгалтерскомУчете	, "ОтражатьВБухгалтерскомУчете");
	
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.СчетУчетаЗатрат,"СчетРасходовБУ");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.Субконто1,"СубконтоРасходовБУ1");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.Субконто2,"СубконтоРасходовБУ2");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.Субконто3,"СубконтоРасходовБУ3");
	
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.СчетУчетаДоходов,"СчетДоходовБУ");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.СубконтоКт1,"СубконтоДоходовБУ1");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.СубконтоКт2,"СубконтоДоходовБУ2");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.СубконтоКт3,"СубконтоДоходовБУ3");
	
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.НоменклатурнаяГруппа,"НоменклатурнаяГруппа");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ПодразделениеОрганизации,"ПодразделениеОрганизации");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.СтатьяЗатрат,"СтатьяЗатрат");

	Для каждого Строка Из ТаблицаДвижений Цикл		
		СтрокаТЧ = ТаблицаПоТоварам.Получить(ТаблицаДвижений.Индекс(Строка));		
		Строка.НалоговоеНазначениеПоФакту = СтрокаТЧ.НалоговоеНазначение;
	КонецЦикла;
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат, "НалоговоеНазначениеНовое");

	НаборДвижений.мПериод            = КонецМесяца(ПериодРегистрации);
	НаборДвижений.мТаблицаДвижений   = ТаблицаДвижений;

	Если Не Отказ Тогда
		Движения.СписанныеТовары.ВыполнитьДвижения();
	КонецЕсли;
	
	Движения.СписанныеТовары.Записать(Истина);

КонецПроцедуры // ДвиженияПоРегиструСписанныеТовары()

// По результату запроса по шапке документа формируем движения по регистрам.
//
// Параметры: 
//  РежимПроведения           - режим проведения документа (оперативный или неоперативный),
//  СтруктураШапкиДокумента   - выборка из результата запроса по шапке документа,
//  ТаблицаПоТоварам          - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  ТаблицаПоТаре             - таблица значений, содержащая данные для проведения и проверки ТЧ "Возвратная тара",
//  Отказ                     - флаг отказа в проведении,
//  Заголовок                 - строка, заголовок сообщения об ошибке проведения.
//
Процедура ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);

	ДвиженияПоРегиструСписанныеТовары(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);

	// 1. Зарегистрируем документ в последовательности на конец месяца. 
	//    Документ РегламентнаяПереоценкаЗапасов может проводится только после
	//    полного расчета себестоимости. 
	// 2. Документ РасчетСебестоимостиВыпуска регистрируется в последовательности 
	//    "ПартионныйУчетБУ" на конец месяца, как следствие этот документ регистрируется
	//    аналогично.
	// 3. Т.к. документ РасчетСебестоимостиВыпуска не перепроводится при  
	//    восстановлении последовательности, т.о. нет необходимости контроллировать
	//    последовательность проведения документов зарегистрированных
	//    в последовательности с одним периодом (конец месяца)
	УправлениеЗапасами.ЗарегистрироватьДокументВПоследовательностяхПартионногоУчета(
		ЭтотОбъект, 
		КонецМесяца(ПериодРегистрации), 
		СтруктураШапкиДокумента.Организация,
		СтруктураШапкиДокумента.ОтражатьВУправленческомУчете,
		СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете
	);
	
	// Выполним проведение, при этом отключив стандартный сдвиг границы последовательности
	// ПартионныйУчетБУ аналогично документу РасчетСебестоимостиВыпуска
	УправлениеЗапасамиПартионныйУчет.ДвижениеПартийТоваров(Ссылка, Движения.СписанныеТовары.Выгрузить());	
	
	// Удалим информацию обеспечивающую перепроведение обработкой "Проведение по партиям".
	// Позволим обработке только смещение границы последовательности аналогично документу РасчетСебестоимостиВыпуска
	Если Не СтруктураШапкиДокумента.ИспользоватьРАУЗ Тогда 
		Движения.СписанныеТовары.Очистить();
		Движения.СписанныеТовары.Записать(Истина);
	КонецЕсли;
	
КонецПроцедуры // ДвиженияПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура вызывается перед записью документа 
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	мУдалятьДвижения = НЕ ЭтоНовый();
	
	// всегда отражаем в бухучете
	ОтражатьВБухгалтерскомУчете = Истина;
	ОтражатьВУправленческомУчете = Ложь;

	НалоговыйУчет.ЗаполнитьНалоговыеНазначенияВТабличныхЧастяхПередЗаписьюДокумента(
		"Списание",
		Дата,
		Организация,
		Товары,
	);

КонецПроцедуры // ПередЗаписью

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// Дерево значений, содержащее имена необходимых полей в запросе по шапке.
	Перем ДеревоПолейЗапросаПоШапке;
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
	ПодготовитьСтруктуруШапкиДокумента(СтруктураШапкиДокумента, Отказ, Заголовок);
    
	// Заполним по шапке документа дерево параметров, нужных при проведении.
	ДеревоПолейЗапросаПоШапке      = УправлениеЗапасами.СформироватьДеревоПолейЗапросаПоШапке();

	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = УправлениеЗапасами.СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета);

	// Получим данные учетной политики
	ПодготовитьПараметрыУчетнойПолитики(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	// Проверим правильность заполнения шапки документа
	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Получим необходимые данные для проведения и проверки заполенения данные по табличной части "Товары".
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Номенклатура"              , "Номенклатура");
	СтруктураПолей.Вставить("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
	СтруктураПолей.Вставить("СерияНоменклатуры"         , "СерияНоменклатуры");
	СтруктураПолей.Вставить("Качество"         			, "Качество");
	СтруктураПолей.Вставить("Услуга"                    , "Номенклатура.Услуга");
	СтруктураПолей.Вставить("Набор"                     , "Номенклатура.Набор");
	СтруктураПолей.Вставить("Цена"         				, "Цена");
	СтруктураПолей.Вставить("СчетУчетаБУ"         		, "СчетУчетаБУ");
	СтруктураПолей.Вставить("НалоговоеНазначение"       , "НалоговоеНазначение"); 
	СтруктураПолей.Вставить("ВидДеятельностиНДС"        , "НалоговоеНазначение.ВидДеятельностиНДС");
	СтруктураПолей.Вставить("ВидНалоговойДеятельности"  , "НалоговоеНазначение.ВидНалоговойДеятельности");

	РезультатЗапросаПоТоварам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Товары", СтруктураПолей);

	// Подготовим таблицу товаров для проведения.
	ТаблицаПоТоварам = ПодготовитьТаблицуТоваров(РезультатЗапросаПоТоварам, СтруктураШапкиДокумента);

	// Проверить заполнение ТЧ "Товары".
	ПроверитьЗаполнениеТабличнойЧастиТовары(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок);

	// Движения по документу
	Если Не Отказ Тогда
		ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);
	КонецЕсли;

	// Сделаем переменные доступными из подписок на события (РАУЗ)
	ДополнительныеСвойства.Вставить("СтруктураШапкиДокумента", СтруктураШапкиДокумента);
	// Основой для отражения оборотов по затратам является таблица товаров
	ДополнительныеСвойства.Вставить("СтруктураТабличныхЧастей", Новый Структура("ТаблицаПоТоварам, ТаблицаПоПоступлениюЗатрат, ТаблицаПоСписаниюЗатрат", ТаблицаПоТоварам, ТаблицаПоТоварам, ТаблицаПоТоварам));
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ОбработкаЗаполнения(Основание)
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаКачестваТоваров") Тогда
		
		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);

		Запрос = Новый Запрос;
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	КорКачества.Номенклатура,
		               |	КорКачества.СерияНоменклатуры,
		               |	КорКачества.СчетУчетаБУ,
		               |	КорКачества.ХарактеристикаНоменклатуры,
		               |	КорКачества.КачествоНовое
		               |ИЗ
		               |	Документ.КорректировкаКачестваТоваров.Товары КАК КорКачества
		               |
		               |ГДЕ
		               |	КорКачества.КачествоНовое = &Качество И
		               |	КорКачества.Ссылка = &Основание";
		
		Запрос.Текст = ТекстЗапроса;
		
		Запрос.УстановитьПараметр("Качество",Справочники.Качество.Новый);
		Запрос.УстановитьПараметр("Основание",Основание);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Номенклатура 				= Выборка.Номенклатура;
			НоваяСтрока.СерияНоменклатуры 			= Выборка.СерияНоменклатуры;
			НоваяСтрока.СчетУчетаБУ 				= Выборка.СчетУчетаБУ;
			НоваяСтрока.Качество 					= Выборка.КачествоНовое;
			НоваяСтрока.ХарактеристикаНоменклатуры 	= Выборка.ХарактеристикаНоменклатуры;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");