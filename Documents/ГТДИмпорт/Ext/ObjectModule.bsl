Перем мУдалятьДвижения;

Перем мВалютаРегламентированногоУчета Экспорт;

// Хранит схему налогообложения текущей организации 
Перем мТекущаяСхемаНалогообложения Экспорт; 

Перем мКоэффициентПропорциональногоНДС Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

Процедура ЗаполнитьСтатьюКнигиПриобретенияРегл(СтрокаТЧ) Экспорт
	
	Раздел = Разделы[СтрокаТЧ.НомерРаздела - 1];
	
	// Определим, имеется ли право на налоговый кредит	
	Если НалоговыйУчет.ЕстьНалоговыйКредит(СтрокаТЧ) Тогда
		Если ОформленНалоговыйВексельПоНДС Тогда
			Если Раздел.СтавкаНДС =  Перечисления.СтавкиНДС.НДС0 
			ИЛИ Раздел.СтавкаНДС = Перечисления.СтавкиНДС.НДС7
			ИЛИ Раздел.СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Тогда
				// ст 6 Декларации
				СтрокаТЧ.СтатьяКнигиПриобретения = Справочники.СтатьиНалоговыхДеклараций.НДС_НОИмпортВексель;
			КонецЕсли; 
		Иначе
			Если Раздел.СтавкаНДС =  Перечисления.СтавкиНДС.НДС0 
			ИЛИ Раздел.СтавкаНДС = Перечисления.СтавкиНДС.НДС7
			ИЛИ Раздел.СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Тогда
				// ст 12.1 Декларации
				СтрокаТЧ.СтатьяКнигиПриобретения = Справочники.СтатьиНалоговыхДеклараций.НДС_НКИмпортВРОблагНДСТамож;
			Иначе
				// ст 12.3 Декларации
				СтрокаТЧ.СтатьяКнигиПриобретения = Справочники.СтатьиНалоговыхДеклараций.НДС_НКИмпортВРОблагБезНДС;
			КонецЕсли; 
		КонецЕсли; 
	Иначе
		Если ОформленНалоговыйВексельПоНДС Тогда
			Если Раздел.СтавкаНДС =  Перечисления.СтавкиНДС.НДС0 
			ИЛИ Раздел.СтавкаНДС = Перечисления.СтавкиНДС.НДС7
			ИЛИ Раздел.СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Тогда
				// ст 6 Декларации
				Если Дата >='20110301' Тогда
				    СтрокаТЧ.СтатьяКнигиПриобретения = Справочники.СтатьиНалоговыхДеклараций.ПустаяСсылка();
				Иначе
					СтрокаТЧ.СтатьяКнигиПриобретения = Справочники.СтатьиНалоговыхДеклараций.НДС_НОИмпортВексель;
				КонецЕсли;	
			КонецЕсли; 
		Иначе
			Если Раздел.СтавкаНДС =  Перечисления.СтавкиНДС.НДС0 
			ИЛИ Раздел.СтавкаНДС = Перечисления.СтавкиНДС.НДС7
			ИЛИ Раздел.СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Тогда
				// ст 13.1.1 Декларации
				СтрокаТЧ.СтатьяКнигиПриобретения = Справочники.СтатьиНалоговыхДеклараций.НДС_НКИмпортВРНеОблагНеОбъектНДСТамож;
			Иначе
				// ст 13.1.2 Декларации
				СтрокаТЧ.СтатьяКнигиПриобретения = Справочники.СтатьиНалоговыхДеклараций.НДС_НКИмпортВРНеОблагНеОбъектБезНДС;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать()

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА
Процедура ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл(СтрокаТЧ, ИмяТабЧасти, ЗаполнятьБУ) Экспорт
	
	ЗаполнитьСчетаУчетаВТабЧасти(СтрокаТЧ, ИмяТабЧасти, ЗаполнятьБУ);
	
    ЗаполнитьСтатьюКнигиПриобретенияРегл(СтрокаТЧ);

КонецПроцедуры // ЗаполнитьСчетаУчетаВСтрокеТабЧасти()

// Заполняет счета БУ и НУ в табличной части документа
//
Процедура ЗаполнитьСчетаУчетаВТабЧасти(ТабличнаяЧасть, ИмяТабЧасти, ЗаполнятьБУ) Экспорт
	
	// Счета заполняются в любом режиме
	СчетаУчетаВДокументах.ЗаполнитьСчетаУчетаТабличнойЧасти(ИмяТабЧасти, ТабличнаяЧасть, ЭтотОбъект, ЗаполнятьБУ, , , Истина);
	
КонецПроцедуры // ЗаполнитьСчетаУчетаВТабЧасти()

// Добавляет строку в табличную часть "Разделы"
//
// Возвращаемое значение:
//   строка табличной части, которую добавили.
//
Функция ДобавитьРаздел() Экспорт

#Если Клиент Тогда
	СтавкаПошлины = ВосстановитьЗначение("СтавкаТаможеннойПошлины");
	СтавкаАкциза  = ВосстановитьЗначение("СтавкаАкциза");
#Иначе
	СтавкаПошлины = 0;
	СтавкаАкциза  = 0;
#КонецЕсли

	НовыйРаздел = Разделы.Добавить();
	НовыйРаздел.СтавкаНДС      = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяСтавкаНДС");
	НовыйРаздел.СтавкаПошлины  = СтавкаПошлины;
	НовыйРаздел.СтавкаАкциза   = СтавкаАкциза;

	Возврат НовыйРаздел;

КонецФункции // ДобавитьРаздел()

// Вычисляет суммы по данным раздела.
//
// Параметры
//  НомерРаздела   - число, номер раздела по которому надо получить итоги,
//  ВсегоСтоимость - число, в этот параметр будет возвращена сумма фактурной стоимости,
//  ВсегоПошлина   - число, в этот параметр будет возвращена сумма пошлины, 
//  ВсегоНДС       - число, в этот параметр будет возвращена сумма НДС.
//
Процедура ПосчитатьИтогиПоРазделу(НомерРаздела, ВсегоСтоимость, ВсегоПошлина, ВсегоАкциз, ВсегоНДС)  Экспорт

	ВсегоСтоимость = 0;
	ВсегоПошлина   = 0;
	ВсегоАкциз     = 0;
	ВсегоНДС       = 0;
	МассивСтрок = Товары.НайтиСтроки(Новый Структура("НомерРаздела", НомерРаздела));
	Для каждого ЭлементМассива Из МассивСтрок Цикл
		ВсегоСтоимость = ВсегоСтоимость + ЭлементМассива.ФактурнаяСтоимость;
		ВсегоПошлина   = ВсегоПошлина   + ЭлементМассива.СуммаПошлины;
		ВсегоАкциз     = ВсегоАкциз     + ЭлементМассива.СуммаАкциза;
		ВсегоНДС       = ВсегоНДС       + ЭлементМассива.СуммаНДС;
	КонецЦикла;
	МассивСтрок = Оборудование.НайтиСтроки(Новый Структура("НомерРаздела", НомерРаздела));
	Для каждого ЭлементМассива Из МассивСтрок Цикл
		ВсегоСтоимость = ВсегоСтоимость + ЭлементМассива.ФактурнаяСтоимость;
		ВсегоПошлина   = ВсегоПошлина   + ЭлементМассива.СуммаПошлины;
		ВсегоАкциз     = ВсегоАкциз     + ЭлементМассива.СуммаАкциза;
		ВсегоНДС       = ВсегоНДС       + ЭлементМассива.СуммаНДС;
	КонецЦикла;

КонецПроцедуры // ПосчитатьИтогиПоРазделу()

Процедура РассчитатьСуммыПошлиныГТДИАкциза(Раздел)
	
	ВсегоСтоимость = 0;
	ВсегоАкциз     = 0;
	ВсегоПошлина   = 0;
	ВсегоНДС       = 0;
	
	НомерРаздела = Разделы.Индекс(Раздел) + 1;
	
	ПосчитатьИтогиПоРазделу(НомерРаздела, ВсегоСтоимость, ВсегоПошлина, ВсегоАкциз, ВсегоНДС);

	Раздел.ТаможеннаяСтоимость = ВсегоСтоимость;	
	Раздел.СуммаПошлины = Раздел.ТаможеннаяСтоимость * Раздел.СтавкаПошлины / 100;
   	Раздел.СуммаАкциза  = Раздел.ТаможеннаяСтоимость * Раздел.СтавкаАкциза  / 100;

	БазаНДС = Раздел.ТаможеннаяСтоимость + Раздел.СуммаПошлины + Раздел.СуммаАкциза;
	Раздел.СуммаНДС = Ценообразование.РассчитатьСуммуНДС(БазаНДС, Истина, Ложь, Ценообразование.ПолучитьСтавкуНДС(Раздел.СтавкаНДС));  // УчитыватьНДС, Сумма не включает НДС.
	
	МассивСтрок  = Товары.НайтиСтроки(Новый Структура("НомерРаздела", НомерРаздела));
	МассивСтрокОборудование = Оборудование.НайтиСтроки(Новый Структура("НомерРаздела", НомерРаздела));
	// добавим оборудование в общий массив
	Для каждого ЭлементМассива Из МассивСтрокОборудование Цикл
		МассивСтрок.Добавить(ЭлементМассива);
	КонецЦикла;
	РаспределитьСуммы(Раздел.СуммаПошлины, Раздел.СуммаАкциза, Раздел.СуммаНДС, НомерРаздела, МассивСтрок);
	
КонецПроцедуры

Процедура РаспределитьСуммы(СуммаПошлины, СуммаАкциза, СуммаНДС, НомерРаздела, МассивСтрок) Экспорт

	СтрокаМаксимальнойСуммы  = Неопределено; // На эту строку будем относить остаток после распределения (ошибки округления)
	МаксимальнаяСумма        = 0; // Значение максимальной суммы.
	НепогашеннаяСуммаПошлины = СуммаПошлины;
	НепогашеннаяСуммаАкциза  = СуммаАкциза;
	НепогашеннаяСуммаНДС     = СуммаНДС;
	
	ВсегоСтоимость = 0;
	Для каждого ЭлементМассива Из МассивСтрок Цикл
		ВсегоСтоимость = ВсегоСтоимость + ЭлементМассива.ФактурнаяСтоимость;
	КонецЦикла;

	Для каждого ЭлементМассива Из МассивСтрок Цикл
		
        ДельтаПошлины = СуммаПошлины * ЭлементМассива.ФактурнаяСтоимость / ВсегоСтоимость;
		
		// Если Дельта по модулю оказалась больше, чем осталось погасить
		Если ДельтаПошлины < 0 Тогда
			ДельтаПошлины = Макс(НепогашеннаяСуммаПошлины, ДельтаПошлины)
		Иначе
			ДельтаПошлины = Мин(НепогашеннаяСуммаПошлины, ДельтаПошлины)
		КонецЕсли; 

        ДельтаАкциза = СуммаАкциза * ЭлементМассива.ФактурнаяСтоимость / ВсегоСтоимость;
		
		// Если Дельта по модулю оказалась больше, чем осталось погасить
		Если ДельтаПошлины < 0 Тогда
			ДельтаАкциза = Макс(НепогашеннаяСуммаАкциза, ДельтаАкциза)
		Иначе
			ДельтаАкциза = Мин(НепогашеннаяСуммаАкциза, ДельтаАкциза)
		КонецЕсли;
		
		ДельтаНДС = СуммаНДС * ЭлементМассива.ФактурнаяСтоимость / ВсегоСтоимость;

		// Если Дельта по модулю оказалась больше, чем осталось погасить
		Если ДельтаНДС < 0 Тогда
			ДельтаНДС = Макс(НепогашеннаяСуммаНДС, ДельтаНДС)
		Иначе
			ДельтаНДС = Мин(НепогашеннаяСуммаНДС, ДельтаНДС)
		КонецЕсли; 

		// Проверим текущую сумму на максимум.
		Если ЭлементМассива.ФактурнаяСтоимость > МаксимальнаяСумма  Тогда
			МаксимальнаяСумма       = ЭлементМассива.ФактурнаяСтоимость;
			СтрокаМаксимальнойСуммы = ЭлементМассива;
		КонецЕсли;

		// Заполняем значения.
		ЭлементМассива.СуммаПошлины = ДельтаПошлины;
		
		// Остаток нераспределенной суммы надо уменьшать на реальное изменение
		НепогашеннаяСуммаПошлины = НепогашеннаяСуммаПошлины - ЭлементМассива.СуммаПошлины;

		ЭлементМассива.СуммаАкциза = ДельтаАкциза;
		
		// Остаток нераспределенной суммы надо уменьшать на реальное изменение
		НепогашеннаяСуммаАкциза = НепогашеннаяСуммаАкциза - ЭлементМассива.СуммаАкциза;
		
		ЭлементМассива.СуммаНДС = ДельтаНДС;
		
		// Остаток нераспределенной суммы надо уменьшать на реальное изменение
		НепогашеннаяСуммаНДС = НепогашеннаяСуммаНДС - ЭлементМассива.СуммаНДС;

	КонецЦикла;
		
	// Если что-то осталось, кидаем на строку с максимальной суммой.
	Если НепогашеннаяСуммаПошлины > 0
	   И СтрокаМаксимальнойСуммы <> Неопределено Тогда

		СтрокаМаксимальнойСуммы.СуммаПошлины = СтрокаМаксимальнойСуммы.СуммаПошлины + НепогашеннаяСуммаПошлины;
	КонецЕсли;
	
	// Если что-то осталось, кидаем на строку с максимальной суммой.
	Если НепогашеннаяСуммаАкциза > 0
	   И СтрокаМаксимальнойСуммы <> Неопределено Тогда

		СтрокаМаксимальнойСуммы.СуммаАкциза = СтрокаМаксимальнойСуммы.СуммаАкциза + НепогашеннаяСуммаАкциза;
	КонецЕсли;
	
	// Если что-то осталось, кидаем на строку с максимальной суммой.
	Если НепогашеннаяСуммаНДС > 0
	   И СтрокаМаксимальнойСуммы <> Неопределено Тогда

		СтрокаМаксимальнойСуммы.СуммаНДС = СтрокаМаксимальнойСуммы.СуммаНДС + НепогашеннаяСуммаНДС;
	КонецЕсли;

КонецПроцедуры //  КоманднаяПанельТоварыРаспределить()


Процедура РассчитатьПропорциональныйНДС() Экспорт
	
	Если (НЕ мТекущаяСхемаНалогообложения.НДС = Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Если мКоэффициентПропорциональногоНДС = Неопределено Тогда
		
		мКоэффициентПропорциональногоНДС = НалоговыйУчетПовтИсп.ПолучитьКоэффициентПропорциональногоНДС(Организация, Дата);
	
	КонецЕсли;
	
	СуммаНДСПропорциональноВсего = 0;
	
	СтрокиПропорциональногоНДС = Товары.НайтиСтроки(Новый Структура("НалоговоеНазначение", Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально));
	Для каждого СтрокаПропорциональногоНДС Из СтрокиПропорциональногоНДС Цикл
		СуммаНДСПропорциональноВсего = СуммаНДСПропорциональноВсего + СтрокаПропорциональногоНДС.СуммаНДС;
	КонецЦикла;
	
	СтрокиПропорциональногоНДС = Оборудование.НайтиСтроки(Новый Структура("НалоговоеНазначение", Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально));
	Для каждого СтрокаПропорциональногоНДС Из СтрокиПропорциональногоНДС Цикл
		СуммаНДСПропорциональноВсего = СуммаНДСПропорциональноВсего + СтрокаПропорциональногоНДС.СуммаНДС;	
	КонецЦикла;
	
	СуммаНДСПропорциональноКредит = СуммаНДСПропорциональноВсего * мКоэффициентПропорциональногоНДС;
	
КонецПроцедуры // РассчитатьПропорциональныйНДС

// Заполнение раздела документа "ГТД по импорту" по документам поступления
//
// Параметры
//  ДокументПоступления - ссылка на документ ПоступлениеТоваровУслуг, определяет документ поступления, по которому надо заполнить этот документ,
//  НомерРаздела        - число, номер раздела, который надо заполнить.
//
Процедура ЗаполнитьПоПоступлению(ДокументПоступления, НомерРаздела) Экспорт
	ТаблицаЗначенийТовары = ДокументПоступления.Товары.Выгрузить();
	Для каждого СтрокаТаблицыЗначений Из ТаблицаЗначенийТовары Цикл

		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.НомерРаздела               = НомерРаздела;
		НоваяСтрока.ДокументПартии             = ДокументПоступления;
		НоваяСтрока.ЕдиницаИзмерения           = СтрокаТаблицыЗначений.ЕдиницаИзмерения;
		НоваяСтрока.ЕдиницаИзмеренияМест       = СтрокаТаблицыЗначений.ЕдиницаИзмеренияМест;
		НоваяСтрока.ЗаказПокупателя            = СтрокаТаблицыЗначений.Заказ;
		НоваяСтрока.Количество                 = СтрокаТаблицыЗначений.Количество;
		НоваяСтрока.КоличествоМест             = СтрокаТаблицыЗначений.КоличествоМест;
		НоваяСтрока.Коэффициент                = СтрокаТаблицыЗначений.Коэффициент;
		НоваяСтрока.Номенклатура               = СтрокаТаблицыЗначений.Номенклатура;
		НоваяСтрока.СерияНоменклатуры          = СтрокаТаблицыЗначений.СерияНоменклатуры;
		НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТаблицыЗначений.ХарактеристикаНоменклатуры;
		НоваяСтрока.ФактурнаяСтоимость         = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаТаблицыЗначений.Сумма, ДокументПоступления.ВалютаДокумента,
			                                               ВалютаДокумента, 
			                                               ЗаполнениеДокументов.КурсДокумента(ДокументПоступления, мВалютаРегламентированногоУчета),
			                                               КурсДокумента,
			                                               ЗаполнениеДокументов.КратностьДокумента(ДокументПоступления, мВалютаРегламентированногоУчета),
			                                               КратностьДокумента);
		
		// Заполним бухгалтерские и налоговые реквизиты
		НоваяСтрока.СчетУчетаБУ             	= СтрокаТаблицыЗначений.СчетУчетаБУ;
		НоваяСтрока.НалоговоеНазначение         = СтрокаТаблицыЗначений.НалоговоеНазначение;
			
		НоваяСтрока.СчетУчетаНДС 				= ПланыСчетов.Хозрасчетный.НалоговыеОбязательства;

		ЗаполнитьСтатьюКнигиПриобретенияРегл(НоваяСтрока);
	КонецЦикла;

	Если ДокументПоступления.Метаданные().ТабличныеЧасти.Найти("Оборудование") = Неопределено Тогда
		// нет ТЧ Оборудование
		Возврат;
	КонецЕсли;
		
	ТаблицаЗначенийОборудование = ДокументПоступления.Оборудование.Выгрузить();
	Для каждого СтрокаТаблицыЗначений Из ТаблицаЗначенийОборудование Цикл
		
		НоваяСтрока = Оборудование.Добавить();
		НоваяСтрока.НомерРаздела               = НомерРаздела;
		НоваяСтрока.ДокументПартии             = ДокументПоступления;
		НоваяСтрока.ЕдиницаИзмерения           = СтрокаТаблицыЗначений.ЕдиницаИзмерения;
		НоваяСтрока.ЕдиницаИзмеренияМест       = СтрокаТаблицыЗначений.ЕдиницаИзмеренияМест;
		НоваяСтрока.Количество                 = СтрокаТаблицыЗначений.Количество;
		НоваяСтрока.КоличествоМест             = СтрокаТаблицыЗначений.КоличествоМест;
		НоваяСтрока.Коэффициент                = СтрокаТаблицыЗначений.Коэффициент;
		НоваяСтрока.Номенклатура               = СтрокаТаблицыЗначений.Номенклатура;
		НоваяСтрока.СерияНоменклатуры          = СтрокаТаблицыЗначений.СерияНоменклатуры;
		НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТаблицыЗначений.ХарактеристикаНоменклатуры;
		НоваяСтрока.ФактурнаяСтоимость         = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаТаблицыЗначений.Сумма, ДокументПоступления.ВалютаДокумента,
													   ВалютаДокумента, 
													   ЗаполнениеДокументов.КурсДокумента(ДокументПоступления, мВалютаРегламентированногоУчета),
													   КурсДокумента,
													   ЗаполнениеДокументов.КратностьДокумента(ДокументПоступления, мВалютаРегламентированногоУчета),
													   КратностьДокумента);

		 // Заполним бухгалтерские и налоговые реквизиты
		НоваяСтрока.СчетУчетаБУ             	= СтрокаТаблицыЗначений.СчетУчетаБУ;
		НоваяСтрока.НалоговоеНазначение    		= СтрокаТаблицыЗначений.НалоговоеНазначение;
			
		НоваяСтрока.СчетУчетаНДС 				= ПланыСчетов.Хозрасчетный.НалоговыеОбязательства;
			
		ЗаполнитьСтатьюКнигиПриобретенияРегл(НоваяСтрока);
	КонецЦикла;
КонецПроцедуры // ЗаполнитьПоПоступлению()

Процедура ДополнитьСтруктуруОбязательныхПолейТабличнойЧастиТоварыРегл(СтруктураОбязательныхПолей, СтруктураШапкиДокумента)
	ПоставщикКомитент = (СтруктураШапкиДокумента.ДоговорПоставщикаТоваров.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		
	КонецЕсли;
КонецПроцедуры // ДополнитьСтруктуруОбязательныхПолейТабличнойЧастиТоварыРегл()	

// Проверяет правильность заполнения строк табличной части "Товары".
//
// Параметры:
// Параметры: 
//  ТаблицаПоТоварам        - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  СтруктураШапкиДокумента - структура, содержащая рексвизиты шапки документа и результаты запроса по шапке
//  Отказ                   - флаг отказа в проведении.
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиРазделы(ТаблицаПоРазделам, СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("ТаможеннаяСтоимость, СтавкаНДС");

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Разделы", СтруктураОбязательныхПолей, Отказ, Заголовок);

	//Суммы пошлины и НДС в каждом разделе должны совпадать с итогами по разделу в ТЧ Товары.
	Для каждого Раздел Из ТаблицаПоРазделам Цикл

		ВсегоСтоимость = 0;
		ВсегоПошлина   = 0;
		ВсегоНДС       = 0;
		НомерРаздела = ТаблицаПоРазделам.Индекс(Раздел) + 1;
		ВсегоАкциз       = 0;
		ПосчитатьИтогиПоРазделу(НомерРаздела, ВсегоСтоимость, ВсегоПошлина, ВсегоАкциз, ВсегоНДС);

		Если ВсегоПошлина <> Раздел.СуммаПошлины Тогда
			ОбщегоНазначения.СообщитьОбОшибке("По разделу """ + СокрЛП(НомерРаздела) + """ сумма пошлины не совпадает с итогом по товарам раздела", Отказ, Заголовок);
		КонецЕсли;

		Если ВсегоНДС <> Раздел.СуммаНДС Тогда
			ОбщегоНазначения.СообщитьОбОшибке("По разделу """ + СокрЛП(НомерРаздела) + """ сумма НДС не совпадает с итогом по товарам раздела", Отказ, Заголовок);
		КонецЕсли;
		Если ВсегоАкциз <> Раздел.СуммаАкциза Тогда
			ОбщегоНазначения.СообщитьОбОшибке("По разделу """ + СокрЛП(НомерРаздела) + """ сумма акциза не совпадает с итогом по товарам раздела", Отказ, Заголовок);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиРазделы()

// Проверяет правильность заполнения строк табличной части "Товары".
//
// Параметры:
// Параметры: 
//  ТаблицаПоТоварам        - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  СтруктураШапкиДокумента - структура, содержащая рексвизиты шапки документа и результаты запроса по шапке
//  Отказ                   - флаг отказа в проведении.
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиТовары(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок)

	ИмяТабличнойЧасти = "Товары";

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Номенклатура, Количество, ФактурнаяСтоимость");

	ПоставщикКомитент = (СтруктураШапкиДокумента.ДоговорПоставщикаТоваров.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
    Если НЕ ПоставщикКомитент Тогда
	
		СтруктураОбязательныхПолей.Вставить("ДокументПартии");
	
	КонецЕсли; 
	
	ДополнитьСтруктуруОбязательныхПолейТабличнойЧастиТоварыРегл(СтруктураОбязательныхПолей, СтруктураШапкиДокумента);

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Товары", СтруктураОбязательныхПолей, Отказ, Заголовок);

	// Здесь бланков быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетБланковСтрогогоУчета(ЭтотОбъект, "Товары", ТаблицаПоТоварам, Отказ, Заголовок);
	
	// Здесь услуг быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетУслуг(ЭтотОбъект, "Товары", ТаблицаПоТоварам, Отказ, Заголовок);

	// Здесь наборов быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетНаборов(ЭтотОбъект, "Товары", ТаблицаПоТоварам, Отказ, Заголовок);

	// Здесь комплектов быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетКомплектов(ЭтотОбъект, "Товары", ТаблицаПоТоварам, Отказ, Заголовок);
	
	// При поступлении товаров должен быть указан конкретный вид налоговой деятельности.
	СтруктураПараметровПроверки = Новый Структура("ПроверятьРаспределяемую");
	НалоговыйУчет.ПроверитьТабличнуюЧастьНаРазрешенныеВидыНалоговойДеятельности(ЭтотОбъект, "Товары", , ТаблицаПоТоварам,
	                                                              СтруктураПараметровПроверки, 
																  СтруктураШапкиДокумента, 
											                      Отказ, Заголовок);
	// Проверка номера ГТД.
	Для каждого СтрокаТаблицы Из ТаблицаПоТоварам Цикл
    
		СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) +
		                               """ табличной части """ + "Товары" + """: ";
		Если НЕ СтрокаТаблицы.ДокументПартииДоговор = СтруктураШапкиДокумента.ДоговорПоставщикаТоваров Тогда
			СтрокаСообщения = " реквизит ""Документ партии"" не соответствует реквизиту ""Договор поставщика товаров""!";
			
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения, Отказ, Заголовок);
		ИначеЕсли НЕ СтрокаТаблицы.ДокументПартииКонтрагент = СтруктураШапкиДокумента.ПоставщикТоваров Тогда	
			СтрокаСообщения = " реквизит ""Документ партии"" не соответствует реквизиту ""Поставщик товаров""!";
			
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения, Отказ, Заголовок);
		КонецЕсли;
										
		Если СтрокаТаблицы.ДокументПартииВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
			// ГТД принятые в переработку не обрабатывает
			СтрокаСообщения = " реквизит ""Документ партии"" является поступлением в переработку. ГТД оформляется только для купленных товаров !";
			
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения, Отказ, Заголовок);
		КонецЕсли;
		
	КонецЦикла;

	НалоговыйУчет.ПроверитьЗаполнениеНалоговыхНазначений(
					СтруктураШапкиДокумента, 
					ТаблицаПоТоварам, 
					ИмяТабличнойЧасти,
					Отказ, 
					Заголовок, 
					"Поступление", // ВидОперации
					Ложь           // ЭтоЗатраты 
	);	
		
	
	
КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// Проверяет правильность заполнения строк табличной части "Оборудование".
//
// Параметры:
// Параметры: 
//  ТаблицаПоТоварам        - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  СтруктураШапкиДокумента - структура, содержащая рексвизиты шапки документа и результаты запроса по шапке
//  Отказ                   - флаг отказа в проведении.
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиОборудование(ТаблицаПоОборудованию, СтруктураШапкиДокумента, Отказ, Заголовок)

	ИмяТабличнойЧасти = "Оборудование";

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Номенклатура, Количество, ФактурнаяСтоимость");
	
	ПоставщикКомитент = (СтруктураШапкиДокумента.ДоговорПоставщикаТоваров.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
    Если НЕ ПоставщикКомитент Тогда
	
		СтруктураОбязательныхПолей.Вставить("ДокументПартии");
	
	КонецЕсли; 

	ДополнитьСтруктуруОбязательныхПолейТабличнойЧастиТоварыРегл(СтруктураОбязательныхПолей, СтруктураШапкиДокумента);

	// Теперь позовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Оборудование", СтруктураОбязательныхПолей, Отказ, Заголовок);

	// Здесь бланков быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетБланковСтрогогоУчета(ЭтотОбъект, "Оборудование", ТаблицаПоОборудованию, Отказ, Заголовок);
	
	// Здесь услуг быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетУслуг(ЭтотОбъект, "Оборудование", ТаблицаПоОборудованию, Отказ, Заголовок);

	// Здесь наборов быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетНаборов(ЭтотОбъект, "Оборудование", ТаблицаПоОборудованию, Отказ, Заголовок);
	
	// Все партии должны быть от ПоставщикаТоваров
	Для каждого СтрокаТаблицы Из ТаблицаПоОборудованию Цикл

		СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) +
		                               """ табличной части """ + "Товары" + """: ";
		
		Если НЕ СтрокаТаблицы.ДокументПартииДоговор = СтруктураШапкиДокумента.ДоговорПоставщикаТоваров Тогда
			СтрокаСообщения = " реквизит ""Документ партии"" не соответствует реквизиту ""Договор поставщика товаров""!";
			
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения, Отказ, Заголовок);
		ИначеЕсли НЕ СтрокаТаблицы.ДокументПартииКонтрагент = СтруктураШапкиДокумента.ПоставщикТоваров Тогда	
			СтрокаСообщения = " реквизит ""Документ партии"" не соответствует реквизиту ""Поставщик товаров""!";
			
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения, Отказ, Заголовок);
		КонецЕсли;
										
		Если СтрокаТаблицы.ДокументПартииВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
			// ГТД принятые в переработку не обрабатывает
			СтрокаСообщения = " реквизит ""Документ партии"" является поступлением в переработку. ГТД оформляется только для купленных товаров !";
			
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения, Отказ, Заголовок);
		КонецЕсли;
		
	КонецЦикла;
	
	НалоговыйУчет.ПроверитьЗаполнениеНалоговыхНазначений(
					СтруктураШапкиДокумента, 
					ТаблицаПоОборудованию, 
					ИмяТабличнойЧасти,
					Отказ, 
					Заголовок, 
					"Поступление", // ВидОперации
					Ложь           // ЭтоЗатраты 
	);	

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

Процедура ДополнитьСтруктуруОбязательныхПолейШапкиРегл(СтруктураОбязательныхПолей, СтруктураШапкиДокумента)

	
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		
		СтруктураОбязательныхПолей.Вставить("СчетУчетаРасчетовСКонтрагентом");
		СтруктураОбязательныхПолей.Вставить("СчетУчетаПошлины");
		СтруктураОбязательныхПолей.Вставить("СчетУчетаАкциза");
		Если Не СтруктураШапкиДокумента.ОформленНалоговыйВексельПоНДС Тогда
			СтруктураОбязательныхПолей.Вставить("СчетУчетаНДС");
		КонецЕсли; 
		
		Если СтруктураШапкиДокумента.ОформленНалоговыйВексельПоНДС Тогда
			СтруктураОбязательныхПолей.Вставить("СчетУчетаВексель");	
		КонецЕсли;
		
		ПоставщикКомитент = (СтруктураШапкиДокумента.ДоговорПоставщикаТоваров.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
		Если ПоставщикКомитент Тогда
			СтруктураОбязательныхПолей.Вставить("СчетЗатрат");
			СтруктураОбязательныхПолей.Вставить("ПодразделениеОрганизации");
		КонецЕсли;
			
		Если ВключаетсяВУточняющийРасчет Тогда
			СтруктураОбязательныхПолей.Вставить("УточняемыйПериод");
		КонецЕсли;
			
		
		Если Дата >= глЗначениеПеременной("ДатаВступленияВСилуЗУ643") Тогда
			СтруктураОбязательныхПолей.Вставить("СчетНДСУсловнаяПродажа");	
		КонецЕсли;
		
		
	КонецЕсли;
КонецПроцедуры // Процедура ДополнитьСтруктуруОбязательныхПолейШапкиРегл()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение, не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - структура, содержащая рексвизиты шапки документа и результаты запроса по шапке
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Организация,
	                             |ВалютаДокумента, КурсДокумента, КратностьДокумента, 
								 |Контрагент, ДоговорКонтрагента, ПоставщикТоваров, ДоговорПоставщикаТоваров,
	                             |КурсВзаиморасчетов, КратностьВзаиморасчетов");

								 
	ДополнитьСтруктуруОбязательныхПолейШапкиРегл(СтруктураОбязательныхПолей, СтруктураШапкиДокумента);
	
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);

	//Организация в документе должна совпадать с организацией, указанной в договоре взаиморасчетов.
	УправлениеВзаиморасчетами.ПроверитьСоответствиеОрганизацииДоговоруВзаиморасчетов(Организация, ДоговорКонтрагента, СтруктураШапкиДокумента.ДоговорОрганизация, Отказ, Заголовок);

	// Договор с таможней должен иметь вид "Прочее", в валюте регламентированного учета
	// и ведение взаиморасчетов "по договору в целом".
	Если СтруктураШапкиДокумента.ВидДоговораКонтрагента<> Перечисления.ВидыДоговоровКонтрагентов.Прочее Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Договор с таможней должен иметь вид ""Прочее"".", Отказ, Заголовок);
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.ВалютаВзаиморасчетов <> мВалютаРегламентированногоУчета Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Договор с таможней должен быть в валюте регламентированного учета ("+ мВалютаРегламентированногоУчета +") .", Отказ, Заголовок);
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.ВедениеВзаиморасчетов <> Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Для договора с таможней ведение взаиморасчетов должно быть ""По договору в целом"".", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

Процедура ОбработатьТабличнуюЧастьРазделы(СтруктураШапкиДокумента)
	// Для проведения по взаиморасчетам посчитаем долг таможне (в регламентированной валюте).
	СуммаВзаиморасчетов    = ТаможенныйСбор;
	Для каждого СтрокаТабличнойЧасти Из Разделы Цикл
			СуммаВзаиморасчетов = СуммаВзаиморасчетов + СтрокаТабличнойЧасти.СуммаПошлины;
			СуммаВзаиморасчетов = СуммаВзаиморасчетов + СтрокаТабличнойЧасти.СуммаАкциза;
	
		Если НЕ СтруктураШапкиДокумента.ОформленНалоговыйВексельПоНДС Тогда
			СуммаВзаиморасчетов = СуммаВзаиморасчетов + СтрокаТабличнойЧасти.СуммаНДС;
		КонецЕсли;
	КонецЦикла;

	СтруктураШапкиДокумента.Вставить("СуммаВзаиморасчетов",    СуммаВзаиморасчетов);

КонецПроцедуры //ОбработатьТабличнуюЧастьРазделы()

Процедура РаспределитьСуммыИзШапки(ТаблицаПоТоварам,ТаблицаПоОборудованию,СтруктураШапкиДокумента)
	
	// Распределим таможенный сбор по товарам и оборудованию
	ТаблицаПоТоварам.Колонки.Добавить("ТаможенныйСбор", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоТоварам.Колонки.Добавить("ТаможеннаяСтоимость", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	ТаблицаПоОборудованию.Колонки.Добавить("ТаможенныйСбор", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоОборудованию.Колонки.Добавить("ТаможеннаяСтоимость", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	ОшибкаОкругленияТаможенныйСбор = 0;
	ВсегоСтоимостьПоГТД = ТаблицаПоТоварам.Итог("ФактурнаяСтоимость") + ТаблицаПоОборудованию.Итог("ФактурнаяСтоимость");
	
	Для каждого ТекРаздел Из Разделы Цикл
		
		СтрокиТоваровПоРазделу 		= ТаблицаПоТоварам.НайтиСтроки(Новый Структура("НомерРаздела", Разделы.Индекс(ТекРаздел) + 1)); 
		СтрокиОборудованияПоРазделу = ТаблицаПоОборудованию.НайтиСтроки(Новый Структура("НомерРаздела", Разделы.Индекс(ТекРаздел) + 1)); 
		
		ОшибкаОкругленияТаможеннаяСтоимость = 0;
		ТаможеннаяСтоимость = ТекРаздел.ТаможеннаяСтоимость;
		
		ВсегоСтоимостьПоРазделу = 0;
		
		Для каждого СтрокаРаздела Из СтрокиТоваровПоРазделу Цикл
			ВсегоСтоимостьПоРазделу = ВсегоСтоимостьПоРазделу + СтрокаРаздела.ФактурнаяСтоимость;
		КонецЦикла;
	
		Для каждого СтрокаРаздела Из СтрокиОборудованияПоРазделу Цикл
			ВсегоСтоимостьПоРазделу = ВсегоСтоимостьПоРазделу + СтрокаРаздела.ФактурнаяСтоимость;
		КонецЦикла;
		
		Если ВсегоСтоимостьПоРазделу = 0 Тогда
			// распределять нечего
			Продолжить;
		КонецЕсли;
		
		Для каждого СтрокаРаздела Из СтрокиТоваровПоРазделу Цикл
			// таможенный сбор распределяем пропорционально строкам всех разделов
			СтрокаРаздела.ТаможенныйСбор 	  = ОбщегоНазначения.ОкруглитьСУчетомПогрешности(ТаможенныйСбор      * (СтрокаРаздела.ФактурнаяСтоимость/ВсегоСтоимостьПоГТД), 2, ОшибкаОкругленияТаможенныйСбор);
			// таможенную стоимость распределяем только в рамках раздела, для которого она указана.
			СтрокаРаздела.ТаможеннаяСтоимость = ОбщегоНазначения.ОкруглитьСУчетомПогрешности(ТаможеннаяСтоимость * (СтрокаРаздела.ФактурнаяСтоимость/ВсегоСтоимостьПоРазделу), 2, ОшибкаОкругленияТаможеннаяСтоимость);
		КонецЦикла;
	
		Для каждого СтрокаРаздела Из СтрокиОборудованияПоРазделу Цикл
			// таможенный сбор распределяем пропорционально строкам всех разделов
			СтрокаРаздела.ТаможенныйСбор 	  = ОбщегоНазначения.ОкруглитьСУчетомПогрешности(ТаможенныйСбор      * (СтрокаРаздела.ФактурнаяСтоимость/ВсегоСтоимостьПоГТД), 2, ОшибкаОкругленияТаможенныйСбор);
			// таможенную стоимость распределяем только в рамках раздела, для которого она указана.
			СтрокаРаздела.ТаможеннаяСтоимость = ОбщегоНазначения.ОкруглитьСУчетомПогрешности(ТаможеннаяСтоимость * (СтрокаРаздела.ФактурнаяСтоимость/ВсегоСтоимостьПоРазделу), 2, ОшибкаОкругленияТаможеннаяСтоимость);
		КонецЦикла;
		
	КонецЦикла;
	

	
КонецПроцедуры

Процедура РаспределитьПоСкладам(ТаблицаПоТоварам, ТаблицаРаспределенияПоСкладам, СтруктураШапкиДокумента, СтатусПартии, Отказ, Заголовок)
	
	ТаблицаПоТоварам.Колонки.Добавить("МеткаРаспределения", Новый ОписаниеТипов("Булево"));
	
	ДопСтроки = ТаблицаПоТоварам.СкопироватьКолонки();
		
	Для Каждого Строка Из ТаблицаПоТоварам Цикл
		
		Если НЕ ЗначениеЗаполнено(Строка.ДокументОприходования) Тогда
		
			Продолжить;
		
		КонецЕсли;
		
		// В таблице распределения по складам могут быть записи с любыми комбинациями 
		// ДокументОприходования, Номенклатура, СерияНоменклатуры, ХарактеристикаНоменклатуры
		// Отберем нужные
		СтруктураПоиска = Новый Структура ("ДокументОприходования,
											|Номенклатура, 
											|СерияНоменклатуры, 
											|ХарактеристикаНоменклатуры, 
											|СтатусПартии,
											|СчетУчетаБУ",
											Строка.ДокументОприходования,
											Строка.Номенклатура,
											Строка.СерияНоменклатуры,
											Строка.ХарактеристикаНоменклатуры,
											СтатусПартии,
											Строка.СчетУчетаБУ);
																						
		Если СтруктураШапкиДокумента.ЕстьНДС Тогда
			
			СтруктураПоиска.Вставить("НалоговоеНазначение",  		Строка.НалоговоеНазначение);
			
		КонецЕсли;
											
		МассивСтрокРаспределения = ТаблицаРаспределенияПоСкладам.НайтиСтроки(СтруктураПоиска);
		
		Если МассивСтрокРаспределения.Количество() = 0 тогда
			ТекстСообщения = "Не найден в документе (регл.учет)"+ Строка.ДокументОприходования
			+ " товар " + Строка.Номенклатура
			+ ?(Строка.Номенклатура.ВестиУчетПоХарактеристикам, ", х-ка: " + Строка.ХарактеристикаНоменклатуры, "")
			+ ?(Строка.Номенклатура.ВестиУчетПоСериям, ", серия: " + Строка.СерияНоменклатуры, "")
			+ ?(СтруктураШапкиДокумента.ЕстьНДС, ", налоговое назначение: " + Строка.НалоговоеНазначение,"");
				
			ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения, Отказ, Заголовок);
		КонецЕсли;
		
		ИтогоКоличество = 0;
		Для Каждого Элемент Из МассивСтрокРаспределения Цикл
			ИтогоКоличество = ИтогоКоличество + Элемент.Количество * Элемент.Коэффициент;
		КонецЦикла;
		
		// Перечисляем все суммы, которые необходимо распределить:
		//  1) Для движений и проводок создаваемых в модуле документа
		//  2) Для движений и проводок создаваемых в процедуре УправлениеЗапасамиПартионныйУчет.ВыполнитьПриходПоРегистрамПартий(...)
		//  3) Для движений выполняемых в модуле УправлениеЗапасамиРасширеннаяАналитика для упр и бух учета
		//  4) Для зачета аванса (таможни)
		СуммовыеРеквизиты = Новый Структура("Пошлина, Акциз, ТаможенныйСбор, ПроводкиСуммаНДССебестоимость, БазаНДС, Сумма, 
		                                    |Стоимость, СтоимостьНУ, НДС, ПроводкиСуммаБезНДС, ПроводкиСуммаНДСКредит,
											|СуммаСНДСРегл,
		                                    |ПроводкиСуммаБезНДСРегл, СуммаНДСРегл, ПроводкиСуммаНДСРегл");
		// Установим сумму которую нужно распределить									
		Для каждого КлючИЗначение Из СуммовыеРеквизиты Цикл
			ИмяКолонки = КлючИЗначение.Ключ;
			СуммовыеРеквизиты[ИмяКолонки] = Строка[ИмяКолонки];
		КонецЦикла;
		
		ПогрешностиОкругления = Новый Соответствие;
		
		Для Каждого Элемент Из МассивСтрокРаспределения Цикл
			
			Если ИтогоКоличество<=0 Тогда         
				Прервать;
			КонецЕсли;
			
			Если Элемент.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ДопСтрока = ДопСтроки.Добавить();
			ЗаполнитьЗначенияСвойств(ДопСтрока,Строка);
						
			ДопСтрока.Склад        = Элемент.Склад;			
			Если Элемент.Количество<ИтогоКоличество Тогда
				КоэффРаспред = Элемент.Количество * Элемент.Коэффициент/ИтогоКоличество;
			Иначе
				КоэффРаспред = 1;
			КонецЕсли;
			
			Для каждого КлючИЗначение Из СуммовыеРеквизиты Цикл
				ИмяКолонки = КлючИЗначение.Ключ;
				ДопСтрока[ИмяКолонки] = ОбщегоНазначения.ОкруглитьСУчетомПогрешности(СуммовыеРеквизиты[ИмяКолонки] * КоэффРаспред, 2, , ПогрешностиОкругления, ИмяКолонки);
			КонецЦикла;
						
		КонецЦикла;
		
		// Исходная строка будет помечена
		Строка.МеткаРаспределения = Истина;
		
	КонецЦикла;
	
	// Теперь доп строки добавим в таблицу по товарам
	Для Каждого ДопСтрока Из ДопСтроки Цикл
		НоваяСтрока = ТаблицаПоТоварам.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ДопСтрока);
	КонецЦикла;
			
	// После обработки удалим распределенные строки 
	Инд=0;
	Пока Инд<ТаблицаПоТоварам.Количество() Цикл
		Если ТаблицаПоТоварам[Инд].МеткаРаспределения = Истина Тогда
			ТаблицаПоТоварам.Удалить(Инд);
		Иначе
			Инд = Инд+1;
		КонецЕсли;
	КонецЦикла;

	ТаблицаПоТоварам.Колонки.Удалить("МеткаРаспределения");
	
КонецПроцедуры

Процедура РаспределитьДопРасходыПоСкладам(ТаблицаПоТоварам, ТаблицаПоОборудованию, СтруктураШапкиДокумента, Отказ, Заголовок);
	
	ЗапросРаспределенияПоСкладам = Новый Запрос;
	ЗапросРаспределенияПоСкладам.Текст = "ВЫБРАТЬ
	                                     |	ДокументОприходованияТовары.Ссылка КАК ДокументОприходования,										 
										 |	ДокументОприходованияТовары.Склад КАК Склад,
										 |	ДокументОприходованияТовары.Номенклатура КАК Номенклатура,
	                                     |	ДокументОприходованияТовары.СерияНоменклатуры КАК СерияНоменклатуры,
	                                     |	ДокументОприходованияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
										 |	ДокументОприходованияТовары.СчетУчетаБУ 				КАК СчетУчетаБУ,
										 |	ДокументОприходованияТовары.НалоговоеНазначение 		КАК НалоговоеНазначение,										 
										 |	ДокументОприходованияТовары.Коэффициент 				КАК Коэффициент,
	                                     |	СУММА(ДокументОприходованияТовары.Количество) 			КАК Количество,
	                                     |	&СтатусПартии КАК СтатусПартии
	                                     |ИЗ
	                                     |	Документ.ПоступлениеТоваровУслуг.Товары КАК ДокументОприходованияТовары
	                                     |ГДЕ
	                                     |	ДокументОприходованияТовары.Ссылка В(&Ссылка)
	                                     |	И ДокументОприходованияТовары.Номенклатура В(&Номенклатура)
	                                     |	И ДокументОприходованияТовары.ХарактеристикаНоменклатуры В(&ХарактеристикаНоменклатуры)
	                                     |	И ДокументОприходованияТовары.СерияНоменклатуры В(&СерияНоменклатуры)
	                                     |
	                                     |СГРУППИРОВАТЬ ПО
	                                     |	ДокументОприходованияТовары.Номенклатура,
	                                     |	ДокументОприходованияТовары.Коэффициент,
	                                     |	ДокументОприходованияТовары.Склад,
	                                     |	ДокументОприходованияТовары.СерияНоменклатуры,
	                                     |	ДокументОприходованияТовары.ХарактеристикаНоменклатуры,
	                                     |	ДокументОприходованияТовары.Ссылка,
										 |	ДокументОприходованияТовары.СчетУчетаБУ,
										 |	ДокументОприходованияТовары.НалоговоеНазначение
										 |
										 |ОБЪЕДИНИТЬ ВСЕ
										 |
										 |ВЫБРАТЬ
	                                     |	ДокументОприходованияТовары.Ссылка КАК ДокументОприходования,										 
	                                     |	ДокументОприходованияТовары.Склад КАК Склад,
	                                     |	ДокументОприходованияТовары.Номенклатура КАК Номенклатура,
	                                     |	ДокументОприходованияТовары.СерияНоменклатуры КАК СерияНоменклатуры,
	                                     |	ДокументОприходованияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
										 |	ДокументОприходованияТовары.СчетУчетаБУ 				КАК СчетУчетаБУ,
										 |	ДокументОприходованияТовары.НалоговоеНазначение 		КАК НалоговоеНазначение,										 
	                                     |	ДокументОприходованияТовары.Коэффициент КАК Коэффициент,
	                                     |	СУММА(ДокументОприходованияТовары.Количество) КАК Количество,
	                                     |	&СтатусПартииОборудование КАК СтатусПартии
	                                     |ИЗ
	                                     |	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ДокументОприходованияТовары
	                                     |ГДЕ
	                                     |	ДокументОприходованияТовары.Ссылка В(&Ссылка)
	                                     |	И ДокументОприходованияТовары.Номенклатура В(&Номенклатура)
	                                     |	И ДокументОприходованияТовары.ХарактеристикаНоменклатуры В(&ХарактеристикаНоменклатуры)
	                                     |	И ДокументОприходованияТовары.СерияНоменклатуры В(&СерияНоменклатуры)
	                                     |
	                                     |СГРУППИРОВАТЬ ПО
	                                     |	ДокументОприходованияТовары.Номенклатура,
	                                     |	ДокументОприходованияТовары.Коэффициент,
	                                     |	ДокументОприходованияТовары.Склад,
	                                     |	ДокументОприходованияТовары.СерияНоменклатуры,
	                                     |	ДокументОприходованияТовары.ХарактеристикаНоменклатуры,
	                                     |	ДокументОприходованияТовары.Ссылка,
										 |	ДокументОприходованияТовары.СчетУчетаБУ,
										 |	ДокументОприходованияТовары.НалоговоеНазначение										 
                                         |
	                                     |ОБЪЕДИНИТЬ ВСЕ
	                                     |
	                                     |ВЫБРАТЬ
	                                     |	ДокументОприходованияТовары.Ссылка,										 
	                                     |	ДокументОприходованияТовары.Ссылка.Склад,
	                                     |	ДокументОприходованияТовары.Номенклатура,
	                                     |	ДокументОприходованияТовары.СерияНоменклатуры,
	                                     |	ДокументОприходованияТовары.ХарактеристикаНоменклатуры,
										 |	ДокументОприходованияТовары.СчетУчетаБУ 				КАК СчетУчетаБУ,
										 |	ДокументОприходованияТовары.НалоговоеНазначение 		КАК НалоговоеНазначение,										 
	                                     |	ДокументОприходованияТовары.Коэффициент,
	                                     |	СУММА(ДокументОприходованияТовары.Количество),
	                                     |	&СтатусПартии
	                                     |ИЗ
	                                     |	Документ.ПоступлениеТоваровУслугВНТТ.Товары КАК ДокументОприходованияТовары
	                                     |ГДЕ
	                                     |	ДокументОприходованияТовары.Ссылка В(&Ссылка)
	                                     |	И ДокументОприходованияТовары.Номенклатура В(&Номенклатура)
	                                     |	И ДокументОприходованияТовары.ХарактеристикаНоменклатуры В(&ХарактеристикаНоменклатуры)
	                                     |	И ДокументОприходованияТовары.СерияНоменклатуры В(&СерияНоменклатуры)
	                                     |
	                                     |СГРУППИРОВАТЬ ПО
	                                     |	ДокументОприходованияТовары.Номенклатура,
	                                     |	ДокументОприходованияТовары.Коэффициент,
	                                     |	ДокументОприходованияТовары.Ссылка.Склад,
	                                     |	ДокументОприходованияТовары.СерияНоменклатуры,
	                                     |	ДокументОприходованияТовары.ХарактеристикаНоменклатуры,
	                                     |	ДокументОприходованияТовары.Ссылка,
										 |	ДокументОприходованияТовары.СчетУчетаБУ,
										 |	ДокументОприходованияТовары.НалоговоеНазначение
	                                     |
	                                     |ОБЪЕДИНИТЬ ВСЕ
	                                     |
	                                     |ВЫБРАТЬ
	                                     |	ДокументОприходованияТовары.Ссылка,										 
	                                     |	ДокументОприходованияТовары.Склад,
	                                     |	ДокументОприходованияТовары.Номенклатура,
	                                     |	ДокументОприходованияТовары.СерияНоменклатуры,
	                                     |	ДокументОприходованияТовары.ХарактеристикаНоменклатуры,
										 |	ДокументОприходованияТовары.СчетУчетаБУ 				КАК СчетУчетаБУ,
										 |	ДокументОприходованияТовары.НалоговоеНазначение 		КАК НалоговоеНазначение,										 
	                                     |	ДокументОприходованияТовары.Коэффициент,
	                                     |	СУММА(ДокументОприходованияТовары.Количество),
	                                     |	&СтатусПартии
	                                     |ИЗ
	                                     |	Документ.АвансовыйОтчет.Товары КАК ДокументОприходованияТовары
	                                     |ГДЕ
	                                     |	ДокументОприходованияТовары.Ссылка В(&Ссылка)
	                                     |	И ДокументОприходованияТовары.Номенклатура В(&Номенклатура)
	                                     |	И ДокументОприходованияТовары.ХарактеристикаНоменклатуры В(&ХарактеристикаНоменклатуры)
	                                     |	И ДокументОприходованияТовары.СерияНоменклатуры В(&СерияНоменклатуры)
										 |
	                                     |СГРУППИРОВАТЬ ПО
	                                     |	ДокументОприходованияТовары.Номенклатура,
	                                     |	ДокументОприходованияТовары.Коэффициент,
	                                     |	ДокументОприходованияТовары.Склад,
	                                     |	ДокументОприходованияТовары.СерияНоменклатуры,
	                                     |	ДокументОприходованияТовары.ХарактеристикаНоменклатуры,
	                                     |	ДокументОприходованияТовары.Ссылка,
										 |	ДокументОприходованияТовары.СчетУчетаБУ,										 
										 |	ДокументОприходованияТовары.НалоговоеНазначение
	                                     |
	                                     |ОБЪЕДИНИТЬ ВСЕ
	                                     |
	                                     |ВЫБРАТЬ
	                                     |	ДокументОприходованияТовары.Ссылка,										 
	                                     |	ДокументОприходованияТовары.Ссылка.Склад,
	                                     |	ДокументОприходованияТовары.Номенклатура,
	                                     |	ДокументОприходованияТовары.СерияНоменклатуры,
	                                     |	ДокументОприходованияТовары.ХарактеристикаНоменклатуры,
										 |	ДокументОприходованияТовары.СчетУчетаБУ 				КАК СчетУчетаБУ,
										 |	ДокументОприходованияТовары.НалоговоеНазначение 		КАК НалоговоеНазначение,										 
	                                     |	ДокументОприходованияТовары.Коэффициент,
	                                     |	СУММА(ДокументОприходованияТовары.Количество),
	                                     |	ВЫБОР КОГДА ДокументОприходованияТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОприходованиеТоваров.Оборудование) 
										 |		ТОГДА &СтатусПартииОборудование
										 |		ИНАЧЕ &СтатусПартии
										 |	КОНЕЦ 
	                                     |ИЗ
	                                     |	Документ.ОприходованиеТоваров.Товары КАК ДокументОприходованияТовары
	                                     |ГДЕ
	                                     |	ДокументОприходованияТовары.Ссылка В(&Ссылка)
	                                     |	И ДокументОприходованияТовары.Номенклатура В(&Номенклатура)
	                                     |	И ДокументОприходованияТовары.ХарактеристикаНоменклатуры В(&ХарактеристикаНоменклатуры)
	                                     |	И ДокументОприходованияТовары.СерияНоменклатуры В(&СерияНоменклатуры)
										 |
	                                     |СГРУППИРОВАТЬ ПО
	                                     |	ДокументОприходованияТовары.Номенклатура,
	                                     |	ДокументОприходованияТовары.Коэффициент,
	                                     |	ДокументОприходованияТовары.Ссылка.Склад,
	                                     |	ДокументОприходованияТовары.СерияНоменклатуры,
	                                     |	ДокументОприходованияТовары.ХарактеристикаНоменклатуры,
	                                     |	ДокументОприходованияТовары.Ссылка,
										 |	ДокументОприходованияТовары.СчетУчетаБУ,										 
										 |	ДокументОприходованияТовары.НалоговоеНазначение,
	                                     |	ВЫБОР КОГДА ДокументОприходованияТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОприходованиеТоваров.Оборудование) 
										 |		ТОГДА &СтатусПартииОборудование
										 |		ИНАЧЕ &СтатусПартии
										 |	КОНЕЦ										 
	                                     |";

										 
	ОбщаяТаблица = ТаблицаПоТоварам.Скопировать();
	ОбщаяТаблица.Свернуть("ДокументОприходования, Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры", "");
	ТаблицаОборудованияКопия = ТаблицаПоОборудованию.Скопировать();
	ТаблицаОборудованияКопия.Свернуть("ДокументОприходования, Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры", "");
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаОборудованияКопия, ОбщаяТаблица);
	
	ЗапросРаспределенияПоСкладам.УстановитьПараметр("Ссылка", 						ОбщаяТаблица.ВыгрузитьКолонку("ДокументОприходования"));
	ЗапросРаспределенияПоСкладам.УстановитьПараметр("Номенклатура", 				ОбщаяТаблица.ВыгрузитьКолонку("Номенклатура"));
	ЗапросРаспределенияПоСкладам.УстановитьПараметр("ХарактеристикаНоменклатуры", 	ОбщаяТаблица.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	ЗапросРаспределенияПоСкладам.УстановитьПараметр("СерияНоменклатуры", 			ОбщаяТаблица.ВыгрузитьКолонку("СерияНоменклатуры"));
	ЗапросРаспределенияПоСкладам.УстановитьПараметр("СтатусПартии", 				Перечисления.СтатусыПартийТоваров.Купленный);
	ЗапросРаспределенияПоСкладам.УстановитьПараметр("СтатусПартииОборудование", 	Перечисления.СтатусыПартийТоваров.Оборудование);
	
	ТаблицаРаспределенияПоСкладам = ЗапросРаспределенияПоСкладам.Выполнить().Выгрузить();
	
	РаспределитьПоСкладам(ТаблицаПоТоварам, 	 ТаблицаРаспределенияПоСкладам, СтруктураШапкиДокумента, Перечисления.СтатусыПартийТоваров.Купленный,    Отказ, Заголовок);
    РаспределитьПоСкладам(ТаблицаПоОборудованию, ТаблицаРаспределенияПоСкладам, СтруктураШапкиДокумента, Перечисления.СтатусыПартийТоваров.Оборудование, Отказ, Заголовок);

КонецПроцедуры

Функция ПодготовитьТаблицуТоваров(ТаблицаТоваров, СтруктураШапкиДокумента)

	ТаблицаТоваров.Колонки.Добавить("Сумма");
	ТаблицаТоваров.ЗагрузитьКолонку(ТаблицаТоваров.ВыгрузитьКолонку("ФактурнаяСтоимость"), "Сумма");
	
	ТаблицаТоваров.Колонки.Добавить("Качество");
	ТаблицаТоваров.ЗаполнитьЗначения(Справочники.Качество.Новый, "Качество");
	
	ПодготовитьТаблицуТоваровУпр(ТаблицаТоваров, СтруктураШапкиДокумента);
	ПодготовитьТаблицуТоваровРегл(ТаблицаТоваров, СтруктураШапкиДокумента);
	
	Возврат ТаблицаТоваров;

КонецФункции // ПодготовитьТаблицуТоваров()

Процедура ПодготовитьТаблицуТоваровУпр(ТаблицаТоваров, СтруктураШапкиДокумента)

	ТаблицаТоваров.Колонки.Добавить("Стоимость",     Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));

	// Недостающие поля.
	ТаблицаТоваров.Колонки.Добавить("СтатусПартии");
	Для каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
			СтрокаТаблицы.Стоимость = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.Пошлина + СтрокаТаблицы.Акциз + СтрокаТаблицы.ТаможенныйСбор + ?(СтруктураШапкиДокумента.НеВключатьНДСВСтоимостьПартий, 0, СтрокаТаблицы.НДС)
											 , мВалютаРегламентированногоУчета,
											 СтруктураШапкиДокумента.ВалютаУправленческогоУчета, 
											 1, СтруктураШапкиДокумента.КурсВалютыУправленческогоУчета, 
											 1,СтруктураШапкиДокумента.КратностьВалютыУправленческогоУчета);

		Если СтрокаТаблицы.ОбособленныйУчетТоваровПоЗаказамПокупателей = Ложь Тогда
			СтрокаТаблицы.Заказ = Неопределено;
		КонецЕсли;
		// ГТД по комиссии и по переработке не делаются, только купленный
		СтрокаТаблицы.СтатусПартии = Перечисления.СтатусыПартийТоваров.Купленный; 
	КонецЦикла;

КонецПроцедуры // ПодготовитьТаблицуТоваровУпр()

Процедура ПодготовитьТаблицуТоваровРегл(ТаблицаТоваров, СтруктураШапкиДокумента)
	ТаблицаТоваров.Колонки.Добавить("ПроводкаФактурнаяСтоимость", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("ПроводкаСумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("ПроводкаСуммаНДС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");
	ТаблицаТоваров.Колонки.Добавить("СчетУчетаНУ");
	// Суммы в документе в валюте регл. учета
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДСРегл"        	 		 , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("БазаНДС"        	 		 	 , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаБезНДС" 	 		 , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаНДСКредит"       	 , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаНДССебестоимость"  , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаСНДС"  			 , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаНДСУсловнойПродажи"        , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	// для товаров комитента
	ТаблицаТоваров.Колонки.Добавить("НоменклатурнаяГруппа");
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаБезНДСРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаНДСРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаНДСРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьНУ"                 , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	ТаблицаТоваров.Колонки.Добавить("Амортизируется", Новый ОписаниеТипов("Булево"));

	ВидДеятельностиНДСНеоблагаемый 		= Перечисления.ВидыДеятельностиНДС.Необлагаемая;
	ВидДеятельностиНДСПропорциональный 	= Перечисления.ВидыДеятельностиНДС.ПропорциональноОблагаемая;
	НалоговоеНазначениеНДС_НеоблагаемаяНеХозДеятельность = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность;
	
	НеОблагаемыйВНД = Справочники.ВидыНалоговойДеятельности.НеОблагаемая;
	Для каждого СтрокаТаблицы Из ТаблицаТоваров Цикл

		СтавкаНДС      = Разделы[СтрокаТаблицы.НомерРаздела - 1].СтавкаНДС;
		
		СтрокаТаблицы.СтавкаНДС = СтавкаНДС;
		СтрокаТаблицы.СуммаСНДСРегл			= СтрокаТаблицы.Пошлина + СтрокаТаблицы.Акциз 
												+ СтрокаТаблицы.ТаможенныйСбор + СтрокаТаблицы.НДС;
		СтрокаТаблицы.БазаНДС				= СтрокаТаблицы.Пошлина + СтрокаТаблицы.Акциз 
												+ СтрокаТаблицы.ТаможеннаяСтоимость;
		СтрокаТаблицы.ПроводкиСуммаСНДС 		= СтрокаТаблицы.СуммаСНДСРегл;
		
		ПогрешностиОкругления     = Новый Соответствие;

		Если СтрокаТаблицы.ВидДеятельностиНДС = ВидДеятельностиНДСНеоблагаемый Тогда
			// нет налогового кредита
			СтрокаТаблицы.ПроводкиСуммаНДСКредит = 0;
			СтрокаТаблицы.СуммаНДСУсловнойПродажи = СтрокаТаблицы.НДС;
		ИначеЕсли СтрокаТаблицы.ВидДеятельностиНДС = ВидДеятельностиНДСПропорциональный Тогда
			Если СтруктураШапкиДокумента.СуммаНДСПропорциональноВсего = 0 Тогда
				СтрокаТаблицы.ПроводкиСуммаНДСКредит = 0;
				СтрокаТаблицы.СуммаНДСУсловнойПродажи = СтрокаТаблицы.НДС;
			Иначе	
				СтрокаТаблицы.ПроводкиСуммаНДСКредит = ОбщегоНазначения.ОкруглитьСУчетомПогрешности(СтрокаТаблицы.НДС * (СтруктураШапкиДокумента.СуммаНДСПропорциональноКредит / СтруктураШапкиДокумента.СуммаНДСПропорциональноВсего), 2, , ПогрешностиОкругления, "СуммаНДСПропорциональноВал");
				СтрокаТаблицы.СуммаНДСУсловнойПродажи = СтрокаТаблицы.НДС - СтрокаТаблицы.ПроводкиСуммаНДСКредит;
			КонецЕсли;
		Иначе
			СтрокаТаблицы.ПроводкиСуммаНДСКредит = СтрокаТаблицы.НДС;
		КонецЕсли;

		СтрокаТаблицы.ПроводкиСуммаНДССебестоимость = СтрокаТаблицы.НДС - СтрокаТаблицы.ПроводкиСуммаНДСКредит;
		
		СтрокаТаблицы.СуммаНДСРегл = СтрокаТаблицы.НДС;
		СтрокаТаблицы.ПроводкиСуммаНДСРегл = СтрокаТаблицы.ПроводкиСуммаНДСКредит;
		
		СтрокаТаблицы.ПроводкиСуммаБезНДС 	= СтрокаТаблицы.ПроводкиСуммаСНДС - СтрокаТаблицы.ПроводкиСуммаНДСКредит;
		СтрокаТаблицы.ПроводкиСуммаБезНДСРегл 	= СтрокаТаблицы.ПроводкиСуммаБезНДС;
		
		СтрокаТаблицы.НоменклатурнаяГруппа = СтруктураШапкиДокумента.НоменклатурнаяГруппа;    		
		
		СтрокаТаблицы.Амортизируется = Ложь;
		
		Если (Не СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015) 
			ИЛИ (СтруктураШапкиДокумента.НеОтноситьНаЗатратыПоНУ)
			ИЛИ (СтрокаТаблицы.НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
			// пустое налоговое назначение может быть при поступлении в переработку или от комитента
			ИЛИ (СтрокаТаблицы.НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка())
			Тогда
			
			СтрокаТаблицы.СтоимостьНУ = 0;
			
		Иначе
			
			СтрокаТаблицы.СтоимостьНУ = СтрокаТаблицы.ПроводкиСуммаБезНДСРегл;
			
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры // ПодготовитьТаблицуТоваровРегл()		

Процедура ПодготовитьТаблицуОборудование(ТаблицаОборудование, СтруктураШапкиДокумента)

	ТаблицаОборудование.Колонки.Добавить("Сумма");
	ТаблицаОборудование.ЗагрузитьКолонку(ТаблицаОборудование.ВыгрузитьКолонку("ФактурнаяСтоимость"), "Сумма");

	ТаблицаОборудование.Колонки.Добавить("Качество");
	ТаблицаОборудование.ЗаполнитьЗначения(Справочники.Качество.Новый, "Качество");
	
	ПодготовитьТаблицуОборудованиеУпр(ТаблицаОборудование, СтруктураШапкиДокумента);
	ПодготовитьТаблицуОборудованиеРегл(ТаблицаОборудование, СтруктураШапкиДокумента);
	
КонецПроцедуры // ПодготовитьТаблицуОборудование()

Процедура ПодготовитьТаблицуОборудованиеУпр(ТаблицаТоваров, СтруктураШапкиДокумента)

	ТаблицаТоваров.Колонки.Добавить("Стоимость",     Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));

	// Недостающие поля.
	ТаблицаТоваров.Колонки.Добавить("СтатусПартии");

	Для каждого СтрокаТаблицы Из ТаблицаТоваров Цикл

		СтрокаТаблицы.Стоимость = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.Пошлина + СтрокаТаблицы.Акциз + СтрокаТаблицы.ТаможенныйСбор + ?(СтруктураШапкиДокумента.НеВключатьНДСВСтоимостьПартий, 0, СтрокаТаблицы.НДС)
										 , мВалютаРегламентированногоУчета,
										 СтруктураШапкиДокумента.ВалютаУправленческогоУчета, 
										 1, СтруктураШапкиДокумента.КурсВалютыУправленческогоУчета, 
										 1,СтруктураШапкиДокумента.КратностьВалютыУправленческогоУчета);

		// ГТД по комиссии и по переработке не делаются, только купленный
		СтрокаТаблицы.СтатусПартии = Перечисления.СтатусыПартийТоваров.Оборудование; 

	КонецЦикла;

КонецПроцедуры // ПодготовитьТаблицуОборудованиеУпр()	

Процедура ПодготовитьТаблицуОборудованиеРегл(ТаблицаТоваров, СтруктураШапкиДокумента)

	// Ставку НДС возьмём из разделов
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");

	// Суммы в документе в валюте регл. учета
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДСРегл"        	 		 , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("БазаНДС"        	 		 	 , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаБезНДС" 	 		 , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаНДСКредит"       	 , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаНДССебестоимость"  , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаСНДС"  			 , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаНДСУсловнойПродажи"        , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьНУ"                    , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	// для товаров комитента
	ТаблицаТоваров.Колонки.Добавить("НоменклатурнаяГруппа");
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаБезНДСРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаНДСРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТоваров.Колонки.Добавить("ПроводкиСуммаНДСРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	// для оборудования колонка Амортизируется = Истина
	ТаблицаТоваров.Колонки.Добавить("Амортизируется", Новый ОписаниеТипов("Булево"));

	ВидДеятельностиНДСНеоблагаемый 		= Перечисления.ВидыДеятельностиНДС.Необлагаемая;
	ВидДеятельностиНДСПропорциональный 	= Перечисления.ВидыДеятельностиНДС.ПропорциональноОблагаемая;
	НалоговоеНазначениеНДС_НеоблагаемаяНеХозДеятельность = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность;

	НеОблагаемыйВНД = Справочники.ВидыНалоговойДеятельности.НеОблагаемая;
	
	Для каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
		
		СтрокаТаблицы.СуммаСНДСРегл			= СтрокаТаблицы.Пошлина + СтрокаТаблицы.Акциз 
												+ СтрокаТаблицы.ТаможенныйСбор + СтрокаТаблицы.НДС;
		СтрокаТаблицы.БазаНДС				= СтрокаТаблицы.Пошлина + СтрокаТаблицы.Акциз 
												 + СтрокаТаблицы.ТаможеннаяСтоимость;
		
		СтрокаТаблицы.ПроводкиСуммаСНДС 	= СтрокаТаблицы.СуммаСНДСРегл;
		// Определим значение НДС в проводках
		// Ставку НДС получим из разделов
		СтрокаТаблицы.СтавкаНДС = Разделы[СтрокаТаблицы.НомерРаздела-1].СтавкаНДС;
		
		ПогрешностиОкругления     = Новый Соответствие;
 
		Если СтрокаТаблицы.ВидДеятельностиНДС = ВидДеятельностиНДСНеоблагаемый Тогда
			// нет налогового кредита
			СтрокаТаблицы.ПроводкиСуммаНДСКредит = 0;
			СтрокаТаблицы.СуммаНДСУсловнойПродажи = СтрокаТаблицы.НДС;
		ИначеЕсли СтрокаТаблицы.ВидДеятельностиНДС = ВидДеятельностиНДСПропорциональный Тогда
			Если СтруктураШапкиДокумента.СуммаНДСПропорциональноВсего = 0 Тогда
				СтрокаТаблицы.ПроводкиСуммаНДСКредит = 0;
				СтрокаТаблицы.СуммаНДСУсловнойПродажи = СтрокаТаблицы.НДС;
			Иначе	
				СтрокаТаблицы.ПроводкиСуммаНДСКредит = ОбщегоНазначения.ОкруглитьСУчетомПогрешности(СтрокаТаблицы.НДС * (СтруктураШапкиДокумента.СуммаНДСПропорциональноКредит / СтруктураШапкиДокумента.СуммаНДСПропорциональноВсего), 2, , ПогрешностиОкругления, "СуммаНДСПропорциональноВал");
				СтрокаТаблицы.СуммаНДСУсловнойПродажи = СтрокаТаблицы.НДС - СтрокаТаблицы.ПроводкиСуммаНДСКредит;
			КонецЕсли;
		Иначе
			СтрокаТаблицы.ПроводкиСуммаНДСКредит = СтрокаТаблицы.НДС;
		КонецЕсли;

		СтрокаТаблицы.ПроводкиСуммаНДССебестоимость = СтрокаТаблицы.НДС - СтрокаТаблицы.ПроводкиСуммаНДСКредит;
		
		
		СтрокаТаблицы.СуммаНДСРегл = СтрокаТаблицы.НДС;
		СтрокаТаблицы.ПроводкиСуммаНДСРегл = СтрокаТаблицы.ПроводкиСуммаНДСКредит;
		
		СтрокаТаблицы.ПроводкиСуммаБезНДС 	= СтрокаТаблицы.ПроводкиСуммаСНДС - СтрокаТаблицы.ПроводкиСуммаНДСКредит;
		СтрокаТаблицы.ПроводкиСуммаБезНДСРегл 	= СтрокаТаблицы.ПроводкиСуммаБезНДС;
		
		СтрокаТаблицы.СтоимостьНУ = СтрокаТаблицы.ПроводкиСуммаБезНДСРегл;
		
		СтрокаТаблицы.Амортизируется = Истина;
		
	КонецЦикла;

	ТаблицаТоваров.ЗаполнитьЗначения(СтруктураШапкиДокумента.НоменклатурнаяГруппа, "НоменклатурнаяГруппа");	
	
КонецПроцедуры // ПодготовитьТаблицуОборудованиеРегл()	

Процедура ПодготовитьПараметрыУчетнойПолитики(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	ПодготовитьПараметрыУчетнойПолитикиРегл(СтруктураШапкиДокумента, Отказ, Заголовок);
	
КонецПроцедуры // ПодготовитьПараметрыУчетнойПолитики()

Процедура ПодготовитьПараметрыУчетнойПолитикиРегл(СтруктураШапкиДокумента, Отказ, Заголовок)

	УчетнаяПолитикаРегл = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Дата, Организация,истина);
    Если НЕ ЗначениеЗаполнено(УчетнаяПолитикаРегл) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	
	Если НЕ Отказ Тогда
		СтруктураШапкиДокумента.Вставить("ВестиПартионныйУчетПоСкладамРегл", глЗначениеПеременной("ПараметрыПартионногоУчета").ВестиПартионныйУчетПоСкладамРегл);		
		СтруктураШапкиДокумента.Вставить("СпособОценкиМПЗРегл"             , УчетнаяПолитикаРегл.СпособОценкиМПЗ);
		
		СтруктураШапкиДокумента.Вставить("ЕстьНалогНаПрибыль"             , УчетнаяПолитикаРегл.ЕстьНалогНаПрибыль);
		СтруктураШапкиДокумента.Вставить("ЕстьНалогНаПрибыльДо2015"       , УчетнаяПолитикаРегл.ЕстьНалогНаПрибыльДо2015);
		СтруктураШапкиДокумента.Вставить("ЕстьНДС"                        , УчетнаяПолитикаРегл.ЕстьНДС);
	КонецЕсли;	

	
	
КонецПроцедуры // ПодготовитьПараметрыУчетнойПолитикиРегл()

// Дополняет полями, нужными для регл. учета
Процедура ДополнитьСтруктуруПолейТабличнойЧастиТоварыРегл(СтруктураПолей)

	СтруктураПолей.Вставить("СчетУчетаБУ"             	, "СчетУчетаБУ");
	СтруктураПолей.Вставить("Забалансовый"            	, "СчетУчетаБУ.Забалансовый");
	СтруктураПолей.Вставить("НалоговоеНазначение"		, "НалоговоеНазначение");
	СтруктураПолей.Вставить("ВидДеятельностиНДС"		, "НалоговоеНазначение.ВидДеятельностиНДС");
	СтруктураПолей.Вставить("ВидНалоговойДеятельности"	, "НалоговоеНазначение.ВидНалоговойДеятельности");
	СтруктураПолей.Вставить("СтатьяКнигиПриобретения"	, "СтатьяКнигиПриобретения");
    СтруктураПолей.Вставить("СчетУчетаНДС"             	, "СчетУчетаНДС");

КонецПроцедуры

// Дополняет полями, нужными для упр. учета
Процедура ДополнитьСтруктуруПолейТабличнойЧастиТоварыУпр(СтруктураПолей)

	СтруктураПолей.Вставить("СкладЗаказаПокупателя"		, "ЗаказПокупателя.СкладГруппа");
	СтруктураПолей.Вставить("ОбособленныйУчетТоваровПоЗаказамПокупателей", 
							"ЗаказПокупателя.ДоговорКонтрагента.ОбособленныйУчетТоваровПоЗаказамПокупателей");
КонецПроцедуры

// Дополняет полями, нужными для регл. учета
Процедура ДополнитьСтруктуруПолейТабличнойЧастиОборудованиеРегл(СтруктураПолей, СтруктураПростыхПолей)

	СтруктураПолей.Вставить("СчетУчетаБУ"             	, "СчетУчетаБУ");
	СтруктураПолей.Вставить("Забалансовый"            	, "СчетУчетаБУ.Забалансовый");
	СтруктураПолей.Вставить("НалоговоеНазначение"		, "НалоговоеНазначение");
	СтруктураПолей.Вставить("ВидДеятельностиНДС"		, "НалоговоеНазначение.ВидДеятельностиНДС");
	СтруктураПолей.Вставить("ВидНалоговойДеятельности"	, "НалоговоеНазначение.ВидНалоговойДеятельности");
	СтруктураПолей.Вставить("СтатьяКнигиПриобретения"	, "СтатьяКнигиПриобретения");
    СтруктураПолей.Вставить("СчетУчетаНДС"             	, "СчетУчетаНДС");

КонецПроцедуры

Процедура ДополнитьДеревоПолейЗапросаПоШапкеУпр(ДеревоПолейЗапросаПоШапке)
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "УчетнаяПолитика", "ВестиПартионныйУчетПоСкладам"  , "ВестиПартионныйУчетПоСкладам");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "УчетнаяПолитика", "СпособОценкиМПЗ"  			   , "СпособОценкиМПЗ");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Константы", "ВалютаУправленческогоУчета"     , "ВалютаУправленческогоУчета");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Константы", "КурсВалютыУправленческогоУчета" , "КурсВалютыУправленческогоУчета");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "УчетнаяПолитика", "ВедениеУчетаПоПроектам",    "ВедениеУчетаПоПроектам");

КонецПроцедуры // ДополнитьДеревоПолейЗапросаПоШапкеУпр()	

Процедура ДополнитьДеревоПолейЗапросаПоШапкеРегл(ДеревоПолейЗапросаПоШапке)

КонецПроцедуры // ДополнитьДеревоПолейЗапросаПоШапкеРегл()

// Формируем движения по регистрам.
//
// Параметры: 
//  СтруктураШапкиДокумента   - структура, содержащая рексвизиты шапки документа и результаты запроса по шапке
//  ТаблицаПоТоварам          - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  Отказ                     - флаг отказа в проведении
//
Процедура ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоОборудованию, Отказ, Заголовок)
	
	ДвиженияПоРегистрамУпр(СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоОборудованию,  Отказ, Заголовок);
	ДвиженияПоРегистрамРегл(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоОборудованию, Отказ, Заголовок);

КонецПроцедуры // ДвиженияПоРегистрам()

Процедура ДвиженияПоРегистрамУпр(СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоОборудованию, Отказ, Заголовок)
	Если СтруктураШапкиДокумента.ОтражатьВУправленческомУчете Тогда

		// ТОВАРЫ ПО РЕГИСТРУ ВзаиморасчетыСКонтрагентами

		НаборДвижений = Движения.ВзаиморасчетыСКонтрагентами;

		// Получим таблицу значений, совпадающую со структурой набора записей регистра.
		ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
		// Заполним таблицу движений.
		Если СтруктураШапкиДокумента.СуммаВзаиморасчетов <> 0 Тогда
			СтрокаДвижений = ТаблицаДвижений.Добавить();
			СтрокаДвижений.ДоговорКонтрагента = ДоговорКонтрагента;
			СтрокаДвижений.Контрагент  		   = Контрагент;
			СтрокаДвижений.Организация  	   = Организация;

			СтрокаДвижений.Сделка                = Неопределено;
			СтрокаДвижений.СуммаВзаиморасчетов   = СтруктураШапкиДокумента.СуммаВзаиморасчетов;
			СтрокаДвижений.СуммаУпр   = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтруктураШапкиДокумента.СуммаВзаиморасчетов, СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
													СтруктураШапкиДокумента.ВалютаУправленческогоУчета,
													СтруктураШапкиДокумента.КурсВзаиморасчетов, СтруктураШапкиДокумента.КурсВалютыУправленческогоУчета,
													СтруктураШапкиДокумента.КратностьВзаиморасчетов, СтруктураШапкиДокумента.КратностьВалютыУправленческогоУчета);
		КонецЕсли;

		НаборДвижений.мПериод            = Дата;
		НаборДвижений.мТаблицаДвижений   = ТаблицаДвижений;

		Если Не Отказ Тогда
			Движения.ВзаиморасчетыСКонтрагентами.ВыполнитьРасход();
		КонецЕсли;

		// ПО РЕГИСТРУ РасчетыСКонтрагентами
		НаборДвижений = Движения.РасчетыСКонтрагентами;

		// Получим таблицу значений, совпадающую со структурой набора записей регистра.
		ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
		// Заполним таблицу движений.
			СтрокаДвижений = ТаблицаДвижений.Добавить();
			СтрокаДвижений.РасчетыВозврат        = Перечисления.РасчетыВозврат.Расчеты;
			СтрокаДвижений.ДоговорКонтрагента = ДоговорКонтрагента;
			СтрокаДвижений.Контрагент  		   = Контрагент;
			СтрокаДвижений.Организация  	   = Организация;
        
			СтрокаДвижений.Сделка                = Неопределено;
			СтрокаДвижений.СуммаВзаиморасчетов   = СтруктураШапкиДокумента.СуммаВзаиморасчетов;
			СтрокаДвижений.СуммаУпр   = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтруктураШапкиДокумента.СуммаВзаиморасчетов, СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
			                            СтруктураШапкиДокумента.ВалютаУправленческогоУчета,
			                            СтруктураШапкиДокумента.КурсВзаиморасчетов, СтруктураШапкиДокумента.КурсВалютыУправленческогоУчета,
			                            СтруктураШапкиДокумента.КратностьВзаиморасчетов, СтруктураШапкиДокумента.КратностьВалютыУправленческогоУчета);
		НаборДвижений.мПериод            = Дата;
		НаборДвижений.мТаблицаДвижений   = ТаблицаДвижений;

		Если Не Отказ Тогда
			Движения.РасчетыСКонтрагентами.ВыполнитьРасход();
		КонецЕсли;

		Если НЕ (СтруктураШапкиДокумента.ДоговорПоставщикаТоваров.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом) Тогда
			
			// ПО ПАРТИЯМ
			
			УправлениеЗапасамиПартионныйУчет.ВыполнитьПриходПоРегистрамПартий(
				?(ДополнительныеСвойства.Свойство("ТаблицаСтаройРегистрацииВПоследовательности"),ДополнительныеСвойства.ТаблицаСтаройРегистрацииВПоследовательности,Неопределено),
			    СтруктураШапкиДокумента, 
				Отказ, 
				ТаблицаПоТоварам, 
				Неопределено, 
				ТаблицаПоОборудованию, 
				СтруктураШапкиДокумента.ОтражатьВУправленческомУчете,
				ЛОЖЬ);
			
			// ПО РЕГИСТРУ доп.расходов для последующего распределения
			
			НаборДвижений = Движения.ДопРасходыНаПриобретениеТоваров;

			// Получим таблицу значений, совпадающую со структурой набора записей регистра.
			ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
			
			ТабЗатрат = ТаблицаПоТоварам.Скопировать();
			
			// Из таблицы по товарам выделим строки, для которых НЕ заполнен документ оприходования
			Инд=0;
			Пока Инд<ТабЗатрат.Количество() Цикл
				
				Если ЗначениеЗаполнено(ТабЗатрат[Инд].ДокументОприходования) Тогда
					ТабЗатрат.Удалить(Инд);
				Иначе
					Инд=Инд+1;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ТабЗатрат.Колонки.Найти("Сумма")<>Неопределено Тогда
				ТабЗатрат.Колонки.Удалить("Сумма");
			КонецЕсли;
			ТабЗатрат.Колонки.Стоимость.Имя = "Сумма";
			
			// Заполним таблицу движений.
			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТабЗатрат, ТаблицаДвижений);
			ТабЗатрат = ТаблицаПоОборудованию.Скопировать();
			
			// Из таблицы по товарам выделим строки, для которых НЕ заполнен документ оприходования
			Инд=0;
			Пока Инд<ТабЗатрат.Количество() Цикл
				
				Если ЗначениеЗаполнено(ТабЗатрат[Инд].ДокументОприходования) Тогда
					ТабЗатрат.Удалить(Инд);
				Иначе
					Инд=Инд+1;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ТабЗатрат.Колонки.Найти("Сумма")<>Неопределено Тогда
				ТабЗатрат.Колонки.Удалить("Сумма");
			КонецЕсли;
			ТабЗатрат.Колонки.Стоимость.Имя = "Сумма";
			
			// Заполним таблицу движений.
			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТабЗатрат, ТаблицаДвижений);
			НаборДвижений.мПериод          = Дата;
			НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;

			Если Не Отказ Тогда
				Движения.ДопРасходыНаПриобретениеТоваров.ВыполнитьПриход();
			КонецЕсли;
		Иначе
			
			// Отнесем на затраты 
			// Подготовим структуру таблицы для отражения затрат.
			ТаблицаЗатрат = УправлениеЗатратами.СформироватьТаблицуЗатрат();
			
			ВалютаРеглУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
			ВалютаУпрУчета = глЗначениеПеременной("ВалютаУправленческогоУчета");
			КурсВалютыРеглУчета = МодульВалютногоУчета.ПолучитьКурсВалюты(ВалютаРеглУчета, СтруктураШапкиДокумента.Дата);
			КурсВалютыУпрУчета  = МодульВалютногоУчета.ПолучитьКурсВалюты(ВалютаУпрУчета,  СтруктураШапкиДокумента.Дата);
			
			
			Для каждого СтрокаТЧ Из ТаблицаПоТоварам Цикл
				НоваяСтрока = ТаблицаЗатрат.Добавить();
				НоваяСтрока.Сумма                = СтрокаТЧ.Пошлина + СтрокаТЧ.Акциз + СтрокаТЧ.НДС + СтрокаТЧ.ТаможенныйСбор;
				// пересчитаем сумму в валюту упр. учета
				НоваяСтрока.Сумма = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(НоваяСтрока.Сумма, 
										ВалютаРеглУчета, 
				                     	ВалютаУпрУчета,
				                        КурсВалютыРеглУчета.Курс, 
				                        КурсВалютыУпрУчета.Курс,
				                        КурсВалютыРеглУчета.Кратность, 
				                        КурсВалютыУпрУчета.Кратность);
				
				НоваяСтрока.Заказ                = СтрокаТЧ.Заказ;
				НоваяСтрока.СтатьяЗатрат         = СтатьяЗатрат;
				НоваяСтрока.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
				НоваяСтрока.Подразделение        = Подразделение;
			КонецЦикла;
			
			Для каждого СтрокаТЧ Из ТаблицаПоОборудованию Цикл
				НоваяСтрока = ТаблицаЗатрат.Добавить();
				НоваяСтрока.Сумма                = СтрокаТЧ.Пошлина + СтрокаТЧ.Акциз + СтрокаТЧ.НДС + СтрокаТЧ.ТаможенныйСбор;
				// пересчитаем сумму в валюту упр. учета
				НоваяСтрока.Сумма = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(НоваяСтрока.Сумма, 
										ВалютаРеглУчета, 
				                     	ВалютаУпрУчета,
				                        КурсВалютыРеглУчета.Курс, 
				                        КурсВалютыУпрУчета.Курс,
				                        КурсВалютыРеглУчета.Кратность, 
				                        КурсВалютыУпрУчета.Кратность);
				НоваяСтрока.Заказ                = СтрокаТЧ.Заказ;
				НоваяСтрока.СтатьяЗатрат         = СтатьяЗатрат;
				НоваяСтрока.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
				НоваяСтрока.Подразделение        = Подразделение;
			КонецЦикла;
			
			// отразим затраты по УУ
			УправлениеЗатратами.ДвиженияПоПрочимЗатратам(
				СтруктураШапкиДокумента,
				ТаблицаЗатрат,
				Перечисления.ВидыОтраженияВУчете.ОтражатьВУправленческомУчете
			);
			
		КонецЕсли;
		
		// ПО РЕГИСТРУ Закупки
		НаборДвижений = Движения.Закупки;

		// Получим таблицу значений, совпадающую со структурой набора записей регистра.
		ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();

		// Подготовим таблицу товаров для регистра закупки.
		ТаблицаПоТоварамЗакупки = ТаблицаПоТоварам.Скопировать();
		
		ТаблицаПоТоварамЗакупки.Колонки.ДокументОприходования.Имя = "ДокументЗакупки";
		ТабИмен = Неопределено;
		ОбщегоНазначения.ПереименоватьКолонкуТаблицыЗначений(ТаблицаПоОборудованию, ТабИмен, "ДокументОприходования", "ДокументЗакупки");
		
		Если СтруктураШапкиДокумента.ВедениеУчетаПоПроектам Тогда
			
			УправлениеПроектами.ОтразитьДвиженияПоПроектам(ТаблицаПоТоварамЗакупки, ТаблицаДвижений, Проект, Дата, "Закупки");
			УправлениеПроектами.ОтразитьДвиженияПоПроектам(ТаблицаПоОборудованию, ТаблицаДвижений, Проект, Дата, "Закупки");
			
		Иначе		
			
			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоТоварамЗакупки, ТаблицаДвижений);
			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоОборудованию, ТаблицаДвижений);
			
		КонецЕсли;
		
		ОбщегоНазначения.ВосстановитьИменаКолонокТаблицыЗначений(ТаблицаПоОборудованию, ТабИмен);
		
		ТаблицаДвижений.ЗаполнитьЗначения(0,                  "Количество");
		ТаблицаДвижений.ЗаполнитьЗначения(Подразделение,      "Подразделение");
		ТаблицаДвижений.ЗаполнитьЗначения(ДоговорКонтрагента, "ДоговорКонтрагента");
		ТаблицаДвижений.ЗаполнитьЗначения(Контрагент, 		  "Контрагент");
		ТаблицаДвижений.ЗаполнитьЗначения(Организация, 		  "Организация");
		
		Для Каждого СтрокаТаблицы Из ТаблицаДвижений Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументЗакупки) Тогда
				СтрокаТаблицы.ДокументЗакупки = Ссылка;
			КонецЕсли;
		КонецЦикла;
		
		ВалютаРеглУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
		ВалютаУпрУчета = глЗначениеПеременной("ВалютаУправленческогоУчета");
		КурсВалютыРеглУчета = МодульВалютногоУчета.ПолучитьКурсВалюты(ВалютаРеглУчета, СтруктураШапкиДокумента.Дата);
		КурсВалютыУпрУчета  = МодульВалютногоУчета.ПолучитьКурсВалюты(ВалютаУпрУчета,  СтруктураШапкиДокумента.Дата);
		
		Для Каждого СтрокаТаблицы Из ТаблицаДвижений Цикл
			СтрокаТаблицы.НДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(
									СтрокаТаблицы.НДС, 
									ВалютаРеглУчета, 
				                    ВалютаУпрУчета,
				                    КурсВалютыРеглУчета.Курс, 
				                    КурсВалютыУпрУчета.Курс,
				                    КурсВалютыРеглУчета.Кратность, 
				                    КурсВалютыУпрУчета.Кратность
								);
		КонецЦикла;
		
		НаборДвижений.мПериод          = Дата;
		НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;

		Если Не Отказ Тогда
			Движения.Закупки.ВыполнитьДвижения();
		КонецЕсли;

	КонецЕсли; // ОтражатьВУправленческомУчете

КонецПроцедуры // ДвиженияПоРегистрамУпр()

Функция ПодготовитьТаблицуДвиженийДляРегистраРасчетовПоПриобретению(СтруктураШапкиДокумента, ТаблицаПоТоварам)
	
	// ТОВАРЫ
	ТаблицаКопия = ТаблицаПоТоварам.Скопировать();
	ТаблицаКопия.Свернуть("","СуммаСНДСРегл");
	
	ТаблицаКопия.Колонки.Добавить("СуммаСНДСВал"		, Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	Для каждого СтрокаТаблицы Из ТаблицаКопия Цикл
	
		СтрокаТаблицы.СуммаСНДСВал = СтрокаТаблицы.СуммаСНДСРегл;
	
	КонецЦикла; 
	
	ТаблицаДвижений = ТаблицаКопия.Скопировать();
	
	ТаблицаДвижений.Колонки.Добавить("Контрагент");
	ТаблицаДвижений.Колонки.Добавить("ДоговорКонтрагента");
	ТаблицаДвижений.Колонки.Добавить("ВидДоговораКонтрагента");
	ТаблицаДвижений.Колонки.Добавить("СчетОплаты");
	ТаблицаДвижений.Колонки.Добавить("СчетАванса");
	ТаблицаДвижений.Колонки.Добавить("СделкаРегл");
	
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.Контрагент        , "Контрагент");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ДоговорКонтрагента, "ДоговорКонтрагента");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ВидДоговораКонтрагента, "ВидДоговораКонтрагента");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.СчетУчетаРасчетовСКонтрагентом, "СчетОплаты");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.СчетУчетаРасчетовПоАвансам    , "СчетАванса");
	
	Возврат ТаблицаДвижений;
КонецФункции

// Формирует бухгалтерские и налоговые проводки по регистрам
// 
Процедура ДвиженияПоРегистрамРегл(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоОборудованию, Отказ, Заголовок)

	Если НЕ СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		Возврат;
	КонецЕсли;

	ДатаДок    = Дата;
	ПроводкиБУ = Движения.Хозрасчетный;
	
	ПоставщикКомитент = (СтруктураШапкиДокумента.ДоговорПоставщикаТоваров.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
	Если ПоставщикКомитент Тогда
		
		ПроизводственныеРасходы = УправлениеЗатратами.ПроверитьСчетЗатратНаПроизводственныеРасходы(СтруктураШапкиДокумента.СчетЗатрат);
	
	КонецЕсли;

	// Проводки по товарам и оборудованию
	МассивСтрок = Новый Массив;
	
	// занесём все строки в один массив, чтобы работать с ними единообразно
	Для каждого СтрокаТаблицы Из ТаблицаПоТоварам Цикл
		МассивСтрок.Добавить(СтрокаТаблицы);
	КонецЦикла;
	Для каждого СтрокаТаблицы Из ТаблицаПоОборудованию Цикл
		МассивСтрок.Добавить(СтрокаТаблицы);
	КонецЦикла;		
	
	Для каждого СтрокаТаблицы Из МассивСтрок Цикл

		// Бухгалтерский учет
		// Пошлина
		Если НЕ СтрокаТаблицы.Пошлина = 0 Тогда
		
			Проводка = ПроводкиБУ.Добавить();

			Проводка.Период                     = ДатаДок;
			Проводка.Активность                 = Истина;
			Проводка.Организация                = СтруктураШапкиДокумента.Организация;
			Проводка.Сумма                      = СтрокаТаблицы.Пошлина;
			Проводка.Содержание                 = "Таможенная пошлина";
			Проводка.НомерЖурнала               = "";

			Если НЕ ПоставщикКомитент Тогда
				
				Проводка.СчетДт                     = СтрокаТаблицы.СчетУчетаБУ;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Номенклатура"        , СтрокаТаблицы.Номенклатура);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Склады"              , СтрокаТаблицы.Склад);
				Проводка.КоличествоДт               = 0;
				
				Проводка.НалоговоеНазначениеДт		= СтрокаТаблицы.НалоговоеНазначение;
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 И 
					СтруктураШапкиДокумента.НеОтноситьНаЗатратыПоНУ = Ложь И 
					СтрокаТаблицы.НалоговоеНазначение <> Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность Тогда
					
					Проводка.СуммаНУДт = Проводка.Сумма;
					
				КонецЕсли;
				

			Иначе	
				
				// ГТД от комитента - все на затраты
				Проводка.СчетДт                     = СтруктураШапкиДокумента.СчетЗатрат;
				Если ПроизводственныеРасходы Тогда
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Подразделения"       , СтруктураШапкиДокумента.ПодразделениеОрганизации);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НоменклатурныеГруппы", СтруктураШапкиДокумента.НоменклатурнаяГруппа);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СтатьиЗатрат"        , СтруктураШапкиДокумента.СтатьяЗатрат);
				Иначе
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтруктураШапкиДокумента.ЗатратСубконто1);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтруктураШапкиДокумента.ЗатратСубконто2);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтруктураШапкиДокумента.ЗатратСубконто3);
				КонецЕсли;
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
					Проводка.НалоговоеНазначениеДт = СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
				    Если СтруктураШапкиДокумента.НеОтноситьНаЗатратыПоНУ = Ложь И 
							СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат <> Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
						Проводка.СуммаНУДт = Проводка.Сумма;
					КонецЕсли;
				КонецЕсли;
			
			КонецЕсли; 

			Проводка.СчетКт                     = СтруктураШапкиДокумента.СчетУчетаПошлины;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1 , СтруктураШапкиДокумента.ПошлинаСубконто1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2 , СтруктураШапкиДокумента.ПошлинаСубконто2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3 , СтруктураШапкиДокумента.ПошлинаСубконто3);
		
		КонецЕсли; 
		
		// Акциз
		Если НЕ СтрокаТаблицы.Акциз = 0 Тогда
			
			Проводка = ПроводкиБУ.Добавить();

			Проводка.Период                     = ДатаДок;
			Проводка.Активность                 = Истина;
			Проводка.Организация                = СтруктураШапкиДокумента.Организация;
			Проводка.Сумма                      = СтрокаТаблицы.Акциз;
			Проводка.Содержание                 = "Акциз";
			Проводка.НомерЖурнала               = "";
			
			Если НЕ ПоставщикКомитент Тогда

				Проводка.СчетДт                     = СтрокаТаблицы.СчетУчетаБУ;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Номенклатура"        , СтрокаТаблицы.Номенклатура);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Склады"              , СтрокаТаблицы.Склад);
				Проводка.КоличествоДт               = 0;
				
				Проводка.НалоговоеНазначениеДт		= СтрокаТаблицы.НалоговоеНазначение;
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 И 
					СтруктураШапкиДокумента.НеОтноситьНаЗатратыПоНУ = Ложь И 
					СтрокаТаблицы.НалоговоеНазначение <> Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность Тогда
					
					Проводка.СуммаНУДт = Проводка.Сумма;
					
				КонецЕсли;
				

			Иначе	
				
				// ГТД от комитента - все на затраты
				Проводка.СчетДт                     = СтруктураШапкиДокумента.СчетЗатрат;
				Если ПроизводственныеРасходы Тогда
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Подразделения"       , СтруктураШапкиДокумента.ПодразделениеОрганизации);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НоменклатурныеГруппы", СтруктураШапкиДокумента.НоменклатурнаяГруппа);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СтатьиЗатрат"        , СтруктураШапкиДокумента.СтатьяЗатрат);
				Иначе
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтруктураШапкиДокумента.ЗатратСубконто1);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтруктураШапкиДокумента.ЗатратСубконто2);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтруктураШапкиДокумента.ЗатратСубконто3);
				КонецЕсли;
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
					Проводка.НалоговоеНазначениеДт		= СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
				    Если СтруктураШапкиДокумента.НеОтноситьНаЗатратыПоНУ = Ложь И 
							СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат <> Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
					
						Проводка.СуммаНУДт = Проводка.Сумма;
					КонецЕсли;
				КонецЕсли;
				
			
			КонецЕсли; 

			Проводка.СчетКт                     = СтруктураШапкиДокумента.СчетУчетаАкциза;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1 , СтруктураШапкиДокумента.АкцизСубконто1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2 , СтруктураШапкиДокумента.АкцизСубконто2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3 , СтруктураШапкиДокумента.АкцизСубконто3);
			
		КонецЕсли; 
				
		// Таможенный сбор
		Если НЕ СтрокаТаблицы.ТаможенныйСбор = 0 Тогда
			
			Проводка = ПроводкиБУ.Добавить();

			Проводка.Период                     = ДатаДок;
			Проводка.Активность                 = Истина;
			Проводка.Организация                = СтруктураШапкиДокумента.Организация;
			Проводка.Сумма                      = СтрокаТаблицы.ТаможенныйСбор;
			Проводка.Содержание                 = "Таможенный сбор";
			Проводка.НомерЖурнала               = "";

			Если НЕ ПоставщикКомитент Тогда
						
				Проводка.СчетДт                     = СтрокаТаблицы.СчетУчетаБУ;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Номенклатура"        , СтрокаТаблицы.Номенклатура);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Склады"              , СтрокаТаблицы.Склад);
				Проводка.КоличествоДт               = 0;
				
				Проводка.НалоговоеНазначениеДт		= СтрокаТаблицы.НалоговоеНазначение;
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 И 
					СтруктураШапкиДокумента.НеОтноситьНаЗатратыПоНУ = Ложь И 
					СтрокаТаблицы.НалоговоеНазначение <> Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность Тогда
					
					Проводка.СуммаНУДт = Проводка.Сумма;
					
				КонецЕсли;
				
				
			Иначе	
				
				// ГТД от комитента - все на затраты
				Проводка.СчетДт                     = СтруктураШапкиДокумента.СчетЗатрат;
				Если ПроизводственныеРасходы Тогда
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Подразделения"       , СтруктураШапкиДокумента.ПодразделениеОрганизации);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НоменклатурныеГруппы", СтруктураШапкиДокумента.НоменклатурнаяГруппа);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СтатьиЗатрат"        , СтруктураШапкиДокумента.СтатьяЗатрат);
				Иначе
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтруктураШапкиДокумента.ЗатратСубконто1);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтруктураШапкиДокумента.ЗатратСубконто2);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтруктураШапкиДокумента.ЗатратСубконто3);
				КонецЕсли;
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
					Проводка.НалоговоеНазначениеДт		= СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
				    Если СтруктураШапкиДокумента.НеОтноситьНаЗатратыПоНУ = Ложь И 
							СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат <> Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
					
						Проводка.СуммаНУДт = Проводка.Сумма;
					КонецЕсли;
				КонецЕсли;
			
			КонецЕсли; 
			
			Проводка.СчетКт                     = СтруктураШапкиДокумента.СчетУчетаРасчетовСКонтрагентом;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты", СтруктураШапкиДокумента.Контрагент);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Договоры",    СтруктураШапкиДокумента.ДоговорКонтрагента);
				
		КонецЕсли; 
		
		// НДС (если он идет на себестоимость)
		Если НЕ СтрокаТаблицы.ПроводкиСуммаНДССебестоимость = 0 Тогда
			
			Проводка = ПроводкиБУ.Добавить();

			Проводка.Период                     = ДатаДок;
			Проводка.Активность                 = Истина;
			Проводка.Организация                = СтруктураШапкиДокумента.Организация;
			Проводка.Сумма                      = СтрокаТаблицы.ПроводкиСуммаНДССебестоимость;
			Если НЕ ПоставщикКомитент Тогда
				Проводка.Содержание                 = "НДС: на себестоимость запасов";
			Иначе
				Проводка.Содержание                 = "НДС: на затраты";
			КонецЕсли;
			Проводка.НомерЖурнала               = "";

			
			Если НЕ ПоставщикКомитент Тогда
				
				Проводка.СчетДт                     = СтрокаТаблицы.СчетУчетаБУ;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Номенклатура"        , СтрокаТаблицы.Номенклатура);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Склады"              , СтрокаТаблицы.Склад);
				Проводка.КоличествоДт               = 0;
				
				Проводка.НалоговоеНазначениеДт		= СтрокаТаблицы.НалоговоеНазначение;
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 И 
					СтруктураШапкиДокумента.НеОтноситьНаЗатратыПоНУ = Ложь И 
					СтрокаТаблицы.НалоговоеНазначение <> Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность Тогда
					
					Проводка.СуммаНУДт = Проводка.Сумма;
					
				КонецЕсли;
				
			Иначе	
				
				// ГТД от комитента - все на затраты
				Проводка.СчетДт                     = СтруктураШапкиДокумента.СчетЗатрат;
				Если ПроизводственныеРасходы Тогда
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Подразделения"       , СтруктураШапкиДокумента.ПодразделениеОрганизации);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НоменклатурныеГруппы", СтруктураШапкиДокумента.НоменклатурнаяГруппа);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СтатьиЗатрат"        , СтруктураШапкиДокумента.СтатьяЗатрат);
				Иначе
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтруктураШапкиДокумента.ЗатратСубконто1);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтруктураШапкиДокумента.ЗатратСубконто2);
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтруктураШапкиДокумента.ЗатратСубконто3);
				КонецЕсли;
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыльДо2015 Тогда
					Проводка.НалоговоеНазначениеДт		= СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат;
				    Если СтруктураШапкиДокумента.НеОтноситьНаЗатратыПоНУ = Ложь И 
							СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат <> Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность Тогда
					
						Проводка.СуммаНУДт = Проводка.Сумма;
					КонецЕсли;
				КонецЕсли;
				
			
			КонецЕсли; 
			
			Если СтруктураШапкиДокумента.ОформленНалоговыйВексельПоНДС Тогда
				
				Проводка.СчетКт                     = СтруктураШапкиДокумента.СчетУчетаВексель;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1 , СтруктураШапкиДокумента.ВексельСубконто1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2 , СтруктураШапкиДокумента.ВексельСубконто2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3 , СтруктураШапкиДокумента.ВексельСубконто3);
				
			Иначе
				
				Проводка.СчетКт                     = СтруктураШапкиДокумента.СчетУчетаРасчетовСКонтрагентом;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты", СтруктураШапкиДокумента.Контрагент);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Договоры",    СтруктураШапкиДокумента.ДоговорКонтрагента);
			
			КонецЕсли; 
				
		КонецЕсли; 
		
	КонецЦикла; // Проводки по товарам и оборудованию
	
	// Взаиморасчетам с таможней по пошлине
	ВсегоПошлина = ТаблицаПоТоварам.Итог("Пошлина")+ТаблицаПоОборудованию.Итог("Пошлина");
	Если НЕ ВсегоПошлина = 0 Тогда
	
		Проводка = ПроводкиБУ.Добавить();

		Проводка.Период                     = ДатаДок;
		Проводка.Активность                 = Истина;
		Проводка.Организация                = СтруктураШапкиДокумента.Организация;
		Проводка.Сумма                      = ВсегоПошлина;
		Проводка.Содержание                 = "Таможенная пошлина";
		Проводка.НомерЖурнала               = "";

		Проводка.СчетДт                     = СтруктураШапкиДокумента.СчетУчетаПошлины;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1 , СтруктураШапкиДокумента.ПошлинаСубконто1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2 , СтруктураШапкиДокумента.ПошлинаСубконто2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3 , СтруктураШапкиДокумента.ПошлинаСубконто3);
		
		Проводка.СчетКт                     = СтруктураШапкиДокумента.СчетУчетаРасчетовСКонтрагентом;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты", СтруктураШапкиДокумента.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Договоры",    СтруктураШапкиДокумента.ДоговорКонтрагента);
		
	КонецЕсли;
	
	// Взаиморасчетам с таможней по акцизу
	ВсегоАкциз = ТаблицаПоТоварам.Итог("Акциз") + ТаблицаПоОборудованию.Итог("Акциз");
	Если НЕ ВсегоАкциз = 0 Тогда
		
		Проводка = ПроводкиБУ.Добавить();

		Проводка.Период                     = ДатаДок;
		Проводка.Активность                 = Истина;
		Проводка.Организация                = СтруктураШапкиДокумента.Организация;
		Проводка.Сумма                      = ВсегоАкциз;
		Проводка.Содержание                 = "Акциз";
		Проводка.НомерЖурнала               = "";
		
		Проводка.СчетДт                     = СтруктураШапкиДокумента.СчетУчетаАкциза;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1 , СтруктураШапкиДокумента.АкцизСубконто1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2 , СтруктураШапкиДокумента.АкцизСубконто2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3 , СтруктураШапкиДокумента.АкцизСубконто3);
		
		Проводка.СчетКт                     = СтруктураШапкиДокумента.СчетУчетаРасчетовСКонтрагентом;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты", СтруктураШапкиДокумента.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Договоры",    СтруктураШапкиДокумента.ДоговорКонтрагента);
		
	КонецЕсли;
	
	// налоговый кредит по НДС
	ВсегоНДСКредит = ТаблицаПоТоварам.Итог("ПроводкиСуммаНДСКредит") + ТаблицаПоОборудованию.Итог("ПроводкиСуммаНДСКредит");
	Если НЕ ВсегоНДСКредит = 0 Тогда
	

		Если СтруктураШапкиДокумента.ОформленНалоговыйВексельПоНДС Тогда
			
			ТаблицаКопия = ТаблицаПоТоварам.Скопировать();
			ТаблицаКопия.Свернуть("СчетУчетаНДС", "ПроводкиСуммаНДСКредит");
			
			ТаблицаКопияОборудование = ТаблицаПоОборудованию.Скопировать();
			ТаблицаКопияОборудование.Свернуть("СчетУчетаНДС", "ПроводкиСуммаНДСКредит");
			
			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаКопияОборудование,ТаблицаКопия);
			ТаблицаКопия.Свернуть("СчетУчетаНДС", "ПроводкиСуммаНДСКредит");
			
			Для каждого Строка Из ТаблицаКопия Цикл
			
				Проводка = ПроводкиБУ.Добавить();

				Проводка.Период                     = ДатаДок;
				Проводка.Активность                 = Истина;
				Проводка.Организация                = СтруктураШапкиДокумента.Организация;
				Проводка.Сумма                      = Строка.ПроводкиСуммаНДСКредит;
				Проводка.Содержание                 = "НДС: оформлен вексель";
				Проводка.НомерЖурнала               = "";

				Проводка.СчетДт                     = Строка.СчетУчетаНДС;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Контрагенты", СтруктураШапкиДокумента.ПоставщикТоваров);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Договоры"   , СтруктураШапкиДокумента.ДоговорПоставщикаТоваров);
					
				Проводка.СчетКт                     = СтруктураШапкиДокумента.СчетУчетаВексель;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1 , СтруктураШапкиДокумента.ВексельСубконто1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2 , СтруктураШапкиДокумента.ВексельСубконто2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3 , СтруктураШапкиДокумента.ВексельСубконто3);
			
			КонецЦикла; 
			
		Иначе
			
			Проводка = ПроводкиБУ.Добавить();

			Проводка.Период                     = ДатаДок;
			Проводка.Активность                 = Истина;
			Проводка.Организация                = СтруктураШапкиДокумента.Организация;
			Проводка.Сумма                      = ВсегоНДСКредит;
			Проводка.Содержание                 = "НДС: налоговый кредит";
			Проводка.НомерЖурнала               = "";

			Проводка.СчетДт                     = СтруктураШапкиДокумента.СчетУчетаНДС;
			Если НЕ ПоставщикКомитент Тогда
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1 , СтруктураШапкиДокумента.НДССубконто1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2 , СтруктураШапкиДокумента.НДССубконто2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3 , СтруктураШапкиДокумента.НДССубконто3);
			Иначе
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Контрагенты", СтруктураШапкиДокумента.ПоставщикТоваров);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Договоры",    СтруктураШапкиДокумента.ДоговорПоставщикаТоваров);
			КонецЕсли;
			
			Проводка.СчетКт                     = СтруктураШапкиДокумента.СчетУчетаРасчетовСКонтрагентом;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты", СтруктураШапкиДокумента.Контрагент);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Договоры",    СтруктураШапкиДокумента.ДоговорКонтрагента);
		
		КонецЕсли; 
		
	КонецЕсли;
	
	// налоговый кредит по НДС для товаров, не относящихся к обл. деятельности после 01.07.2015
	СуммаНДСУсловнойПродажи = ТаблицаПоТоварам.Итог("СуммаНДСУсловнойПродажи") + ТаблицаПоОборудованию.Итог("СуммаНДСУсловнойПродажи");
	
	Если НЕ СуммаНДСУсловнойПродажи = 0 И Дата >= глЗначениеПеременной("ДатаВступленияВСилуЗУ643") Тогда
 			
		Проводка = ПроводкиБУ.Добавить();

		Проводка.Период                     = ДатаДок;
		Проводка.Активность                 = Истина;
		Проводка.Организация                = СтруктураШапкиДокумента.Организация;
		Проводка.Сумма                      = СуммаНДСУсловнойПродажи;
		Проводка.Содержание                 = "Налоговый кредит по операциям не обл. НДС после 01.07.2015";
		Проводка.НомерЖурнала               = "";

		Проводка.СчетДт                     = СтруктураШапкиДокумента.СчетУчетаНДС;
		Если НЕ ПоставщикКомитент Тогда
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1 , СтруктураШапкиДокумента.НДССубконто1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2 , СтруктураШапкиДокумента.НДССубконто2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3 , СтруктураШапкиДокумента.НДССубконто3);
		Иначе
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Контрагенты", СтруктураШапкиДокумента.ПоставщикТоваров);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Договоры",    СтруктураШапкиДокумента.ДоговорПоставщикаТоваров);
		КонецЕсли;
			
		Проводка.СчетКт                     = СтруктураШапкиДокумента.СчетНДСУсловнаяПродажа;
		
		// ожидаемый и подтвержденный НДС на условную продажу того кредита, который был показан по товарам, не относящимся к облагаемой деятельности по НДС
		Если СтруктураШапкиДокумента.ЕстьНДС Тогда
		
			НаборДвижений = Движения.ОжидаемыйИПодтвержденныйНДСПродаж;
			
			// Получим таблицу значений, совпадающую со структурой набора записей регистра.
			ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
			
			// ТОВАРЫ
			ТаблицаКопия = ТаблицаПоТоварам.Скопировать();                                                                                          
			Сч = 0;
			Пока Сч < ТаблицаКопия.Количество() Цикл
			
				СтрокаТоваров = ТаблицаКопия[Сч];	
				Если СтрокаТоваров.СуммаНДСУсловнойПродажи = 0 Тогда
					ТаблицаКопия.Удалить(СтрокаТоваров);                      
				Иначе
					Сч = Сч + 1;
				КонецЕсли;	
			
			КонецЦикла;
			
			ТаблицаКопия.Свернуть("СтавкаНДС, НалоговоеНазначение", 
						  "БазаНДС, СуммаНДСУсловнойПродажи");
			ТаблицаКопия.Колонки.СуммаНДСУсловнойПродажи.Имя = "СуммаНДС";
			
			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаКопия, ТаблицаДвижений);
			
			// Оборудование
			ТаблицаКопия = ТаблицаПоОборудованию.Скопировать();                                                                                          
			Сч = 0;
			Пока Сч < ТаблицаКопия.Количество() Цикл
			
				СтрокаТоваров = ТаблицаКопия[Сч];	
				Если СтрокаТоваров.СуммаНДСУсловнойПродажи = 0 Тогда
					ТаблицаКопия.Удалить(СтрокаТоваров);                      
				Иначе
					Сч = Сч + 1;
				КонецЕсли;	
			
			КонецЦикла;
			
			ТаблицаКопия.Свернуть("СтавкаНДС, НалоговоеНазначение", 
						  "БазаНДС, СуммаНДСУсловнойПродажи");
			ТаблицаКопия.Колонки.СуммаНДСУсловнойПродажи.Имя = "СуммаНДС";
			
			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаКопия, ТаблицаДвижений);
			
			ТаблицаДвижений.ЗаполнитьЗначения(Организация       , "Организация");
			
			ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СобытияОжидаемыйИПодтвержденныйНДСПродаж.УсловнаяПродажа,    "СобытиеНДС");
			ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийОжидаемыйИПодтвержденныйНДСПродаж.ОжидаемыйНДС,  "КодОперации");
				
			Если НЕ Отказ И ТаблицаДвижений.Количество() > 0 Тогда
				
				НаборДвижений.мПериод          = Дата;
				НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
				
				Движения.ОжидаемыйИПодтвержденныйНДСПродаж.ВыполнитьПриход();
			
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЕсли;
	
	СуммаСНДСДокумента = СтруктураШапкиДокумента.СуммаВзаиморасчетов;
	
	// Движения по налоговым взаиморасчетам и зачет аванса
	СтруктураПараметровЗачетАванса = БухгалтерскийУчетРасчетовСКонтрагентами.ПодготовкаСтруктурыПараметровДляЗачетаАванса(Ссылка, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета, Заголовок, СуммаСНДСДокумента);
	Если НЕ СтруктураПараметровЗачетАванса = Ложь тогда
		//Объединим таблицы Товары и Оборудование
		ТаблицаКопия = ТаблицаПоТоварам.Скопировать();
        ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоОборудованию, ТаблицаКопия);
		
		ДвиженияДляРегистраРасчетовПоПриобретению = ПодготовитьТаблицуДвиженийДляРегистраРасчетовПоПриобретению(СтруктураШапкиДокумента, ТаблицаКопия);
	 	СтруктураДвижений = Новый Структура("ПроводкиБУ", ПроводкиБУ); 
 		БухгалтерскийУчетРасчетовСКонтрагентами.ЗачетАванса(СтруктураПараметровЗачетАванса, СтруктураДвижений, мВалютаРегламентированногоУчета,РежимПроведения, ЭтотОбъект, ДвиженияДляРегистраРасчетовПоПриобретению, Отказ, Истина);
	КонецЕсли;
	
	Если НЕ ПоставщикКомитент Тогда
		
		// ПО ПАРТИЯМ
		
		УправлениеЗапасамиПартионныйУчет.ВыполнитьПриходПоРегистрамПартий(
			?(ДополнительныеСвойства.Свойство("ТаблицаСтаройРегистрацииВПоследовательности"),ДополнительныеСвойства.ТаблицаСтаройРегистрацииВПоследовательности,Неопределено),
			СтруктураШапкиДокумента, 
			Отказ, 
			ТаблицаПоТоварам, 
			Неопределено, 
			ТаблицаПоОборудованию, 
			ЛОЖЬ,
			СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете);
		
	Иначе
		//отнесем на затраты
		
		// Подготовим структуру таблицы для отражения затрат.
		ТаблицаЗатрат = УправлениеЗатратами.СформироватьТаблицуЗатрат();
		
		Для каждого СтрокаТЧ Из МассивСтрок Цикл
			НоваяСтрока = ТаблицаЗатрат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
			
			НоваяСтрока.СуммаРегл = СтрокаТЧ.ПроводкиСуммаБезНДСРегл;
			НоваяСтрока.СуммаНал  = СтрокаТЧ.СтоимостьНУ;
			НоваяСтрока.Сумма     = 0;
			
			НоваяСтрока.Заказ                    = СтрокаТЧ.Заказ;
			НоваяСтрока.СчетЗатрат               = СтруктураШапкиДокумента.СчетЗатрат;
			НоваяСтрока.СтатьяЗатрат         	 = СтруктураШапкиДокумента.СтатьяЗатрат;
			НоваяСтрока.НоменклатурнаяГруппа     = СтруктураШапкиДокумента.НоменклатурнаяГруппа;
			НоваяСтрока.ПодразделениеОрганизации = СтруктураШапкиДокумента.ПодразделениеОрганизации;
			
			НоваяСтрока.НалоговоеНазначение      = СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат; 
			НоваяСтрока.НалоговоеНазначениеДоходовИЗатрат = СтруктураШапкиДокумента.НалоговоеНазначениеДоходовИЗатрат; 
			
		КонецЦикла;
		
		// отразим затраты по регл. учету
		УправлениеЗатратами.ДвиженияПоПрочимЗатратам(
			СтруктураШапкиДокумента,
			ТаблицаЗатрат,
			Перечисления.ВидыОтраженияВУчете.ОтражатьВРегламентированномУчете
		);
		
		
	КонецЕсли;
		
	Если ОтражатьВБухгалтерскомУчете И СтруктураШапкиДокумента.ЕстьНДС Тогда
			
		// поскольку ГТД является налговым документом, то пишем сразу в книгу приобретений
		
		// Движения по регистру "Книга приобретений"
		НаборДвижений = Движения.КнигаПриобретений;
		
		// Получим таблицу значений, совпадающую со структурой набора записей регистра.
		ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();
		
		ТаблицаКопия = ТаблицаПоТоварам.Скопировать();
	    ИсходноеКоличествоСтрок = ТаблицаКопия.Количество();
		Для НомСтроки = 1 По ИсходноеКоличествоСтрок Цикл
			ТекСтрока = ТаблицаКопия[НомСтроки-1];
			Если НЕ ТекСтрока.ВидДеятельностиНДС = Перечисления.ВидыДеятельностиНДС.ПропорциональноОблагаемая Тогда
				Продолжить;
			КонецЕсли;
			
			// добавим строку по пропорц. НДС, относящемуся на кредит, текущая строка останется такой, что не относится на кредит
			ДобавленнаяСтрока = ТаблицаКопия.Добавить();
			ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, ТекСтрока);
			
			ПроводкиСуммаНДСКредит = ТекСтрока.ПроводкиСуммаНДСКредит;
			Если Дата >= глЗначениеПеременной("ДатаВступленияВСилуЗУ643") Тогда
				ПроводкиСуммаНДСКредит = ТекСтрока.НДС - ТекСтрока.СуммаНДСУсловнойПродажи;	
			КонецЕсли;
			ДобавленнаяСтрока.БазаНДС = ?(ТекСтрока.НДС = 0, 0, ТекСтрока.БазаНДС * (ПроводкиСуммаНДСКредит / ТекСтрока.НДС));
			ДобавленнаяСтрока.НДС     = ПроводкиСуммаНДСКредит;
			ДобавленнаяСтрока.СтатьяКнигиПриобретения = Справочники.СтатьиНалоговыхДеклараций.НДС_НКПропорциональноВклВКредит;
			
			ТекСтрока.НДС     = ТекСтрока.НДС 	  - ДобавленнаяСтрока.НДС;
			ТекСтрока.БазаНДС = ТекСтрока.БазаНДС - ДобавленнаяСтрока.БазаНДС;
			ТекСтрока.СтатьяКнигиПриобретения = Справочники.СтатьиНалоговыхДеклараций.НДС_НКПропорциональноНеВклВКредит;
			
		КонецЦикла;
		ТаблицаКопия.Свернуть("СтатьяКнигиПриобретения,СтавкаНДС","БазаНДС,НДС");
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаКопия, ТаблицаДвижений);
		
		//оборудование
		ТаблицаКопия = ТаблицаПоОборудованию.Скопировать();
		ИсходноеКоличествоСтрок = ТаблицаКопия.Количество();
		Для НомСтроки = 1 По ИсходноеКоличествоСтрок Цикл
			ТекСтрока = ТаблицаКопия[НомСтроки-1];
			Если НЕ ТекСтрока.ВидДеятельностиНДС = Перечисления.ВидыДеятельностиНДС.ПропорциональноОблагаемая Тогда
				Продолжить;
			КонецЕсли;
			
			// добавим строку по пропорц. НДС, относящемуся на кредит, текущая строка останется такой, что не относится на кредит
			ДобавленнаяСтрока = ТаблицаКопия.Добавить();
			ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, ТекСтрока);
			
			ПроводкиСуммаНДСКредит = ТекСтрока.ПроводкиСуммаНДСКредит;
			Если Дата >= глЗначениеПеременной("ДатаВступленияВСилуЗУ643") Тогда
				ПроводкиСуммаНДСКредит = ТекСтрока.НДС - ТекСтрока.СуммаНДСУсловнойПродажи;	
			КонецЕсли;
			ДобавленнаяСтрока.БазаНДС = ?(ТекСтрока.НДС = 0, 0, ТекСтрока.БазаНДС * (ПроводкиСуммаНДСКредит / ТекСтрока.НДС));			
			ДобавленнаяСтрока.НДС     = ПроводкиСуммаНДСКредит;
			ДобавленнаяСтрока.СтатьяКнигиПриобретения = Справочники.СтатьиНалоговыхДеклараций.НДС_НКПропорциональноВклВКредитПоставкаОФ;
			
			ТекСтрока.НДС     = ТекСтрока.НДС 	  - ДобавленнаяСтрока.НДС;
			ТекСтрока.БазаНДС = ТекСтрока.БазаНДС - ДобавленнаяСтрока.БазаНДС;
			ТекСтрока.СтатьяКнигиПриобретения = Справочники.СтатьиНалоговыхДеклараций.НДС_НКПропорциональноНеВклВКредит;
			
		КонецЦикла;
		ТаблицаКопия.Свернуть("СтатьяКнигиПриобретения,СтавкаНДС","БазаНДС,НДС");
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаКопия, ТаблицаДвижений);
		
		ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.Организация		, "Организация");
		ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ДоговорПоставщикаТоваров, "ДоговорКонтрагента");
		
		Если ОформленНалоговыйВексельПоНДС Тогда
			ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ДатаВыдачиВекселя, "ДатаВыдачиВекселя");
		КонецЕсли; 
		
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийКнигаПриобретений.ПервичныйДокумент, "КодОперации");
		
		НаборДвижений.мПериод          = Дата;
		НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
		
		Если Не Отказ Тогда
			НаборДвижений.ДобавитьДвижение();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ДвиженияПоРегистрамБухгалтерииРегл()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ОбработкаЗаполнения".
//
Процедура ОбработкаЗаполнения(Основание)
	ПоступлениеТоваров    = (ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг"));
	ПоступлениеТоваровНТТ = (ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваровУслугВНТТ"));

	Если ПоступлениеТоваров
	 ИЛИ ПоступлениеТоваровНТТ Тогда
	
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);
		ПоставщикТоваров   		 = Контрагент;
		ДоговорПоставщикаТоваров = ДоговорКонтрагента;
		// под Контрагентом тут подразумевается таможня
		Контрагент         = Неопределено;
		ДоговорКонтрагента = Неопределено;
		Сделка             = Неопределено;
		// Документ только в регламетировананной валюте
		ВалютаДокумента	   = мВалютаРегламентированногоУчета;
		СтруктураКурсаДокумента = МодульВалютногоУчета.ПолучитьКурсВалюты(ВалютаДокумента, ТекущаяДата());
		КурсДокумента = СтруктураКурсаДокумента.Курс;
		КратностьДокумента = СтруктураКурсаДокумента.Кратность;
		// Заполним Табличную часть
		Раздел = ДобавитьРаздел();
		ЗаполнитьПоПоступлению(Основание, 1);

		РассчитатьСуммыПошлиныГТДИАкциза(Раздел);

	КонецЕсли;
	
	мТекущаяСхемаНалогообложения 	 = НалоговыйУчет.ПолучитьСхемуНалогообложения(Дата, Организация);
	мКоэффициентПропорциональногоНДС = Неопределено;
	РассчитатьПропорциональныйНДС();

КонецПроцедуры // ОбработкаЗаполнения()

// Процедура формирует структуру шапки документа и дополнительных полей.
//
Процедура ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента, СтруктураШапкиДокументаВалюта, СтруктураШапкиДокументаРубли, Отказ=Ложь) Экспорт
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

	// Заполним по шапке документа дерево параметров, нужных при проведении.
	ДеревоПолейЗапросаПоШапке = УправлениеЗапасами.СформироватьДеревоПолейЗапросаПоШапке();
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорКонтрагента",     "ВалютаВзаиморасчетов"          	, "ВалютаВзаиморасчетов");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорКонтрагента",     "ВедениеВзаиморасчетов"         	, "ВедениеВзаиморасчетов");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорКонтрагента", 	"ВидДоговора"						, "ВидДоговораКонтрагента");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорКонтрагента"  , "НеОтноситьНаЗатратыПоНУ" 				 , "НеОтноситьНаЗатратыПоНУ");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорКонтрагента",     "Организация"                   	, "ДоговорОрганизация");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "УчетнаяПолитика"     	,  "НеВключатьНДСВСтоимостьПартий" , "НеВключатьНДСВСтоимостьПартий");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "УчетнаяПолитика"     	,  "СпособОценкиМПЗ"           	   , "СпособОценкиМПЗ");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "НастройкаСпособовВеденияУправленческогоПартионногоУчета", "СпособВеденияПартионногоУчетаПоОрганизации", "СпособВеденияПартионногоУчетаПоОрганизации");

	ДополнитьДеревоПолейЗапросаПоШапкеУпр(ДеревоПолейЗапросаПоШапке);
	ДополнитьДеревоПолейЗапросаПоШапкеРегл(ДеревоПолейЗапросаПоШапке);

	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = УправлениеЗапасами.СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета);
	
	// Получим данные учетной политики
	ПодготовитьПараметрыУчетнойПолитики(СтруктураШапкиДокумента, Отказ, Заголовок);
КонецПроцедуры // ПодготовитьСтруктуруШапкиДокумента()

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Перем Заголовок, СтруктураШапкиДокумента, СтруктураШапкиДокументаВалюта, СтруктураШапкиДокументаРубли;
	Перем ТаблицаПоТоварамВалюта, ТаблицаПоТоварамРубли;
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента, СтруктураШапкиДокументаВалюта, СтруктураШапкиДокументаРубли, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Получим необходимые данные для проверки и проведения по табличной части "Товары".
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("НомерРаздела"                , "НомерРаздела");
	СтруктураПолей.Вставить("Номенклатура"                , "Номенклатура");
	СтруктураПолей.Вставить("Количество"                  , "Количество * Коэффициент /Номенклатура.ЕдиницаХраненияОстатков.Коэффициент");
	СтруктураПолей.Вставить("ФактурнаяСтоимость"          , "ФактурнаяСтоимость");
	СтруктураПолей.Вставить("НДС"                         , "СуммаНДС");
	СтруктураПолей.Вставить("Пошлина"                     , "СуммаПошлины");
	СтруктураПолей.Вставить("Акциз"                   	, "СуммаАкциза");
	СтруктураПолей.Вставить("ХарактеристикаНоменклатуры"  , "ХарактеристикаНоменклатуры");
	СтруктураПолей.Вставить("СерияНоменклатуры"           , "СерияНоменклатуры");
	СтруктураПолей.Вставить("ВестиПартионныйУчетПоСериям" , "Номенклатура.ВестиПартионныйУчетПоСериям");

	СтруктураПолей.Вставить("Услуга"                      , "Номенклатура.Услуга");
	СтруктураПолей.Вставить("Набор"                       , "Номенклатура.Набор");
	СтруктураПолей.Вставить("Комплект"                    , "Номенклатура.Комплект");
	СтруктураПолей.Вставить("Заказ"                		, "ЗаказПокупателя");
	СтруктураПолей.Вставить("ДокументОприходования"       , "ДокументПартии");
	СтруктураПолей.Вставить("ДокументПартииВидОперации"   , "ДокументПартии.ВидОперации");
	СтруктураПолей.Вставить("ДокументПартииВидПоступления", "ДокументПартии.ВидПоступления");
	СтруктураПолей.Вставить("ДокументПартииКонтрагент"  , "ДокументПартии.Контрагент");
	СтруктураПолей.Вставить("ДокументПартииДоговор"  	, "ДокументПартии.ДоговорКонтрагента");
	СтруктураПолей.Вставить("ДокументПартииВидДоговора"	, "ДокументПартии.ДоговорКонтрагента.ВидДоговора");
	
    ДополнитьСтруктуруПолейТабличнойЧастиТоварыУпр(СтруктураПолей);
	ДополнитьСтруктуруПолейТабличнойЧастиТоварыРегл(СтруктураПолей);

	СтруктураСложныхПолей = Новый Структура();
    СтруктураСложныхПолей.Вставить("Склад"              , "ВЫБОР
											              |КОГДА ДокументПартии ССЫЛКА Документ.ПоступлениеТоваровУслугВНТТ ТОГДА ДокументПартии.Склад
											              |КОГДА ДокументПартии.СкладОрдер ССЫЛКА Документ.ПриходныйОрдерНаТовары ТОГДА ДокументПартии.СкладОрдер.Склад
											              |ИНАЧЕ ДокументПартии.СкладОрдер КОНЕЦ"); 

	Если СтруктураШапкиДокумента.ВедениеУчетаПоПроектам 
	   И Не ЗначениеЗаполнено(Проект) Тогда
		СтруктураСложныхПолей.Вставить("Проект", "ПроектыНоменклатуры.Проект");
	КонецЕсли;	
	
	РезультатЗапросаПоТоварам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Товары", СтруктураПолей,,СтруктураСложныхПолей);
	
	ТаблицаПоТоварам = РезультатЗапросаПоТоварам.Выгрузить();
	// Получим необходимые данные для проверки и проведения по табличной части "Оборудование".
	СтруктураПолей = Новый Структура;
	СтруктураПростыхПолей = Новый Структура;
	СтруктураПолей.Вставить("НомерРаздела"              , "НомерРаздела");
	СтруктураПолей.Вставить("Номенклатура"              , "Номенклатура");
	СтруктураПолей.Вставить("Количество"                , "Количество");
	СтруктураПолей.Вставить("ФактурнаяСтоимость"        , "ФактурнаяСтоимость");
	СтруктураПолей.Вставить("НДС"                       , "СуммаНДС");
	СтруктураПолей.Вставить("Пошлина"                   , "СуммаПошлины");
	СтруктураПолей.Вставить("Акциз"                   	, "СуммаАкциза");
	СтруктураПолей.Вставить("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
	СтруктураПолей.Вставить("СерияНоменклатуры"         , "СерияНоменклатуры");
	СтруктураПолей.Вставить("ВестиПартионныйУчетПоСериям" , "Номенклатура.ВестиПартионныйУчетПоСериям");
	
	СтруктураПолей.Вставить("Услуга"               		, "Номенклатура.Услуга");
	СтруктураПолей.Вставить("Набор"                		, "Номенклатура.Набор");
	СтруктураПолей.Вставить("ДокументОприходования"		, "ДокументПартии");
	СтруктураПолей.Вставить("ДокументПартииВидОперации" , "ДокументПартии.ВидОперации");
	СтруктураПолей.Вставить("ДокументПартииВидПоступления" , "ДокументПартии.ВидПоступления");
	СтруктураПолей.Вставить("ДокументПартииКонтрагент"  , "ДокументПартии.Контрагент");
	СтруктураПолей.Вставить("ДокументПартииДоговор"  	, "ДокументПартии.ДоговорКонтрагента");
	СтруктураПолей.Вставить("ДокументПартииВидДоговора"	, "ДокументПартии.ДоговорКонтрагента.ВидДоговора");
	
	ДополнитьСтруктуруПолейТабличнойЧастиОборудованиеРегл(СтруктураПолей, СтруктураПростыхПолей);
	СтруктураСложныхПолей.Вставить("Склад"              , "ВЫБОР
											              |КОГДА ДокументПартии.СкладОрдер ССЫЛКА Документ.ПриходныйОрдерНаТовары ТОГДА ДокументПартии.СкладОрдер.Склад
											              |ИНАЧЕ ДокументПартии.СкладОрдер КОНЕЦ"); 

	Если СтруктураШапкиДокумента.ВедениеУчетаПоПроектам 
	   И Не ЗначениеЗаполнено(Проект) Тогда
		СтруктураСложныхПолей.Вставить("Проект", "ПроектыНоменклатуры.Проект");
	КонецЕсли;	
	
	РезультатЗапросаПоОборудованию = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Оборудование", СтруктураПолей, СтруктураПростыхПолей,СтруктураСложныхПолей);
	ТаблицаПоОборудованию = РезультатЗапросаПоОборудованию.Выгрузить();
	
	ОбработатьТабличнуюЧастьРазделы(СтруктураШапкиДокумента);

	РаспределитьСуммыИзШапки(ТаблицаПоТоварам,ТаблицаПоОборудованию,СтруктураШапкиДокумента);

	ПогрешностиОкругления     = Новый Соответствие;

	ТаблицаПоТоварам = ПодготовитьТаблицуТоваров(ТаблицаПоТоварам, СтруктураШапкиДокумента);
	ПодготовитьТаблицуОборудование(ТаблицаПоОборудованию, СтруктураШапкиДокумента);
	
	Если СтруктураШапкиДокумента.ВестиПартионныйУчетПоСкладам Тогда
		// потребуется распределение по складам
		РаспределитьДопРасходыПоСкладам(ТаблицаПоТоварам, ТаблицаПоОборудованию, СтруктураШапкиДокумента, Отказ, Заголовок);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ЗаполнитьСчетУчетаНДСВТаблицеДокумента(СтруктураШапкиДокумента, ТаблицаПоТоварам);
	ОбщегоНазначенияКлиентСервер.ЗаполнитьСчетУчетаНДСВТаблицеДокумента(СтруктураШапкиДокумента, ТаблицаПоОборудованию);
	
	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);
	ПроверитьЗаполнениеТабличнойЧастиРазделы(Разделы, СтруктураШапкиДокумента, Отказ, Заголовок);
	
	//Заполнение и проверка заполнения счетов учета номенклатуры и затрат
	СчетаУчетаВДокументах.ПроверитьСчетаУчетаТабличнойЧасти("Товары", ТаблицаПоТоварам, 	СтруктураШапкиДокумента, Отказ, Заголовок);
	СчетаУчетаВДокументах.ПроверитьСчетаУчетаТабличнойЧасти("Оборудование", ТаблицаПоОборудованию, 	СтруктураШапкиДокумента, Отказ, Заголовок);
	
	ПроверитьЗаполнениеТабличнойЧастиТовары(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок);
	ПроверитьЗаполнениеТабличнойЧастиОборудование(ТаблицаПоОборудованию, СтруктураШапкиДокумента, Отказ, Заголовок);	
	
	Если НЕ Отказ Тогда
		ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоОборудованию, Отказ, Заголовок);
	КонецЕсли;
	
	//Сделаем переменные доступными из подписок на события
	ДополнительныеСвойства.Вставить("СтруктураШапкиДокумента", СтруктураШапкиДокумента);
	ДополнительныеСвойства.Вставить("СтруктураТабличныхЧастей", Новый Структура("ТаблицаПоТоварам, ТаблицаПоОборудованию", ТаблицаПоТоварам, ТаблицаПоОборудованию));

КонецПроцедуры

// Процедура вызывается перед записью документа 
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтражатьВБухгалтерскомУчете Тогда
		НалоговыйУчет.ЗаполнитьНалоговыеНазначенияВТабличныхЧастяхПередЗаписьюДокумента(
			"Поступление",
			Дата,
			Организация,
			Товары, 		// ТабличнаяЧастьТовары
			Неопределено,	// ТабличнаяЧастьВозвратнаяТара
			Неопределено,   // ТабличнаяЧастьУслуги
			Оборудование,   // ТабличнаяЧастьОборудование
			Неопределено, 	// ТабличнаяЧастьОбъектыСтроительства
			Неопределено 	// ТабличнаяЧастьБланкиСтрогогоУчета
		);
	КонецЕсли;	
	
	Если СчетНДСУсловнаяПродажа.Пустая() Тогда
		СчетНДСУсловнаяПродажа = ПланыСчетов.Хозрасчетный.УсловнаяПродажа;
	КонецЕсли;

	// Проверка заполнения единицы измерения мест и количества мест
	ОбработкаТабличныхЧастей.ПриЗаписиПроверитьЕдиницуИзмеренияМест(Товары);
	
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;



КонецПроцедуры // ПриЗаписи()

мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
