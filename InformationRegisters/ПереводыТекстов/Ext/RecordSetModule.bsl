
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	Для Каждого ЗаписьНабора Из ЭтотОбъект Цикл
		
		НовыйХешRU = ЛокализацияПовтИсп.ПолучитьХешСтроки(ЗаписьНабора.ТекстRU);
		НовыйХешUK = ЛокализацияПовтИсп.ПолучитьХешСтроки(ЗаписьНабора.ТекстUK);
		Если НЕ ЗначениеЗаполнено(ЗаписьНабора.УИД) Тогда
			НовыйУИД = Новый УникальныйИдентификатор();
		Иначе
			НовыйУИД = ЗаписьНабора.УИД;
		КонецЕсли; 
		
		Если НовыйХешRU <> ЗаписьНабора.ХешRU ИЛИ НовыйХешUK <> ЗаписьНабора.ХешUK ИЛИ НовыйУИД <> ЗаписьНабора.УИД Тогда
			
			ЗаписьНабора.ХешRU = НовыйХешRU;
			ЗаписьНабора.ХешUK = НовыйХешUK;
			ЗаписьНабора.УИД   = НовыйУИД;
			
			Если ЭтотОбъект.Отбор.ХешRU.Использование Тогда
				ЭтотОбъект.Отбор.ХешRU.Установить(НовыйХешRU);
			КонецЕсли; 
			
			Если ЭтотОбъект.Отбор.ХешUK.Использование Тогда
				ЭтотОбъект.Отбор.ХешUK.Установить(НовыйХешUK);
			КонецЕсли; 
			
			Если ЭтотОбъект.Отбор.УИД.Использование Тогда
				ЭтотОбъект.Отбор.УИД.Установить(НовыйУИД);
			КонецЕсли; 
			
		КонецЕсли;	
		
	КонецЦикла;
		
КонецПроцедуры // ПередЗаписью


Процедура ПриЗаписи(Отказ, Замещение)
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры


