
// Процедура - обработчик события "ПередЗаписью".
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		
		Если ВидЗатрат.Пустая() Тогда
			Сообщить("Укажите вид затрат!", СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;
		
		Если ВидЗатрат = Перечисления.ВидыЗатрат.Материальные 
		   И ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПроизводственныеРасходы
		   И СтатусМатериальныхЗатрат.Пустая() Тогда
			Сообщить("Укажите статус материальных затрат!", СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;
		
		Если ХарактерЗатрат.Пустая() Тогда
			Сообщить("Укажите характер затрат!", СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;
		
		
		Если ЗначениеЗаполнено(Счет8Класса) Тогда
			Если БухгалтерскийУчетПовтИсп.ПолучитьИспользоватьКлассыСчетовВКачествеГрупп() Тогда
				мСчетЗатратыПоЭлементам 		= ПланыСчетов.Хозрасчетный.ЗатратыПоЭлементам;
			Иначе
				мСчетЗатратыПоЭлементам	= Новый Массив;
				мСчетЗатратыПоЭлементам.Добавить(ПланыСчетов.Хозрасчетный.МатериальныеЗатраты);
				мСчетЗатратыПоЭлементам.Добавить(ПланыСчетов.Хозрасчетный.ЗатратыНаОплатуТруда);
				мСчетЗатратыПоЭлементам.Добавить(ПланыСчетов.Хозрасчетный.ОтчисленияНаСоциальныеМероприятия);
				мСчетЗатратыПоЭлементам.Добавить(ПланыСчетов.Хозрасчетный.Амортизация);
				мСчетЗатратыПоЭлементам.Добавить(ПланыСчетов.Хозрасчетный.ДругиеОперационныеЗатраты);
				мСчетЗатратыПоЭлементам.Добавить(ПланыСчетов.Хозрасчетный.ДругиеЗатратыПоЭлементам);
			КонецЕсли;
			
			Если НЕ БухгалтерскийУчетПовтИсп.СчетПринадлежитРодителю(Счет8Класса, мСчетЗатратыПоЭлементам) Тогда
				Сообщить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Счет %1 ""%2"" не является счетом 8-ого класса!';uk='Рахунок %1 ""%2"" не є рахунком 8-ого классу!'"), СокрЛП(Счет8Класса), Счет8Класса.Наименование), СтатусСообщения.Важное);
				Отказ = Истина;
			КонецЕсли;
		Иначе
			Сообщить("Укажите счет 8 класса!", СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;
		
		Если Не ЭтоНовый() Тогда

			Если (ВидЗатрат <> Ссылка.ВидЗатрат
					ИЛИ СтатусМатериальныхЗатрат <> Ссылка.СтатусМатериальныхЗатрат
					ИЛИ ХарактерЗатрат <> Ссылка.ХарактерЗатрат)
			   И ПолныеПрава.СтатьяЗатрат_СуществуютСсылкиВРегистрахНакопления(Ссылка)
			Тогда
				ТекстСообщения = "
				|Статья затрат """ + СокрЛП(Ссылка) + """ указана в проведенных документах.
				|Реквизиты ""Характер затрат"", ""Вид затрат"", ""Статус мат. затрат"" не могут быть изменены!";
				ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения, Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

