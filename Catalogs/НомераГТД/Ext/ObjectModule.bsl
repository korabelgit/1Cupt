Перем ИзменилсяКод;

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	// необходимо перезаписать (перезаполнить наименования и реквизиты) в справочнике НоменклатураГТД 
	// если изменился код справочника
	Если ИзменилсяКод = Истина  Тогда
	
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.Текст = "ВЫБРАТЬ
		               |	НоменклатураГТД.Ссылка
		               |ИЗ
		               |	Справочник.НоменклатураГТД КАК НоменклатураГТД
		               |ГДЕ
		               |	НоменклатураГТД.НомерГТД = &Ссылка";
					   
		СообщениеОбОшибкеВыдавалось = Ложь;
		
		Выборка = Запрос.Выполнить().Выбрать();				   
		Пока Выборка.Следующий() Цикл
		
			Спр = Выборка.Ссылка.ПолучитьОбъект();
			Спр.ОбновлениеИндексныхПолей();
			Спр.ОбменДанными.Загрузка = Истина;
			Попытка
			    Спр.Записать();
			Исключение
				
				Если СообщениеОбОшибкеВыдавалось = Ложь Тогда
					Сообщить("Не удалось обновить данные справочника Номенклатура ГТД. Отображение Номера ГТД в списке и в наименовании этого справочника может быть не правильным!", СтатусСообщения.Внимание);
					СообщениеОбОшибкеВыдавалось = Истина;
				КонецЕсли;
				
			КонецПопытки;
		
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ИзменилсяКод = Ссылка.Код <> Код;
	
КонецПроцедуры // ПередЗаписью
