
Процедура ОбновлениеИндексныхПолей() Экспорт

	// заполним ссылочные реквизиты, которые позволят сортировать список справочника.
	КодУКТВЭД_Индекс = КодУКТВЭД.Код;
	НомерГТД_Индекс  = НомерГТД.Код;
	ДатаГТД_Индекс   = НомерГТД.Дата;
	
	// создадим наименование, которое будет использоваться для отображения элемента
	Наименование = КодУКТВЭД.Наименование + ?(ЗначениеЗаполнено(НомерГТД_Индекс), ", ВМД №: " + НомерГТД_Индекс + " від " + Формат(ДатаГТД_Индекс, "ДФ=dd.MM.yy"), "");
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИндексныхПолей();
	
	// не будем записывать дубли
	Если НЕ ОбменДанными.Загрузка Тогда
	
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("КодУКТВЭД", КодУКТВЭД);
		Запрос.УстановитьПараметр("НомерГТД",  НомерГТД);
		Запрос.УстановитьПараметр("Владелец",  Владелец);
		Запрос.УстановитьПараметр("Ссылка",    Ссылка);
		
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		               |	НоменклатураГТД.Ссылка
		               |ИЗ
		               |	Справочник.НоменклатураГТД КАК НоменклатураГТД
		               |ГДЕ
		               |	НЕ НоменклатураГТД.Ссылка = &Ссылка
		               |	И НоменклатураГТД.КодУКТВЭД = &КодУКТВЭД
		               |	И НоменклатураГТД.НомерГТД = &НомерГТД
		               |	И НоменклатураГТД.Владелец = &Владелец";
					   
		Если НЕ Запрос.Выполнить().Пустой() Тогда
			Отказ = Истина;
			Сообщить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Для номенклатуры %1 уже имеется запись в справочнике ""Коды номенклатуры для НН"" с такими же Кодом и номером ГТД!", Владелец));
		КонецЕсли;					   	
	
	КонецЕсли;
	
КонецПроцедуры
