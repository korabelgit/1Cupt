Перем ПрошлыйИзмененныйГоловнаяОрганизация;

Процедура ЗаполнитьРеквизитыОбъекта(Отказ, ТекстСообщенияОшибки)
	
	ТекстСообщенияОшибки = "";
	
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо И НЕ ЗначениеЗаполнено(ИндивидуальныйПредприниматель) Тогда
		ТекстСообщенияОшибки = НСтр("ru='Организации - индивидуальному предпринимателю не сопоставлено физ. лицо!';uk='Організації - індивідуальному підприємцеві не зіставлена фіз. особа!'");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоНовый() И ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо И ЮрФизЛицо <> Ссылка.ЮрФизЛицо Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
		                      |	КОЛИЧЕСТВО(ИСТИНА) КАК КоличествоПодчиненных
		                      |ИЗ
		                      |	Справочник.Организации КАК Организации
		                      |ГДЕ
		                      |	Организации.ГоловнаяОрганизация = &ГоловнаяОрганизация");
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если ЗначениеЗаполнено(Выборка.КоличествоПодчиненных) Тогда
				ТекстСообщенияОшибки = "Текущая организация является головной для других организаций из справочника.
						 |Физическое лицо не может являться головной организацией!";
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьРеквизитыОбъекта


// Обработчик события "ПередЗаписью" Объекта
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщенияОшибки = "";
	
	ЗаполнитьРеквизитыОбъекта(Отказ, ТекстСообщенияОшибки);
	
	Если Отказ Тогда
		ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщенияОшибки, Отказ);
	ИначеЕсли ЗначениеЗаполнено(ТекстСообщенияОшибки) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщенияОшибки, , , СтатусСообщения.Внимание);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		ПрошлыйИзмененныйГоловнаяОрганизация = ?(Не ЭтоНовый() и Не Ссылка.ГоловнаяОрганизация = ГоловнаяОрганизация, Ссылка.ГоловнаяОрганизация, Неопределено);
		НастройкаПравДоступа.ПередЗаписьюНовогоОбъектаСПравамиДоступаПользователей(ЭтотОбъект, Отказ, ГоловнаяОрганизация);
	КонецЕсли;
	
	// очистка неиспользуемых реквизитов
	Если НЕ Отказ Тогда
		
		Если НЕ ЗначениеЗаполнено(ЮрФизЛицо) Тогда
			ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
		КонецЕсли;
		
		Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
			ГоловнаяОрганизация 			= Неопределено;
		ИначеЕсли ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
			ИндивидуальныйПредприниматель 	= Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;	
	КонецЕсли;
	
	НастройкаПравДоступа.ОбновитьПраваДоступаКИерархическимОбъектамПриНеобходимости(Ссылка,ПрошлыйИзмененныйГоловнаяОрганизация, Отказ);
	
КонецПроцедуры // ПриЗаписи

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ЮрФизЛицо							= Перечисления.ЮрФизЛицо.ЮрЛицо;
	ОтражатьВРегламентированномУчете	= Истина;
КонецПроцедуры // ОбработкаЗаполнения

