#Если Клиент Тогда

/////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом,
//	ЕстьОшибки - флаг того, что при формировании произошли ошибки
//
Процедура СформироватьОтчет(ДокументРезультат, ЕстьОшибки) Экспорт

    Если ТипЗнч(ПараметрОтбора) = Тип("СправочникСсылка.Организации") Тогда 
		Организация = ПараметрОтбора;
	Иначе
		ЕстьОшибки = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	  
    СписокОфицерскогоСостава	=	Новый СписокЗначений;
	СписокОфицерскогоСостава.Добавить(Перечисления.СоставыВоеннослужащих.МладшиеОфицеры);
	СписокОфицерскогоСостава.Добавить(Перечисления.СоставыВоеннослужащих.СтаршиеОфицеры);
	СписокОфицерскогоСостава.Добавить(Перечисления.СоставыВоеннослужащих.ВысшиеОфицеры);
	
	СписокСоставаСержантовИПрапорщиков	=	Новый СписокЗначений;
	СписокСоставаСержантовИПрапорщиков.Добавить(Перечисления.СоставыВоеннослужащих.СержантскийИСтаршинский);
	СписокСоставаСержантовИПрапорщиков.Добавить(Перечисления.СоставыВоеннослужащих.ПрапорщиковИМичманов);
	
	СписокСолдатскогоСостава			=	Новый СписокЗначений;
	СписокСолдатскогоСостава.Добавить(Перечисления.СоставыВоеннослужащих.Рядовой);	
	
	Запрос.УстановитьПараметр("ДатаАктуальности",	ДатаАктуальности);
	Запрос.УстановитьПараметр("Организация",		Организация);
	Запрос.УстановитьПараметр("Офицеры",			СписокОфицерскогоСостава);
	Запрос.УстановитьПараметр("Прапорщики",			СписокСоставаСержантовИПрапорщиков);
	Запрос.УстановитьПараметр("Солдаты",			СписокСолдатскогоСостава);
	Запрос.УстановитьПараметр("Военнообязанный",	Перечисления.ОтношениеКВоинскойОбязанности.Военнообязанный);
	Запрос.УстановитьПараметр("ОграниченноГоден",	Перечисления.ГодностьКВоеннойСлужбе.ГоденКНестроевой);
	Запрос.УстановитьПараметр("До30лет",			ДобавитьМесяц(ДатаАктуальности, -12*30));
	Запрос.УстановитьПараметр("До35лет",			ДобавитьМесяц(ДатаАктуальности, -12*35));
	Запрос.УстановитьПараметр("До40лет",			ДобавитьМесяц(ДатаАктуальности, -12*40));
	Запрос.УстановитьПараметр("До45лет",			ДобавитьМесяц(ДатаАктуальности, -12*45));
	Запрос.УстановитьПараметр("До50лет",			ДобавитьМесяц(ДатаАктуальности, -12*50));
	Запрос.УстановитьПараметр("Призывник",			Перечисления.ОтношениеКВоинскойОбязанности.Призывник);
	Запрос.УстановитьПараметр("НеРаботает",			Перечисления.СостоянияРаботникаОрганизации.НеРаботает);
	Запрос.УстановитьПараметр("Руководители",		Справочники.КатегорииДолжностей.Руководители);
	Запрос.УстановитьПараметр("Специалисты",		Справочники.КатегорииДолжностей.Специалисты);
	
	списокДругиеСлужащие = Новый СписокЗначений;
	списокДругиеСлужащие.Добавить(Справочники.КатегорииДолжностей.ДругиеСлужащие);
	списокДругиеСлужащие.Добавить(Справочники.КатегорииДолжностей.Профессионалы);
	списокДругиеСлужащие.Добавить(Справочники.КатегорииДолжностей.КвалифицированныеРаботникиСИнструментом);
	списокДругиеСлужащие.Добавить(Справочники.КатегорииДолжностей.КвалифицированныеРаботникиСХ);
	списокДругиеСлужащие.Добавить(Справочники.КатегорииДолжностей.РаботникиСферыТорговлиИУслуг);
	списокДругиеСлужащие.Добавить(Справочники.КатегорииДолжностей.ТехническиеСлужащие);
	
	Запрос.УстановитьПараметр("ДругиеСлужащие",		списокДругиеСлужащие);
	Запрос.УстановитьПараметр("Рабочие",			Справочники.КатегорииДолжностей.Рабочие);
	Запрос.УстановитьПараметр("ВидЗанятостиОсновноеМестоРаботы", Перечисления.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы);

	ТекстОсновногоЗапроса ="ВЫБРАТЬ
	              |	РаботникиОрганизации.Должность.Категория КАК ДолжностьКатегория,
	              |	1 КАК ВсегоРаботающих,
	              |	ВЫБОР КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ВсегоВЗапасе,
	              |	ВЫБОР КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.Звание.Состав В (&Офицеры) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ОфицеровВЗапасе,
	              |	ВЫБОР КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.Звание.Состав В (&Прапорщики) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПрапорщиковВЗапасе,
	              |	ВЫБОР КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.Звание.Состав В (&Солдаты) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК СолдатВЗапасе,
	              |	ВЫБОР КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.Годность = &ОграниченноГоден ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПрапорщиковСолдатОграниченноГодныхВЗапасе,
	              |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ВсегоВЗапасеЗабронировано,
	              |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.Звание.Состав В (&Офицеры) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ОфицеровЗабронировано,
	              |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И (ВУ.Звание.Состав В (&Прапорщики) ИЛИ ВУ.Звание.Состав В (&Солдаты)) И РаботникиОрганизации.ФизЛицо.ДатаРождения > &До30лет ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПрапорщиковСолдатДо30Забронировано,
	              |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И (ВУ.Звание.Состав В (&Прапорщики) ИЛИ ВУ.Звание.Состав В (&Солдаты)) И РаботникиОрганизации.ФизЛицо.ДатаРождения > &До35лет И РаботникиОрганизации.ФизЛицо.ДатаРождения <= &До30лет ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПрапорщиковСолдатОт31До35Забронировано,
	              |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И (ВУ.Звание.Состав В (&Прапорщики) ИЛИ ВУ.Звание.Состав В (&Солдаты)) И РаботникиОрганизации.ФизЛицо.ДатаРождения > &До40лет И РаботникиОрганизации.ФизЛицо.ДатаРождения <= &До35лет ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПрапорщиковСолдатОт36До40Забронировано,
	              |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И (ВУ.Звание.Состав В (&Прапорщики) ИЛИ ВУ.Звание.Состав В (&Солдаты)) И РаботникиОрганизации.ФизЛицо.ДатаРождения > &До45лет И РаботникиОрганизации.ФизЛицо.ДатаРождения <= &До40лет ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПрапорщиковСолдатОт41До45Забронировано,
	              |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И (ВУ.Звание.Состав В (&Прапорщики) ИЛИ ВУ.Звание.Состав В (&Солдаты)) И РаботникиОрганизации.ФизЛицо.ДатаРождения > &До50лет И РаботникиОрганизации.ФизЛицо.ДатаРождения <= &До45лет ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПрапорщиковСолдатОт46До50Забронировано,
	              |	ВЫБОР КОГДА НЕ(ВУ.ЗабронированОрганизацией = &Организация) И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И НЕ(ВУ.НаличиеМобпредписания) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК НезабронированноБезМобпредписаний,
	              |	ВЫБОР КОГДА НЕ(ВУ.ЗабронированОрганизацией = &Организация) И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.НаличиеМобпредписания ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК НезабронированноСМобпредписанием,
	              |	ВЫБОР КОГДА НЕ(ВУ.ЗабронированОрганизацией = &Организация) И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.НаличиеМобпредписания И ВУ.Звание.Состав В (&Офицеры) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК НезабронированноОфицеровСМобпредписанием,
	              |	ВЫБОР КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Призывник ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК Призывников,
				  |	ВЫБОР КОГДА РаботникиОрганизации.Должность.Категория = &Руководители ТОГДА 1 КОГДА РаботникиОрганизации.Должность.Категория = &Специалисты ТОГДА 2 КОГДА РаботникиОрганизации.Должность.Категория В (&ДругиеСлужащие) ТОГДА 11 КОГДА РаботникиОрганизации.Должность.Категория = &Рабочие ТОГДА 12 ИНАЧЕ РаботникиОрганизации.Должность.Категория.Код КОНЕЦ КАК НомерСтрокиПП
	              |ИЗ
	              |	(ВЫБРАТЬ
	              |		РаботникиОрганизацииСрезПоследних.Должность КАК Должность,
	              |		РаботникиОрганизацииСрезПоследних.Сотрудник.Физлицо КАК ФизЛицо
				  |	ИЗ
	              |		РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаАктуальности,Сотрудник.ВидЗанятости = &ВидЗанятостиОсновноеМестоРаботы) КАК РаботникиОрганизацииСрезПоследних
	              |	
	              |	ГДЕ
	              |		РаботникиОрганизацииСрезПоследних.Организация = &Организация И
	              |		(РаботникиОрганизацииСрезПоследних.ЗанимаемыхСтавок > 0)) КАК РаботникиОрганизации
	              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВоинскийУчет.СрезПоследних(&ДатаАктуальности, ) КАК ВУ
	              |		ПО РаботникиОрганизации.ФизЛицо = ВУ.Физлицо
				  |УПОРЯДОЧИТЬ ПО
	              |	НомерСтрокиПП
				  |
	              |ИТОГИ СУММА(ВсегоРаботающих), СУММА(ВсегоВЗапасе), СУММА(ОфицеровВЗапасе), СУММА(ПрапорщиковВЗапасе), СУММА(СолдатВЗапасе), СУММА(ПрапорщиковСолдатОграниченноГодныхВЗапасе), СУММА(ВсегоВЗапасеЗабронировано), СУММА(ОфицеровЗабронировано), СУММА(ПрапорщиковСолдатДо30Забронировано), СУММА(ПрапорщиковСолдатОт31До35Забронировано), СУММА(ПрапорщиковСолдатОт36До40Забронировано), СУММА(ПрапорщиковСолдатОт41До45Забронировано), СУММА(ПрапорщиковСолдатОт46До50Забронировано), СУММА(НезабронированноБезМобпредписаний), СУММА(НезабронированноСМобпредписанием), СУММА(НезабронированноОфицеровСМобпредписанием), СУММА(Призывников) ПО
	              |	ОБЩИЕ,
	              |	ДолжностьКатегория";
				  
	ДокументРезультат.Очистить();
	
	Макет 			= ПолучитьМакет("Форма6");
	ОбластьШапка 	= Макет.ПолучитьОбласть("ШапкаФормы6");
	ОбластьШапка.Параметры.ДатаАктуальности		=	Формат(ДатаАктуальности, "ДЛФ=D");
	ОбластьШапка.Параметры.НазваниеОрганизации 	= 	Организация.Наименование;
	ДокументРезультат.Присоединить(ОбластьШапка);

		Запрос.Текст = ТекстОсновногоЗапроса;
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаПоКатегориям	=	РезультатЗапроса.Выбрать();
		Пока ВыборкаПоКатегориям.Следующий() Цикл 
			Если	ВыборкаПоКатегориям.Уровень()	=	0	тогда
				ОбластьПодвал 	= Макет.ПолучитьОбласть("ПодвалФормы6");
				ОбластьПодвал.Параметры.ВсегоРаботающих								= ВыборкаПоКатегориям.ВсегоРаботающих;
				ОбластьПодвал.Параметры.ВсегоВЗапасе 								= ВыборкаПоКатегориям.ВсегоВЗапасе;
				ОбластьПодвал.Параметры.ОфицеровВЗапасе 							= ВыборкаПоКатегориям.ОфицеровВЗапасе;
				ОбластьПодвал.Параметры.ПрапорщиковВЗапасе 							= ВыборкаПоКатегориям.ПрапорщиковВЗапасе;
				ОбластьПодвал.Параметры.СолдатВЗапасе 								= ВыборкаПоКатегориям.СолдатВЗапасе;
				ОбластьПодвал.Параметры.ПрапорщиковСолдатОграниченноГодныхВЗапасе 	= ВыборкаПоКатегориям.ПрапорщиковСолдатОграниченноГодныхВЗапасе;
				ОбластьПодвал.Параметры.ВсегоВЗапасеЗабронировано 					= ВыборкаПоКатегориям.ВсегоВЗапасеЗабронировано;
				ОбластьПодвал.Параметры.ОфицеровЗабронировано 						= ВыборкаПоКатегориям.ОфицеровЗабронировано;
				ОбластьПодвал.Параметры.ПрапорщиковСолдатДо30Забронировано 			= ВыборкаПоКатегориям.ПрапорщиковСолдатДо30Забронировано;
				ОбластьПодвал.Параметры.ПрапорщиковСолдатОт31До35Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт31До35Забронировано;
				ОбластьПодвал.Параметры.ПрапорщиковСолдатОт36До40Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт36До40Забронировано;
				ОбластьПодвал.Параметры.ПрапорщиковСолдатОт41До45Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт41До45Забронировано;
				ОбластьПодвал.Параметры.ПрапорщиковСолдатОт46До50Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт46До50Забронировано;
				ОбластьПодвал.Параметры.НезабронированноБезМобпредписаний 			= ВыборкаПоКатегориям.НезабронированноБезМобпредписаний;
				ОбластьПодвал.Параметры.Призывников									= ВыборкаПоКатегориям.Призывников;

				ОтветственныеЛицаОрганизации	=	РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация,ДатаАктуальности);
				ОбластьПодвал.Параметры.ДолжностьРуководителя	=	ОтветственныеЛицаОрганизации.РуководительДолжность;
				ОбластьПодвал.Параметры.ФИОРуководителя			=	ОтветственныеЛицаОрганизации.Руководитель;
				Продолжить;
			КонецЕсли; 
			Пока	ВыборкаПоКатегориям.СледующийПоЗначениюПоля("ДолжностьКатегория")	Цикл
				ОбластьСтрока 	= Макет.ПолучитьОбласть("СтрокаФормы6");
				ОбластьСтрока.Параметры.ВсегоРаботающих								= ВыборкаПоКатегориям.ВсегоРаботающих;
				ОбластьСтрока.Параметры.ДолжностьКатегория 							= ВыборкаПоКатегориям.ДолжностьКатегория;
				ОбластьСтрока.Параметры.НомерСтрокиПП	 							= ВыборкаПоКатегориям.НомерСтрокиПП;
				ОбластьСтрока.Параметры.ВсегоРаботающих								= ВыборкаПоКатегориям.ВсегоРаботающих;
				ОбластьСтрока.Параметры.ВсегоВЗапасе 								= ВыборкаПоКатегориям.ВсегоВЗапасе;
				ОбластьСтрока.Параметры.ОфицеровВЗапасе 							= ВыборкаПоКатегориям.ОфицеровВЗапасе;
				ОбластьСтрока.Параметры.ПрапорщиковВЗапасе 							= ВыборкаПоКатегориям.ПрапорщиковВЗапасе;
				ОбластьСтрока.Параметры.СолдатВЗапасе 								= ВыборкаПоКатегориям.СолдатВЗапасе;
				ОбластьСтрока.Параметры.ПрапорщиковСолдатОграниченноГодныхВЗапасе 	= ВыборкаПоКатегориям.ПрапорщиковСолдатОграниченноГодныхВЗапасе;
				ОбластьСтрока.Параметры.ВсегоВЗапасеЗабронировано 					= ВыборкаПоКатегориям.ВсегоВЗапасеЗабронировано;
				ОбластьСтрока.Параметры.ОфицеровЗабронировано 						= ВыборкаПоКатегориям.ОфицеровЗабронировано;
				ОбластьСтрока.Параметры.ПрапорщиковСолдатДо30Забронировано 			= ВыборкаПоКатегориям.ПрапорщиковСолдатДо30Забронировано;
				ОбластьСтрока.Параметры.ПрапорщиковСолдатОт31До35Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт31До35Забронировано;
				ОбластьСтрока.Параметры.ПрапорщиковСолдатОт36До40Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт36До40Забронировано;
				ОбластьСтрока.Параметры.ПрапорщиковСолдатОт41До45Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт41До45Забронировано;
				ОбластьСтрока.Параметры.ПрапорщиковСолдатОт46До50Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт46До50Забронировано;
				ОбластьСтрока.Параметры.НезабронированноБезМобпредписаний 			= ВыборкаПоКатегориям.НезабронированноБезМобпредписаний;
				ОбластьСтрока.Параметры.Призывников									= ВыборкаПоКатегориям.Призывников;
				ДокументРезультат.Присоединить(ОбластьСтрока);
			КонецЦикла;
		КонецЦикла;
		
		
	Если ТипЗнч(ОбластьПодвал)	=	Тип("ТабличныйДокумент")	тогда
		ДокументРезультат.Присоединить(ОбластьПодвал);
	КонецЕсли;
	
	//Параметры документа
	ДокументРезультат.Автомасштаб 			= Истина;
	ДокументРезультат.ОриентацияСтраницы 	= ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.Защита              	= Ложь;
	
КонецПроцедуры

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом,
//	ЕстьОшибки - флаг того, что при формировании произошли ошибки
//
Процедура СформироватьОтчет2015(ДокументРезультат, ЕстьОшибки) Экспорт

    Если ТипЗнч(ПараметрОтбора) = Тип("СправочникСсылка.Организации") Тогда 
		Организация = ПараметрОтбора;
	Иначе
		ЕстьОшибки = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
    СписокОфицерскогоСостава	=	Новый СписокЗначений;
	СписокОфицерскогоСостава.Добавить(Перечисления.СоставыВоеннослужащих.МладшиеОфицеры);
	СписокОфицерскогоСостава.Добавить(Перечисления.СоставыВоеннослужащих.СтаршиеОфицеры);
	СписокОфицерскогоСостава.Добавить(Перечисления.СоставыВоеннослужащих.ВысшиеОфицеры);
	
	СписокСоставаСержантовИПрапорщиков	=	Новый СписокЗначений;
	СписокСоставаСержантовИПрапорщиков.Добавить(Перечисления.СоставыВоеннослужащих.СержантскийИСтаршинский);
	СписокСоставаСержантовИПрапорщиков.Добавить(Перечисления.СоставыВоеннослужащих.ПрапорщиковИМичманов);
	
	СписокСолдатскогоСостава			=	Новый СписокЗначений;
	СписокСолдатскогоСостава.Добавить(Перечисления.СоставыВоеннослужащих.Рядовой);	
	
	Запрос.УстановитьПараметр("ДатаАктуальности",	ДатаАктуальности);
	Запрос.УстановитьПараметр("Организация",		Организация);
	Запрос.УстановитьПараметр("Офицеры",			СписокОфицерскогоСостава);
	Запрос.УстановитьПараметр("Прапорщики",			СписокСоставаСержантовИПрапорщиков);
	Запрос.УстановитьПараметр("Солдаты",			СписокСолдатскогоСостава);
	Запрос.УстановитьПараметр("Военнообязанный",	Перечисления.ОтношениеКВоинскойОбязанности.Военнообязанный);
	Запрос.УстановитьПараметр("До35лет",			ДобавитьМесяц(ДатаАктуальности, -12*35));
	Запрос.УстановитьПараметр("До40лет",			ДобавитьМесяц(ДатаАктуальности, -12*40));
	Запрос.УстановитьПараметр("До45лет",			ДобавитьМесяц(ДатаАктуальности, -12*45));
	Запрос.УстановитьПараметр("До60лет",			ДобавитьМесяц(ДатаАктуальности, -12*60));
	Запрос.УстановитьПараметр("НеРаботает",			Перечисления.СостоянияРаботникаОрганизации.НеРаботает);
	Запрос.УстановитьПараметр("Руководители",		Справочники.КатегорииДолжностей.Руководители);
	Запрос.УстановитьПараметр("Профессионалы",		Справочники.КатегорииДолжностей.Профессионалы);
	Запрос.УстановитьПараметр("Специалисты",		Справочники.КатегорииДолжностей.Специалисты);
	Запрос.УстановитьПараметр("ТехническиеСлужащие", Справочники.КатегорииДолжностей.ТехническиеСлужащие);
	Запрос.УстановитьПараметр("РаботникиСферыТорговлиИУслуг", Справочники.КатегорииДолжностей.РаботникиСферыТорговлиИУслуг);
	Запрос.УстановитьПараметр("КвалифицированныеРаботникиСХ", Справочники.КатегорииДолжностей.КвалифицированныеРаботникиСХ);
	Запрос.УстановитьПараметр("КвалифицированныеРаботникиСИнструментом", Справочники.КатегорииДолжностей.КвалифицированныеРаботникиСИнструментом);
	Запрос.УстановитьПараметр("РабочиеПоТехническомуОбслуживанию", Справочники.КатегорииДолжностей.Рабочие);
	Запрос.УстановитьПараметр("ПростейшиеПрофессии",			Справочники.КатегорииДолжностей.ДругиеСлужащие);
	Запрос.УстановитьПараметр("ВидЗанятостиОсновноеМестоРаботы", Перечисления.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы);

	НаборСтрок = Новый ТаблицаЗначений;
	НаборСтрок.Колонки.Добавить("НомерСтрокиПП",Новый ОписаниеТипов("Число"));
	Для Инд = 1 по 10 Цикл
		НоваяСтрока = НаборСтрок.Добавить(); 
		НоваяСтрока.НомерСтрокиПП = Инд;
	КонецЦикла;
	Запрос.УстановитьПараметр("НаборСтрок", НаборСтрок);
    ТекстОсновногоЗапроса ="
	              |ВЫБРАТЬ 
				  |	НаборСтрок.НомерСтрокиПП КАК НомерСтрокиПП
				  |ПОМЕСТИТЬ ВТНаборСтрок
				  |ИЗ &НаборСтрок КАК НаборСтрок;
				  |
				  |ВЫБРАТЬ
	              |	РаботникиОрганизации.Должность.Категория КАК ДолжностьКатегория,
	              |	1 КАК ВсегоРаботающих,
	              |	ВЫБОР КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ВсегоВЗапасе,
	              |	ВЫБОР КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.Звание.Состав В (&Офицеры) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ОфицеровВЗапасе,
	              |	ВЫБОР КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.Звание.Состав В (&Прапорщики) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПрапорщиковВЗапасе,
	              |	ВЫБОР КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.Звание.Состав В (&Солдаты) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК СолдатВЗапасе,
	              |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ВсегоВЗапасеЗабронировано,
	              |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.Звание.Состав В (&Офицеры) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ОфицеровЗабронировано,
	              |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И (ВУ.Звание.Состав В (&Прапорщики) ИЛИ ВУ.Звание.Состав В (&Солдаты)) И РаботникиОрганизации.ФизЛицо.ДатаРождения > &До35лет ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПрапорщиковСолдатДо35Забронировано,
	              |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И (ВУ.Звание.Состав В (&Прапорщики) ИЛИ ВУ.Звание.Состав В (&Солдаты)) И РаботникиОрганизации.ФизЛицо.ДатаРождения > &До40лет И РаботникиОрганизации.ФизЛицо.ДатаРождения <= &До35лет ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПрапорщиковСолдатОт36До40Забронировано,
	              |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И (ВУ.Звание.Состав В (&Прапорщики) ИЛИ ВУ.Звание.Состав В (&Солдаты)) И РаботникиОрганизации.ФизЛицо.ДатаРождения > &До45лет И РаботникиОрганизации.ФизЛицо.ДатаРождения <= &До40лет ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПрапорщиковСолдатОт41До45Забронировано,
              	  |	ВЫБОР КОГДА ВУ.ЗабронированОрганизацией = &Организация И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И (ВУ.Звание.Состав В (&Прапорщики) ИЛИ ВУ.Звание.Состав В (&Солдаты)) И РаботникиОрганизации.ФизЛицо.ДатаРождения > &До60лет И РаботникиОрганизации.ФизЛицо.ДатаРождения <= &До45лет ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК ПрапорщиковСолдатОт46До60Забронировано,
	              |	ВЫБОР КОГДА НЕ(ВУ.ЗабронированОрганизацией = &Организация) И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И НЕ(ВУ.НаличиеМобпредписания) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК НезабронированноБезМобпредписаний,
	              |	ВЫБОР КОГДА НЕ(ВУ.ЗабронированОрганизацией = &Организация) И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.НаличиеМобпредписания ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК НезабронированноСМобпредписанием,
	              |	ВЫБОР КОГДА НЕ(ВУ.ЗабронированОрганизацией = &Организация) И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный И ВУ.НаличиеМобпредписания И ВУ.Звание.Состав В (&Офицеры) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК НезабронированноОфицеровСМобпредписанием,
				  |	ВЫБОР КОГДА РаботникиОрганизации.Должность.Категория = &Руководители ТОГДА 1 
				  |			КОГДА РаботникиОрганизации.Должность.Категория = &Профессионалы ТОГДА 2 
				  |			КОГДА РаботникиОрганизации.Должность.Категория = &Специалисты ТОГДА 3 
				  |       	КОГДА РаботникиОрганизации.Должность.Категория = &ТехническиеСлужащие ТОГДА 4 
				  |       	КОГДА РаботникиОрганизации.Должность.Категория = &РаботникиСферыТорговлиИУслуг ТОГДА 5
  				  |       	КОГДА РаботникиОрганизации.Должность.Категория = &КвалифицированныеРаботникиСХ ТОГДА 6
  				  |       	КОГДА РаботникиОрганизации.Должность.Категория = &КвалифицированныеРаботникиСИнструментом ТОГДА 7
  				  |       	КОГДА РаботникиОрганизации.Должность.Категория = &РабочиеПоТехническомуОбслуживанию ТОГДА 8
  				  |       	КОГДА РаботникиОрганизации.Должность.Категория = &ПростейшиеПрофессии ТОГДА 9
  				  |       	ИНАЧЕ 10
			      |КОНЕЦ КАК НомерСтрокиПП
				  |ПОМЕСТИТЬ ВТДанныеВУ
	              |ИЗ
	              |	(ВЫБРАТЬ
	              |		РаботникиОрганизацииСрезПоследних.Должность КАК Должность,
	              |		РаботникиОрганизацииСрезПоследних.Сотрудник.Физлицо КАК ФизЛицо
				  |	ИЗ
	              |		РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаАктуальности,Сотрудник.ВидЗанятости = &ВидЗанятостиОсновноеМестоРаботы) КАК РаботникиОрганизацииСрезПоследних
	              |	
	              |	ГДЕ
	              |		РаботникиОрганизацииСрезПоследних.Организация = &Организация И
	              |		(РаботникиОрганизацииСрезПоследних.ЗанимаемыхСтавок > 0)) КАК РаботникиОрганизации
	              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВоинскийУчет.СрезПоследних(&ДатаАктуальности, ) КАК ВУ
	              |		ПО РаботникиОрганизации.ФизЛицо = ВУ.Физлицо;
				  |
				  |ВЫБРАТЬ 
				  |	НаборСтрок.НомерСтрокиПП КАК НомерСтрокиПП,
				  | ДанныеВУ.ДолжностьКатегория КАК ДолжностьКатегория,
				  |	ДанныеВУ.ВсегоРаботающих КАК ВсегоРаботающих,
				  | ДанныеВУ.ВсегоВЗапасе КАК ВсегоВЗапасе,
				  | ДанныеВУ.ОфицеровВЗапасе КАК ОфицеровВЗапасе,
				  |	ДанныеВУ.ПрапорщиковВЗапасе КАК ПрапорщиковВЗапасе,
				  |	ДанныеВУ.СолдатВЗапасе КАК СолдатВЗапасе,
				  |	ДанныеВУ.ВсегоВЗапасеЗабронировано КАК ВсегоВЗапасеЗабронировано,
				  |	ДанныеВУ.ОфицеровЗабронировано КАК ОфицеровЗабронировано,
				  |	ДанныеВУ.ПрапорщиковСолдатДо35Забронировано КАК ПрапорщиковСолдатДо35Забронировано,
				  |	ДанныеВУ.ПрапорщиковСолдатОт36До40Забронировано КАК ПрапорщиковСолдатОт36До40Забронировано,
				  |	ДанныеВУ.ПрапорщиковСолдатОт41До45Забронировано КАК ПрапорщиковСолдатОт41До45Забронировано,
				  | ДанныеВУ.ПрапорщиковСолдатОт46До60Забронировано КАК ПрапорщиковСолдатОт46До60Забронировано,
				  | ДанныеВУ.НезабронированноБезМобпредписаний КАК НезабронированноБезМобпредписаний,
				  | ДанныеВУ.НезабронированноСМобпредписанием КАК НезабронированноСМобпредписанием,
				  | ДанныеВУ.НезабронированноОфицеровСМобпредписанием КАК НезабронированноОфицеровСМобпредписанием
				  |ИЗ 
				  | ВТНаборСтрок КАК НаборСтрок
				  |ЛЕВОЕ СОЕДИНЕНИЕ
				  | ВТДанныеВУ КАК ДанныеВУ 
				  |ПО
				  | НаборСтрок.НомерСтрокиПП = ДанныеВУ.НомерСтрокиПП
			  	  |УПОРЯДОЧИТЬ ПО
				  |	НомерСтрокиПП
				  |
				  |ИТОГИ СУММА(ВсегоРаботающих), СУММА(ВсегоВЗапасе), СУММА(ОфицеровВЗапасе), СУММА(ПрапорщиковВЗапасе), СУММА(СолдатВЗапасе), СУММА(ВсегоВЗапасеЗабронировано), СУММА(ОфицеровЗабронировано), СУММА(ПрапорщиковСолдатДо35Забронировано), СУММА(ПрапорщиковСолдатОт36До40Забронировано), СУММА(ПрапорщиковСолдатОт41До45Забронировано), СУММА(ПрапорщиковСолдатОт46До60Забронировано), СУММА(НезабронированноБезМобпредписаний), СУММА(НезабронированноСМобпредписанием), СУММА(НезабронированноОфицеровСМобпредписанием) ПО
				  |	ОБЩИЕ,
				  |	НомерСтрокиПП";
						   
	ДокументРезультат.Очистить();
	
	Макет 			= ПолучитьМакет("Форма_от_04_02_2015");
	ОбластьШапка 	= Макет.ПолучитьОбласть("ШапкаФормы");
	ОбластьШапка.Параметры.ДатаАктуальности		=	Формат(ДатаАктуальности, "ДЛФ=D");
	ОбластьШапка.Параметры.НазваниеОрганизации 	= 	Организация.НаименованиеПолное;
	ДокументРезультат.Присоединить(ОбластьШапка);
	
	Запрос.Текст = ТекстОсновногоЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаПоКатегориям	=	РезультатЗапроса.Выбрать();
	Пока ВыборкаПоКатегориям.Следующий() Цикл 
		Если	ВыборкаПоКатегориям.Уровень()	=	0	тогда
			ОбластьПодвал 	= Макет.ПолучитьОбласть("ПодвалФормы");
			ОбластьПодвал.Параметры.ВсегоРаботающих								= ВыборкаПоКатегориям.ВсегоРаботающих;
			ОбластьПодвал.Параметры.ВсегоВЗапасе 								= ВыборкаПоКатегориям.ВсегоВЗапасе;
			ОбластьПодвал.Параметры.ОфицеровВЗапасе 							= ВыборкаПоКатегориям.ОфицеровВЗапасе;
			ОбластьПодвал.Параметры.ПрапорщиковВЗапасе 							= ВыборкаПоКатегориям.ПрапорщиковВЗапасе;
			ОбластьПодвал.Параметры.СолдатВЗапасе 								= ВыборкаПоКатегориям.СолдатВЗапасе;
			ОбластьПодвал.Параметры.ВсегоВЗапасеЗабронировано 					= ВыборкаПоКатегориям.ВсегоВЗапасеЗабронировано;
			ОбластьПодвал.Параметры.ОфицеровЗабронировано 						= ВыборкаПоКатегориям.ОфицеровЗабронировано;
			ОбластьПодвал.Параметры.ПрапорщиковСолдатДо35Забронировано          = ВыборкаПоКатегориям.ПрапорщиковСолдатДо35Забронировано;
			ОбластьПодвал.Параметры.ПрапорщиковСолдатОт36До40Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт36До40Забронировано;
			ОбластьПодвал.Параметры.ПрапорщиковСолдатОт41До45Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт41До45Забронировано;
			ОбластьПодвал.Параметры.ПрапорщиковСолдатОт46До60Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт46До60Забронировано;
			ОбластьПодвал.Параметры.НезабронированноБезМобпредписаний 			= ВыборкаПоКатегориям.НезабронированноБезМобпредписаний;
			
			ОтветственныеЛицаОрганизации	=	РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация,ДатаАктуальности);
			ОбластьПодвал.Параметры.ДолжностьРуководителя	=	ОтветственныеЛицаОрганизации.РуководительДолжность;
			ОбластьПодвал.Параметры.ФИОРуководителя			=	ОтветственныеЛицаОрганизации.Руководитель;
			Продолжить;
		КонецЕсли; 
		Пока	ВыборкаПоКатегориям.СледующийПоЗначениюПоля("НомерСтрокиПП") Цикл
			ИндексСтроки = "СтрокаФормы" + ВыборкаПоКатегориям.НомерСтрокиПП;
			ОбластьСтрока 	= Макет.ПолучитьОбласть(ИндексСтроки);
			ОбластьСтрока.Параметры.ВсегоРаботающих								= ВыборкаПоКатегориям.ВсегоРаботающих;
			ОбластьСтрока.Параметры.ВсегоРаботающих								= ВыборкаПоКатегориям.ВсегоРаботающих;
			ОбластьСтрока.Параметры.ВсегоВЗапасе 								= ВыборкаПоКатегориям.ВсегоВЗапасе;
			ОбластьСтрока.Параметры.ОфицеровВЗапасе 							= ВыборкаПоКатегориям.ОфицеровВЗапасе;
			ОбластьСтрока.Параметры.ПрапорщиковВЗапасе 							= ВыборкаПоКатегориям.ПрапорщиковВЗапасе;
			ОбластьСтрока.Параметры.СолдатВЗапасе 								= ВыборкаПоКатегориям.СолдатВЗапасе;
			ОбластьСтрока.Параметры.ВсегоВЗапасеЗабронировано 					= ВыборкаПоКатегориям.ВсегоВЗапасеЗабронировано;
			ОбластьСтрока.Параметры.ОфицеровЗабронировано 						= ВыборкаПоКатегориям.ОфицеровЗабронировано;
			ОбластьСтрока.Параметры.ПрапорщиковСолдатДо35Забронировано 		    = ВыборкаПоКатегориям.ПрапорщиковСолдатДо35Забронировано;
			ОбластьСтрока.Параметры.ПрапорщиковСолдатОт36До40Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт36До40Забронировано;
			ОбластьСтрока.Параметры.ПрапорщиковСолдатОт41До45Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт41До45Забронировано;
			ОбластьСтрока.Параметры.ПрапорщиковСолдатОт46До60Забронировано 		= ВыборкаПоКатегориям.ПрапорщиковСолдатОт46До60Забронировано;
			ОбластьСтрока.Параметры.НезабронированноБезМобпредписаний 			= ВыборкаПоКатегориям.НезабронированноБезМобпредписаний;
			Если ВыборкаПоКатегориям.НомерСтрокиПП = 10 И
				НЕ ЗначениеЗаполнено(ВыборкаПоКатегориям.ВсегоРаботающих)
				И НЕ ЗначениеЗаполнено(ВыборкаПоКатегориям.ВсегоРаботающих)
				И НЕ ЗначениеЗаполнено(ВыборкаПоКатегориям.ВсегоВЗапасе)
				И НЕ ЗначениеЗаполнено(ВыборкаПоКатегориям.ОфицеровВЗапасе)
				И НЕ ЗначениеЗаполнено(ВыборкаПоКатегориям.ПрапорщиковВЗапасе)
				И НЕ ЗначениеЗаполнено(ВыборкаПоКатегориям.СолдатВЗапасе)
				И НЕ ЗначениеЗаполнено(ВыборкаПоКатегориям.ВсегоВЗапасеЗабронировано)
				И НЕ ЗначениеЗаполнено(ВыборкаПоКатегориям.ОфицеровЗабронировано)
				И НЕ ЗначениеЗаполнено(ВыборкаПоКатегориям.ПрапорщиковСолдатОт36До40Забронировано)
				И НЕ ЗначениеЗаполнено(ВыборкаПоКатегориям.ПрапорщиковСолдатОт41До45Забронировано)
				И НЕ ЗначениеЗаполнено(ВыборкаПоКатегориям.ПрапорщиковСолдатОт46До60Забронировано)
				И НЕ ЗначениеЗаполнено(ВыборкаПоКатегориям.НезабронированноБезМобпредписаний) Тогда Продолжить 
			КонецЕсли;
			ДокументРезультат.Присоединить(ОбластьСтрока);
		КонецЦикла;
	КонецЦикла;
	
	
	Если ТипЗнч(ОбластьПодвал)	=	Тип("ТабличныйДокумент")	тогда
		ДокументРезультат.Присоединить(ОбластьПодвал);
	КонецЕсли;
	
	//Параметры документа
	ДокументРезультат.Автомасштаб 			= Истина;
	ДокументРезультат.ОриентацияСтраницы 	= ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.Защита              	= Ложь;
	
КонецПроцедуры

#КонецЕсли