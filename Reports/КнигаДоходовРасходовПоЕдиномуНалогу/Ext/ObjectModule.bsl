
//Процедура выводит заголовок в табличный документ
//
Процедура ВывестиЗаголовок(ТабДокумент, ВысотаЗаголовка) Экспорт
	
	Если ЗаголовокСформирован Тогда
		Возврат
	КонецЕсли;
	
	мТекОрганизация  = Организация;
	Макет = ПолучитьМакет("Макет");
	ТабДокумент.Очистить();
	ОбластьМакета    = Макет.ПолучитьОбласть("Шапка");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета    = Макет.ПолучитьОбласть("ЗаголовокТаблицыДоходы");
	ТабДокумент.Вывести(ОбластьМакета);
	ВысотаЗаголовка     = ТабДокумент.ВысотаТаблицы;
	ЗаголовокСформирован = Истина;
	
КонецПроцедуры // ВывестиЗаголовок()

Процедура СформироватьОтчет(ТабДокумент, ВысотаЗаголовка) Экспорт
	
	ЗаголовокСформирован = Ложь;
	
	ВывестиЗаголовок(ТабДокумент, ВысотаЗаголовка);
	
	Макет = ПолучитьМакет("Макет");
	
	ОтчетКнигаДоходов = Отчеты.КнигаДоходовПоЕдиномуНалогу.Создать();
	ЗаполнитьЗначенияСвойств(ОтчетКнигаДоходов, ЭтотОбъект);
	ОтчетКнигаДоходов.СформироватьОтчет(ТабДокумент, ВысотаЗаголовка, Ложь);
	
	ОбластьМакета    = Макет.ПолучитьОбласть("ЗаголовокТаблицыРасходы");
	ТабДокумент.Вывести(ОбластьМакета);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачГода",         ДатаНачалаНарастающегоИтога);
	Запрос.УстановитьПараметр("НачКвартала",     Макс(ДатаНачалаНарастающегоИтога, НачалоКвартала(НачалоПериода)));
	Запрос.УстановитьПараметр("ПередНачПериода", НачалоПериода - 1);
	Запрос.УстановитьПараметр("НачПериода",      НачалоПериода);
	Запрос.УстановитьПараметр("КонПериода",      КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("Организация",     Организация);
	
	Запрос.УстановитьПараметр("СтатьяЗатратыСвязанныеСПриобретением", Справочники.СтатьиНалоговыхДеклараций.ЕННК_ЗатратыЗатратыСвязанныеСПриобретением);
	Запрос.УстановитьПараметр("СтатьяОплатаТруда",                    Справочники.СтатьиНалоговыхДеклараций.ЕННК_ЗатратыОплатаТруда);
	Запрос.УстановитьПараметр("СтатьяЕСВ",                            Справочники.СтатьиНалоговыхДеклараций.ЕННК_ЗатратыЕСВ);
	Запрос.УстановитьПараметр("СтатьяДругиеЗатраты",                  Справочники.СтатьиНалоговыхДеклараций.ЕННК_ЗатратыДругиеЗатраты);
	Запрос.УстановитьПараметр("СтатьяЗатраты",                        Справочники.СтатьиНалоговыхДеклараций.ЕННК_Затраты);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяЗатратыСвязанныеСПриобретением ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК Статья3,
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяОплатаТруда ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК Статья4,
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяЕСВ ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК Статья5,
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяДругиеЗатраты ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК Статья6,
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяЗатратыСвязанныеСПриобретением ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяОплатаТруда ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяЕСВ ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяДругиеЗатраты ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК Статья7,
	|	NULL      КАК ПериодМесяц,
	|	NULL      КАК ПериодДень,
	|	NULL      КАК Период,
	|	NULL      КАК НомерДокумента,
	|	NULL      КАК Регистратор,
	|	NULL      КАК Статья
	|ИЗ
	|	РегистрНакопления.КнигаДоходовРасходовПоЕдиномуНалогу.Обороты(&НачГода, &ПередНачПериода, ,
	|	                  Организация = &Организация) КАК ОборотСНачалаГода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяЗатратыСвязанныеСПриобретением ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяОплатаТруда ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяЕСВ ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяДругиеЗатраты ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяЗатратыСвязанныеСПриобретением ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяОплатаТруда ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяЕСВ ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяДругиеЗатраты ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	NULL,
	|	NULL,
	|	1,
	|	NULL,
	|	NULL,
	|	NULL
	|ИЗ
	|	РегистрНакопления.КнигаДоходовРасходовПоЕдиномуНалогу.Обороты(&НачКвартала, &ПередНачПериода, ,
	|	                  Организация = &Организация) КАК ОборотСНачалаКвартала
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяЗатратыСвязанныеСПриобретением ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяОплатаТруда ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяЕСВ ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяДругиеЗатраты ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяЗатратыСвязанныеСПриобретением ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяОплатаТруда ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяЕСВ ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяДругиеЗатраты ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	НАЧАЛОПЕРИОДА(КнигаДоходовРасходов.Период, МЕСЯЦ),
	|	НАЧАЛОПЕРИОДА(КнигаДоходовРасходов.Период, ДЕНЬ),
	|	КнигаДоходовРасходов.Период,
	|	КнигаДоходовРасходов.НомерПлатежногоДокумента,
	|	КнигаДоходовРасходов.Регистратор,
	|	КнигаДоходовРасходов.Статья
	|ИЗ
	|	РегистрНакопления.КнигаДоходовРасходовПоЕдиномуНалогу КАК КнигаДоходовРасходов
	|
	|ГДЕ
	|	КнигаДоходовРасходов.Организация = &Организация И
	|	КнигаДоходовРасходов.Статья В ИЕРАРХИИ(&СтатьяЗатраты) И
	|	КнигаДоходовРасходов.Период     >= &НачПериода И
	|	КнигаДоходовРасходов.Период     <= &КонПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(КнигаДоходовРасходов.Период, МЕСЯЦ),
	|	НАЧАЛОПЕРИОДА(КнигаДоходовРасходов.Период, ДЕНЬ),
	|	КнигаДоходовРасходов.Период,
	|	КнигаДоходовРасходов.НомерПлатежногоДокумента,
	|	КнигаДоходовРасходов.Регистратор,
	|	КнигаДоходовРасходов.Статья
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПериодМесяц,
	|	ПериодДень,
	|	Период
	|
	|ИТОГИ 
	|	   СУММА(Статья3),
	|	   СУММА(Статья4),
	|	   СУММА(Статья5),
	|	   СУММА(Статья6),
	|	   СУММА(Статья7)
	|	ПО
	|	ПериодМесяц,
	|	ПериодДень";
	ВыборкаМесяц = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОбластьСтрока    = Макет.ПолучитьОбласть("Строка");
	ОбластьДень      = Макет.ПолучитьОбласть("День");
	ОбластьМесяц     = Макет.ПолучитьОбласть("Месяц");
	ОбластьКвартал   = Макет.ПолучитьОбласть("Квартал");
	ОбластьГод       = Макет.ПолучитьОбласть("Год");
	ПараметрыСтрока  = ОбластьСтрока.Параметры;
	ПараметрыДень    = ОбластьДень.Параметры;
	ПараметрыМесяц   = ОбластьМесяц.Параметры;
	ПараметрыКвартал = ОбластьКвартал.Параметры;
	ПараметрыГод     = ОбластьГод.Параметры;
	ТекГод           = НачалоГода(НачалоПериода);
	ТекКвартал       = НачалоКвартала(НачалоПериода);
	
	ТабДокумент.НачатьГруппуСтрок("Год");
	ПараметрыГод.Итого   = "Наростаючим підсумком" + Символы.ПС + "за " + Формат(ТекГод,"ДФ=yyyy") + " рік:";
	
	ТабДокумент.НачатьГруппуСтрок("Квартал");
	ПараметрыКвартал.Итого   = "Наростаючим підсумком" + Символы.ПС + "за "
								+ Формат(ТекКвартал,"ДФ=q") + " квартал "
								+ Формат(ТекКвартал,"ДФ=yyyy") + " року:";
	
	Если ВыборкаМесяц.Следующий() Тогда
									
		ВыборкаДень = ВыборкаМесяц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если ВыборкаДень.Следующий() Тогда
			
			ВыборкаПериоды = ВыборкаДень.Выбрать();
			
			//Получение оборотов с начала года
			Если ВыборкаПериоды.Следующий() Тогда
				
				ПараметрыГод.Статья3 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья3);
				ПараметрыГод.Статья4 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья4);
				ПараметрыГод.Статья5 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья5);
				ПараметрыГод.Статья6 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья6);
				ПараметрыГод.Статья7 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья7);
				
			КонецЕсли;
			
			//Получение оборотов с начала квартала
			Если ВыборкаПериоды.Следующий() Тогда
				
				ПараметрыКвартал.Статья3 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья3);
				ПараметрыКвартал.Статья4 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья4);
				ПараметрыКвартал.Статья5 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья5);
				ПараметрыКвартал.Статья6 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья6);
				ПараметрыКвартал.Статья7 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья7);
				
			КонецЕсли;
			
		КонецЕсли;
		
		
	КонецЕсли;
	
	Начало = Истина;
	
	Пока ВыборкаМесяц.Следующий() Цикл
		
		Месяц         = ВыборкаМесяц.ПериодМесяц;
		Квартал       = НачалоКвартала(Месяц);
		Год           = НачалоГода(Месяц);
		СменаКвартала = (Квартал <> ТекКвартал);
		СменаГода     = (Год <> ТекГод);
		
		Если Не Начало Тогда
			
			Если СменаКвартала Тогда
				
				ТабДокумент.ЗакончитьГруппуСтрок();//Закончить группу "Квартал"
				ТабДокумент.Вывести(ОбластьКвартал);	
				
			КонецЕсли;
			
			Если СменаГода Тогда
				
				ТабДокумент.ЗакончитьГруппуСтрок();//Закончить группу "Год"
				ТабДокумент.Вывести(ОбластьГод);
				
			КонецЕсли;
			
			Если СменаКвартала Тогда
				
				Если СменаГода Тогда
					
					ТабДокумент.НачатьГруппуСтрок("Год");
					
				КонецЕсли;
				
				ТабДокумент.НачатьГруппуСтрок("Квартал");
				
			КонецЕсли;
			
		Иначе
			
			Начало = Ложь
			
		КонецЕсли;
				
		ТабДокумент.НачатьГруппуСтрок("Месяц");
		
		ВыборкаДень =  ВыборкаМесяц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаДень.Следующий() Цикл
		
			ТабДокумент.НачатьГруппуСтрок("День");
			
			ВыборкаДокументы = ВыборкаДень.Выбрать();
			
			Пока ВыборкаДокументы.Следующий() Цикл
				
				ПараметрыСтрока.Заполнить(ВыборкаДокументы);
					
				ПараметрыСтрока.ДатаНомер = Формат(ВыборкаДокументы.Период, "ДФ=dd.MM.yyyy");
				ТабДокумент.Вывести(ОбластьСтрока);							
				
			КонецЦикла;
			
			ТабДокумент.ЗакончитьГруппуСтрок();//Закончить группу "День"
			ПараметрыДень.Заполнить(ВыборкаДень);
			День = ВыборкаДень.ПериодДень;
			ПараметрыДень.Итого = "Разом за " + Формат(День, "ДЛФ=D") + ":";
			ТабДокумент.Вывести(ОбластьДень);
		
		КонецЦикла;
		
		ТабДокумент.ЗакончитьГруппуСтрок();//Закончить группу "Месяц"
		ПараметрыМесяц.Заполнить(ВыборкаМесяц);
		Месяц = ВыборкаМесяц.ПериодМесяц;
		ПараметрыМесяц.Итого = "Разом за " + Формат(Месяц, "Л=uk_UA; ДФ='MMMM yyyy'") + ":";
	    ТабДокумент.Вывести(ОбластьМесяц);
		
		Если СменаКвартала Тогда
			
			ТекКвартал               = Квартал;
			ПараметрыКвартал.Итого   = "Наростаючим підсумком" + Символы.ПС + "за "
			                           + Формат(ТекКвартал,"ДФ=q") + " квартал "
			                           + Формат(ТекКвартал,"ДФ=yyyy") + " року:";
			ПараметрыКвартал.Статья3 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья3);
			ПараметрыКвартал.Статья4 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья4);
			ПараметрыКвартал.Статья5 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья5);
			ПараметрыКвартал.Статья6 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья6);
			ПараметрыКвартал.Статья7 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья7);
			
		Иначе
			
			ПараметрыКвартал.Статья3 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья3)
			                           + ПараметрыКвартал.Статья3;
			ПараметрыКвартал.Статья4 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья4)
			                           + ПараметрыКвартал.Статья4;
			ПараметрыКвартал.Статья5 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья5)
			                           + ПараметрыКвартал.Статья5;
			ПараметрыКвартал.Статья6 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья6)
			                           + ПараметрыКвартал.Статья6;
			ПараметрыКвартал.Статья7 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья7)
			                           + ПараметрыКвартал.Статья7;
			
		КонецЕсли;
		
		
		Если СменаГода Тогда
			
			ТекГод               = Год;
			ПараметрыГод.Итого   = "Наростаючим підсумком" + Символы.ПС + "за " + Формат(ТекГод,"ДФ=yyyy") + " рік:";
			
			ПараметрыГод.Статья3 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья3);
			ПараметрыГод.Статья4 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья4);
			ПараметрыГод.Статья5 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья5);
			ПараметрыГод.Статья6 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья6);
			ПараметрыГод.Статья7 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья7);	
			
		Иначе
			
			ПараметрыГод.Статья3 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья3)
			                       + ПараметрыГод.Статья3;
			ПараметрыГод.Статья4 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья4)
			                       + ПараметрыГод.Статья4;
			ПараметрыГод.Статья5 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья5)
			                       + ПараметрыГод.Статья5;
			ПараметрыГод.Статья6 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья6)
			                       + ПараметрыГод.Статья6;
			ПараметрыГод.Статья7 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья7)
			                       + ПараметрыГод.Статья7;
			
		КонецЕсли;
		
		
	КонецЦикла;
	
	ТабДокумент.ЗакончитьГруппуСтрок();//Закончить группу "Квартал"
	ТабДокумент.Вывести(ОбластьКвартал);
	ТабДокумент.ЗакончитьГруппуСтрок();//Закончить группу "Год"
	ТабДокумент.Вывести(ОбластьГод);
	
	ТабДокумент.ИмяПараметровПечати = "КнигаДоходовРасходовПоЕдиномуНалогу";
	
КонецПроцедуры
