
//Процедура выводит заголовок в табличный документ
//
Процедура ВывестиЗаголовок(ТабДокумент, ВысотаЗаголовка) Экспорт
	
	Если ЗаголовокСформирован Тогда
		Возврат
	КонецЕсли;
	
	мТекОрганизация  = Организация;
	Макет = ПолучитьМакет("Макет");
	ТабДокумент.Очистить();
	ОбластьМакета    = Макет.ПолучитьОбласть("Шапка");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета    = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабДокумент.Вывести(ОбластьМакета);
	ВысотаЗаголовка     = ТабДокумент.ВысотаТаблицы;
	ЗаголовокСформирован = Истина;
	
КонецПроцедуры // ВывестиЗаголовок()

Процедура СформироватьОтчет(ТабДокумент, ВысотаЗаголовка, ВыводитьЗаголовок = Истина) Экспорт
	
	ЗаголовокСформирован = Ложь;
	
	Макет = ПолучитьМакет("Макет");
	Если ВыводитьЗаголовок Тогда
		ВывестиЗаголовок(ТабДокумент, ВысотаЗаголовка);
	КонецЕсли;
	ТабДокумент.ИтогиСнизу               = Истина;
	ТабДокумент.ОбластьПечати            = ТабДокумент.Область( , 2, , ТабДокумент.ШиринаТаблицы);
	ТабДокумент.ПовторятьПриПечатиСтроки = ТабДокумент.Область(ВысотаЗаголовка, , ВысотаЗаголовка, );
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачГода",         ДатаНачалаНарастающегоИтога);
	Запрос.УстановитьПараметр("НачКвартала",     Макс(ДатаНачалаНарастающегоИтога, НачалоКвартала(НачалоПериода)));
	Запрос.УстановитьПараметр("ПередНачПериода", НачалоПериода - 1);
	Запрос.УстановитьПараметр("НачПериода",      НачалоПериода);
	Запрос.УстановитьПараметр("КонПериода",      КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("Организация",     Организация);
	
	Запрос.УстановитьПараметр("СтатьяРеализация",                Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыРеализация);
	Запрос.УстановитьПараметр("СтатьяВозвраты",                  Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыВозвраты);
	Запрос.УстановитьПараметр("СтатьяБезоплатноПолученные",      Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыСтоимостьБезоплатноПолученных);
	Запрос.УстановитьПараметр("СтатьяПросроченнаяЗадолженность", Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыПросроченнаяЗадолженность);
	СписокСтатейПревышений = Новый СписокЗначений;
	СписокСтатейПревышений.Добавить(Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыДоходыПоВидуДеятельностиНеУказанномуВСвидетельстве);
	СписокСтатейПревышений.Добавить(Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыДоходыДругойСпособРасчетов);
	СписокСтатейПревышений.Добавить(Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыДоходыПоВидуДеятельностиНеЕдиныйНалог);
	Запрос.УстановитьПараметр("СписокСтатейПревышений", СписокСтатейПревышений);
	Запрос.УстановитьПараметр("СтатьяДоходы",                    Справочники.СтатьиНалоговыхДеклараций.ЕННК_Доходы);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяРеализация ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК Статья2,
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяВозвраты ТОГДА -ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК Статья3,
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяРеализация ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяВозвраты ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК Статья4,
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяБезоплатноПолученные ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК Статья5,
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяПросроченнаяЗадолженность ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК Статья6,
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяРеализация ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяВозвраты ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяБезоплатноПолученные ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья = &СтатьяПросроченнаяЗадолженность ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК Статья7,
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаГода.Статья В(&СписокСтатейПревышений) ТОГДА ОборотСНачалаГода.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) КАК Статья9,
	|	NULL      КАК ПериодМесяц,
	|	NULL      КАК ПериодДень,
	|	NULL      КАК Период,
	|	NULL      КАК НомерДокумента,
	|	NULL      КАК Регистратор,
	|	NULL      КАК Статья
	|ИЗ
	|	РегистрНакопления.КнигаДоходовРасходовПоЕдиномуНалогу.Обороты(&НачГода, &ПередНачПериода, ,
	|	                  Организация = &Организация) КАК ОборотСНачалаГода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяРеализация ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяВозвраты ТОГДА -ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяРеализация ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяВозвраты ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяБезоплатноПолученные ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяПросроченнаяЗадолженность ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяРеализация ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяВозвраты ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяБезоплатноПолученные ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья = &СтатьяПросроченнаяЗадолженность ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА ОборотСНачалаКвартала.Статья В(&СписокСтатейПревышений) ТОГДА ОборотСНачалаКвартала.СуммаОборот
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	NULL,
	|	NULL,
	|	1,
	|	NULL,
	|	NULL,
	|	NULL
	|ИЗ
	|	РегистрНакопления.КнигаДоходовРасходовПоЕдиномуНалогу.Обороты(&НачКвартала, &ПередНачПериода, ,
	|	                  Организация = &Организация) КАК ОборотСНачалаКвартала
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяРеализация ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяВозвраты ТОГДА -КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяРеализация ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяВозвраты ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяБезоплатноПолученные ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяПросроченнаяЗадолженность ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяРеализация ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяВозвраты ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяБезоплатноПолученные ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ) +
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья = &СтатьяПросроченнаяЗадолженность ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	СУММА(
	|		ВЫБОР КОГДА КнигаДоходовРасходов.Статья В(&СписокСтатейПревышений) ТОГДА КнигаДоходовРасходов.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ),
	|	НАЧАЛОПЕРИОДА(КнигаДоходовРасходов.Период, МЕСЯЦ),
	|	НАЧАЛОПЕРИОДА(КнигаДоходовРасходов.Период, ДЕНЬ),
	|	КнигаДоходовРасходов.Период,
	|	КнигаДоходовРасходов.НомерПлатежногоДокумента,
	|	КнигаДоходовРасходов.Регистратор,
	|	КнигаДоходовРасходов.Статья
	|ИЗ
	|	РегистрНакопления.КнигаДоходовРасходовПоЕдиномуНалогу КАК КнигаДоходовРасходов
	|
	|ГДЕ
	|	КнигаДоходовРасходов.Организация = &Организация И
	|	КнигаДоходовРасходов.Статья В ИЕРАРХИИ(&СтатьяДоходы) И
	|	КнигаДоходовРасходов.Период     >= &НачПериода И
	|	КнигаДоходовРасходов.Период     <= &КонПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(КнигаДоходовРасходов.Период, МЕСЯЦ),
	|	НАЧАЛОПЕРИОДА(КнигаДоходовРасходов.Период, ДЕНЬ),
	|	КнигаДоходовРасходов.Период,
	|	КнигаДоходовРасходов.НомерПлатежногоДокумента,
	|	КнигаДоходовРасходов.Регистратор,
	|	КнигаДоходовРасходов.Статья
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПериодМесяц,
	|	ПериодДень,
	|	Период
	|
	|ИТОГИ СУММА(Статья2),
	|	   СУММА(Статья3),
	|	   СУММА(Статья4),
	|	   СУММА(Статья5),
	|	   СУММА(Статья6),
	|	   СУММА(Статья7),
	|	   СУММА(Статья9)
	|	ПО
	|	ПериодМесяц,
	|	ПериодДень";
	ВыборкаМесяц = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОбластьСтрока    = Макет.ПолучитьОбласть("Строка");
	ОбластьДень      = Макет.ПолучитьОбласть("День");
	ОбластьМесяц     = Макет.ПолучитьОбласть("Месяц");
	ОбластьКвартал   = Макет.ПолучитьОбласть("Квартал");
	ОбластьГод       = Макет.ПолучитьОбласть("Год");
	ОбластьПодвал    = Макет.ПолучитьОбласть("Подвал");
	ПараметрыСтрока  = ОбластьСтрока.Параметры;
	ПараметрыДень    = ОбластьДень.Параметры;
	ПараметрыМесяц   = ОбластьМесяц.Параметры;
	ПараметрыКвартал = ОбластьКвартал.Параметры;
	ПараметрыГод     = ОбластьГод.Параметры;
	ТекГод           = НачалоГода(НачалоПериода);
	ТекКвартал       = НачалоКвартала(НачалоПериода);
	
	ТабДокумент.НачатьГруппуСтрок("Год");
	ПараметрыГод.Итого   = "Наростаючим підсумком" + Символы.ПС + "за " + Формат(ТекГод,"ДФ=yyyy") + " рік:";
	
	ТабДокумент.НачатьГруппуСтрок("Квартал");
	ПараметрыКвартал.Итого   = "Наростаючим підсумком" + Символы.ПС + "за "
								+ Формат(ТекКвартал,"ДФ=q") + " квартал "
								+ Формат(ТекКвартал,"ДФ=yyyy") + " року:";
	
	Если ВыборкаМесяц.Следующий() Тогда
									
		ВыборкаДень = ВыборкаМесяц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если ВыборкаДень.Следующий() Тогда
			
			ВыборкаПериоды = ВыборкаДень.Выбрать();
			
			//Получение оборотов с начала года
			Если ВыборкаПериоды.Следующий() Тогда
				
				ПараметрыГод.Статья2 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья2);
				ПараметрыГод.Статья3 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья3);
				ПараметрыГод.Статья4 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья4);
				ПараметрыГод.Статья5 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья5);
				ПараметрыГод.Статья6 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья6);
				ПараметрыГод.Статья7 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья7);
				ПараметрыГод.Статья9 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья9);
				
			КонецЕсли;
			
			//Получение оборотов с начала квартала
			Если ВыборкаПериоды.Следующий() Тогда
				
				ПараметрыКвартал.Статья2 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья2);
				ПараметрыКвартал.Статья3 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья3);
				ПараметрыКвартал.Статья4 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья4);
				ПараметрыКвартал.Статья5 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья5);
				ПараметрыКвартал.Статья6 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья6);
				ПараметрыКвартал.Статья7 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья7);
				ПараметрыКвартал.Статья9 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаПериоды.Статья9);
				
			КонецЕсли;
			
		КонецЕсли;
		
		
	КонецЕсли;
	
	Начало = Истина;
	
	Пока ВыборкаМесяц.Следующий() Цикл
		
		Месяц         = ВыборкаМесяц.ПериодМесяц;
		Квартал       = НачалоКвартала(Месяц);
		Год           = НачалоГода(Месяц);
		СменаКвартала = (Квартал <> ТекКвартал);
		СменаГода     = (Год <> ТекГод);
		
		Если Не Начало Тогда
			
			Если СменаКвартала Тогда
				
				ТабДокумент.ЗакончитьГруппуСтрок();//Закончить группу "Квартал"
				ТабДокумент.Вывести(ОбластьКвартал);	
				
			КонецЕсли;
			
			Если СменаГода Тогда
				
				ТабДокумент.ЗакончитьГруппуСтрок();//Закончить группу "Год"
				ТабДокумент.Вывести(ОбластьГод);
				
			КонецЕсли;
			
			Если СменаКвартала Тогда
				
				Если СменаГода Тогда
					
					ТабДокумент.НачатьГруппуСтрок("Год");
					
				КонецЕсли;
				
				ТабДокумент.НачатьГруппуСтрок("Квартал");
				
			КонецЕсли;
			
		Иначе
			
			Начало = Ложь
			
		КонецЕсли;
				
		ТабДокумент.НачатьГруппуСтрок("Месяц");
		
		ВыборкаДень =  ВыборкаМесяц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаДень.Следующий() Цикл
		
			ТабДокумент.НачатьГруппуСтрок("День");
			
			ВыборкаДокументы = ВыборкаДень.Выбрать();
			
			Пока ВыборкаДокументы.Следующий() Цикл
				
				ПараметрыСтрока.Заполнить(ВыборкаДокументы);
				ПараметрыСтрока.Статья4 = 0;
				ПараметрыСтрока.Статья7 = 0;
				
				// Заполнение вида дохода в колонке 8
				ВидДохода = "";
				Если ВыборкаДокументы.Статья = Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыДоходыПоВидуДеятельностиНеУказанномуВСвидетельстве Тогда
					ВидДохода = "пп.2 п.293.4";
				ИначеЕсли ВыборкаДокументы.Статья = Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыДоходыДругойСпособРасчетов Тогда
					ВидДохода = "пп.3 п.293.4";
				ИначеЕсли ВыборкаДокументы.Статья = Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыДоходыПоВидуДеятельностиНеЕдиныйНалог Тогда
					ВидДохода = "пп.4 п.293.4";
				КонецЕсли;
				ПараметрыСтрока.Статья8 = ВидДохода;
					
				ПараметрыСтрока.ДатаНомер = Формат(ВыборкаДокументы.Период, "ДФ=dd.MM.yyyy");
				ТабДокумент.Вывести(ОбластьСтрока);							
				
			КонецЦикла;
			
			ТабДокумент.ЗакончитьГруппуСтрок();//Закончить группу "День"
			ПараметрыДень.Заполнить(ВыборкаДень);
			День = ВыборкаДень.ПериодДень;
			ПараметрыДень.Итого = "Разом за " + Формат(День, "ДЛФ=D") + ":";
			ТабДокумент.Вывести(ОбластьДень);
		
		КонецЦикла;
		
		ТабДокумент.ЗакончитьГруппуСтрок();//Закончить группу "Месяц"
		ПараметрыМесяц.Заполнить(ВыборкаМесяц);
		Месяц = ВыборкаМесяц.ПериодМесяц;
		ПараметрыМесяц.Итого = "Разом за " + Формат(Месяц, "Л=uk_UA; ДФ='MMMM yyyy'") + ":";
	    ТабДокумент.Вывести(ОбластьМесяц);
		
		Если СменаКвартала Тогда
			
			ТекКвартал               = Квартал;
			ПараметрыКвартал.Итого   = "Наростаючим підсумком" + Символы.ПС + "за "
			                           + Формат(ТекКвартал,"ДФ=q") + " квартал "
			                           + Формат(ТекКвартал,"ДФ=yyyy") + " року:";
			ПараметрыКвартал.Статья2 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья2);
			ПараметрыКвартал.Статья3 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья3);
			ПараметрыКвартал.Статья4 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья4);

			ПараметрыКвартал.Статья5 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья5);
			ПараметрыКвартал.Статья6 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья6);
			ПараметрыКвартал.Статья7 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья7);
			ПараметрыКвартал.Статья9 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья9);
			
		Иначе
			
			ПараметрыКвартал.Статья2 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья2)
			                           + ПараметрыКвартал.Статья2;
			ПараметрыКвартал.Статья3 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья3)
			                           + ПараметрыКвартал.Статья3;
			ПараметрыКвартал.Статья4 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья4)
			                           + ПараметрыКвартал.Статья4;
			ПараметрыКвартал.Статья5 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья5)
			                           + ПараметрыКвартал.Статья5;
			ПараметрыКвартал.Статья6 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья6)
			                           + ПараметрыКвартал.Статья6;
			ПараметрыКвартал.Статья7 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья7)
			                           + ПараметрыКвартал.Статья7;
			ПараметрыКвартал.Статья9 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья9)
			                           + ПараметрыКвартал.Статья9;
			
		КонецЕсли;
		
		
		Если СменаГода Тогда
			
			ТекГод               = Год;
			ПараметрыГод.Итого   = "Наростаючим підсумком" + Символы.ПС + "за " + Формат(ТекГод,"ДФ=yyyy") + " рік:";
			
			ПараметрыГод.Статья2 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья2);
			ПараметрыГод.Статья3 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья3);
			ПараметрыГод.Статья4 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья4);
			ПараметрыГод.Статья5 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья5);
			ПараметрыГод.Статья6 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья6);
			ПараметрыГод.Статья7 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья7);	
			ПараметрыГод.Статья9 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья9);
			
		Иначе
			
			ПараметрыГод.Статья2 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья2)
			                       + ПараметрыГод.Статья2;
			ПараметрыГод.Статья3 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья3)
			                       + ПараметрыГод.Статья3;
			ПараметрыГод.Статья4 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья4)
			                       + ПараметрыГод.Статья4;
			ПараметрыГод.Статья5 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья5)
			                       + ПараметрыГод.Статья5;
			ПараметрыГод.Статья6 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья6)
			                       + ПараметрыГод.Статья6;
			ПараметрыГод.Статья7 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья7)
			                       + ПараметрыГод.Статья7;
			ПараметрыГод.Статья9 = ОбщегоНазначения.ПреобразоватьВЧисло(ВыборкаМесяц.Статья9)
			                       + ПараметрыГод.Статья9;
			
		КонецЕсли;
		
		
	КонецЦикла;
	
	ТабДокумент.ЗакончитьГруппуСтрок();//Закончить группу "Квартал"
	ТабДокумент.Вывести(ОбластьКвартал);
	ТабДокумент.ЗакончитьГруппуСтрок();//Закончить группу "Год"
	ТабДокумент.Вывести(ОбластьГод);
	
	ТабДокумент.Вывести(ОбластьПодвал);
	
	ТабДокумент.ИмяПараметровПечати = "КнигаДоходовРасходовПоЕдиномуНалогу";
	
КонецПроцедуры
