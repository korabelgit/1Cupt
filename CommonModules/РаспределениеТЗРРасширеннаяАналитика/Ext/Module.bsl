
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ РАСПРЕДЕЛЕНИЯ ТЗР В РЕЖИМЕ РАУЗ

// Процедура распределения всех транспортно-заготовительных расходов 
// 
// Параметры:
//	СтруктураШапкиДокумента - Структура - Реквизиты документа
//
Функция РаспределитьТЗР(
	СтруктураШапкиДокумента
	) Экспорт
	
	БылоРаспределениеТЗР = Ложь;
	
	СтруктураИменРегистров = Новый Структура;
	Если СтруктураШапкиДокумента.ОтражатьВУправленческомУчете Тогда
		СтруктураИменРегистров.Вставить("УчетЗатрат", "УчетЗатрат");
	Иначе
		СтруктураИменРегистров.Вставить("УчетЗатрат", "УчетЗатратРегл");
	КонецЕсли;
	
	СтруктураНаборыЗаписей = РасширеннаяАналитикаУчета.ПолучитьНаборыЗаписейРегистров(
		СтруктураШапкиДокумента,
		СтруктураИменРегистров
	);
	
	Если СтруктураШапкиДокумента.ОтражатьВУправленческомУчете Тогда
		РаспределитьТЗРПоВиду(СтруктураШапкиДокумента, СтруктураНаборыЗаписей);
	ИначеЕсли СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		РаспределитьТЗРПоВиду(СтруктураШапкиДокумента, СтруктураНаборыЗаписей, "Материалы");
		РаспределитьТЗРПоВиду(СтруктураШапкиДокумента, СтруктураНаборыЗаписей, "Товары");
	КонецЕсли; 
	
	Если СтруктураНаборыЗаписей.УчетЗатрат.Модифицированность() Тогда
		СтруктураНаборыЗаписей.УчетЗатрат.Записать(Ложь);
		БылоРаспределениеТЗР = Истина;
	КонецЕсли;
	
	Возврат БылоРаспределениеТЗР;
	
КонецФункции // РаспределитьТЗР

// Процедура распределения транспортно-заготовительных расходов 
//
Процедура РаспределитьТЗРПоВиду(
	СтруктураШапкиДокумента, 
	СтруктураНаборыЗаписей, 
	ВидТЗР = ""
	)
	
	ТекстЗапросаСКомментариями = "
	|////////////////////////////////////////////////////////////////////////////////
	|// 0 - Отбор по аналитике учета затрат по характеру затрат ТЗР
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|
	|	РегистрАналитикаУчетаЗатрат.Ссылка
	|
	|ПОМЕСТИТЬ ОтборАналитикаУчетаЗатратТЗР
	|
	|ИЗ
	|	РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|
	|ГДЕ
	|	РегистрАналитикаУчетаЗатрат.ХарактерЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.ТранспортноЗаготовительныеРасходы)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// 1 - Конечные остатки ТЗР. Для регл. учета в зависимости от вида ТЗР отбираем по счету учета
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|
	|	УчетЗатратОстаткиТЗР.АналитикаВидаУчета,
	|	УчетЗатратОстаткиТЗР.АналитикаУчетаЗатрат,
	|	УчетЗатратОстаткиТЗР.АналитикаУчетаПартий,
	|	УчетЗатратОстаткиТЗР.АналитикаРаспределенияЗатрат,
	|
	|   РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|
	|	//ДляРеглУчета УчетЗатратОстаткиТЗР.СтоимостьНУОстаток  КАК СтоимостьНУ,
	|	//ДляРеглУчета УчетЗатратОстаткиТЗР.НДСВходящийОстаток  КАК НДСВходящий,
	|	//ДляРеглУчета УчетЗатратОстаткиТЗР.НДСКредитОстаток    КАК НДСКредит,
	|
	|	УчетЗатратОстаткиТЗР.СтоимостьОстаток КАК Стоимость
	|
	|ПОМЕСТИТЬ КонечныеОстаткиТЗР
	|
	|ИЗ
	|	РегистрНакопления.УчетЗатрат%СуффиксРегл%.Остатки(
	|			&КонГраница,
	|			АналитикаУчетаЗатрат В
	|				(ВЫБРАТЬ
	|					ОтборАналитикаУчетаЗатратТЗР.Ссылка
	|				ИЗ
	|					ОтборАналитикаУчетаЗатратТЗР КАК ОтборАналитикаУчетаЗатратТЗР)
	|   ) КАК УчетЗатратОстаткиТЗР
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|	ПО 
	|		УчетЗатратОстаткиТЗР.АналитикаРаспределенияЗатрат = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|
	|//ДляРеглУчета 	ЛЕВОЕ СОЕДИНЕНИЕ
	|//ДляРеглУчета 		РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|//ДляРеглУчета 	ПО 
	|//ДляРеглУчета 		УчетЗатратОстаткиТЗР.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|
	|//ДляРеглУчета ГДЕ 
	|//ДляРеглУчета 	РегистрАналитикаВидаУчета.Организация = &Организация
	|//ДляРеглУчета 	И РегистрАналитикаВидаУчета.СчетУчета В (&МассивСчетовЗатратТЗР)
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|// 2 - Номенклатурные группы, которые есть в остатках ТЗР
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|   КонечныеОстаткиТЗР.НоменклатурнаяГруппа
	|
	|ПОМЕСТИТЬ НоменклатурныеГруппыОстатковТЗР
	|
	|ИЗ
	|  КонечныеОстаткиТЗР КАК КонечныеОстаткиТЗР
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|// 3 - Отбор по аналитике учета затрат, отбираем только по тем номенклатурным группам, которые есть в остатках ТЗР
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РегистрАналитикаУчетаЗатрат.Ссылка
	|
	|ПОМЕСТИТЬ ОтборАналитикаУчетаЗатратМПЗ
	|
	|ИЗ
	|	РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|
	|ГДЕ
	|	РегистрАналитикаУчетаЗатрат.Затрата.НоменклатурнаяГруппа В (ВЫБРАТЬ НоменклатурныеГруппыОстатковТЗР.НоменклатурнаяГруппа ИЗ НоменклатурныеГруппыОстатковТЗР КАК НоменклатурныеГруппыОстатковТЗР)
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|// 4 - Отбор по аналитике вида учета МПЗ
	|//     Для регл. учета в зависимости от вида ТЗР отбираем по счету учета МПЗ
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РегистрАналитикаВидаУчета.Ссылка
	|
	|ПОМЕСТИТЬ ОтборАналитикаВидаУчетаМПЗ
	|
	|ИЗ
	|	РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|
	|ГДЕ
	|	РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.МПЗ)
	|	//ДляУпрУчета  И РегистрАналитикаВидаУчета.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|   //ДляРеглУчета И РегистрАналитикаВидаУчета.СчетУчета В (&МассивСчетовЗапасов)
	|	//ДляУпрУчета  И РегистрАналитикаВидаУчета.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)
	|	//ДляРеглУчета И РегистрАналитикаВидаУчета.Организация = &Организация
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|// 5 - Начальные остатки МПЗ
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|
	|   РегистрАналитикаУчетаЗатрат.Затрата.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|
	|	//ДляРеглУчета СУММА(УчетЗатратОстатки.СтоимостьНУОстаток)  КАК СтоимостьНУ,
	|	//ДляРеглУчета СУММА(УчетЗатратОстатки.НДСВходящийОстаток)  КАК НДСВходящий,
	|	//ДляРеглУчета СУММА(УчетЗатратОстатки.НДСКредитОстаток)    КАК НДСКредит,
	|
	|	СУММА(УчетЗатратОстатки.СтоимостьОстаток) КАК Стоимость
	|
	|ПОМЕСТИТЬ НачальныеОстаткиМПЗ
	|
	|ИЗ
	|	РегистрНакопления.УчетЗатрат%СуффиксРегл%.Остатки(
	|			&НачГраница,
	|			АналитикаВидаУчета В
	|				(ВЫБРАТЬ
	|					ОтборАналитикаВидаУчетаМПЗ.Ссылка
	|				ИЗ
	|					ОтборАналитикаВидаУчетаМПЗ КАК ОтборАналитикаВидаУчетаМПЗ)
	|			И АналитикаУчетаЗатрат В
	|				(ВЫБРАТЬ
	|					ОтборАналитикаУчетаЗатратМПЗ.Ссылка
	|				ИЗ
	|					ОтборАналитикаУчетаЗатратМПЗ КАК ОтборАналитикаУчетаЗатратМПЗ)
	|          ) КАК УчетЗатратОстатки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|	ПО 
	|		УчетЗатратОстатки.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.Ссылка
	|
	|//ДляРеглУчета 	ЛЕВОЕ СОЕДИНЕНИЕ
	|//ДляРеглУчета 		РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|//ДляРеглУчета 	ПО 
	|//ДляРеглУчета 		УчетЗатратОстатки.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|
	|   РегистрАналитикаУчетаЗатрат.Затрата.НоменклатурнаяГруппа
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|// 6 - Приход МПЗ - только внешний приход
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|
	|   РегистрАналитикаУчетаЗатрат.Затрата.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|
	|	//ДляРеглУчета СУММА(УчетЗатрат.СтоимостьНУ) КАК СтоимостьНУ,
	|	//ДляРеглУчета СУММА(УчетЗатрат.НДСВходящий) КАК НДСВходящий,
	|	//ДляРеглУчета СУММА(УчетЗатрат.НДСКредит) КАК НДСКредит,
	|
	|	СУММА(УчетЗатрат.Стоимость) КАК Стоимость
	|
	|ПОМЕСТИТЬ ПриходМПЗ
	|
	|ИЗ
	|	РегистрНакопления.УчетЗатрат%СуффиксРегл% КАК УчетЗатрат
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|	ПО 
	|		УчетЗатрат.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.Ссылка
	|
	|//ДляРеглУчета 	ЛЕВОЕ СОЕДИНЕНИЕ
	|//ДляРеглУчета 		РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|//ДляРеглУчета 	ПО 
	|//ДляРеглУчета 		УчетЗатрат.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|
	|ГДЕ
	|	УчетЗатрат.Период МЕЖДУ &НачДата И &КонДата
	|	И УчетЗатрат.Активность
	|	И УчетЗатрат.КорАналитикаВидаУчета = Неопределено
	|	И УчетЗатрат.ВидДвижения = &ВидДвиженияПриход
	|	И УчетЗатрат.АналитикаВидаУчета В (
	|		ВЫБРАТЬ
	|			Ссылка
	|		ИЗ
	|			ОтборАналитикаВидаУчетаМПЗ
	|		)
	|	И УчетЗатрат.АналитикаУчетаЗатрат В (
	|		ВЫБРАТЬ
	|			Ссылка
	|		ИЗ
	|			ОтборАналитикаУчетаЗатратМПЗ
	|		)
	|
	|СГРУППИРОВАТЬ ПО
	|   РегистрАналитикаУчетаЗатрат.Затрата.НоменклатурнаяГруппа
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|// 7 - Использование МПЗ
	|// Получаем движения из раздела МПЗ в другие разделы (за исключением МПЗ, перемещения не обрабатываем)
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|
	|	УчетЗатрат.АналитикаВидаУчета,
	|	УчетЗатрат.АналитикаУчетаЗатрат,
	|	УчетЗатрат.АналитикаУчетаПартий,
	|	УчетЗатрат.АналитикаРаспределенияЗатрат,
	|
	|	УчетЗатрат.КорАналитикаВидаУчета,
	|	УчетЗатрат.КорАналитикаУчетаЗатрат,
	|	УчетЗатрат.КорАналитикаУчетаПартий,
	|	УчетЗатрат.КорАналитикаРаспределенияЗатрат,
	|
	|	УчетЗатрат.КодОперации,
	|
	|   РегистрАналитикаУчетаЗатрат.Затрата.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|
	|	//ДляРеглУчета СУММА(УчетЗатрат.СтоимостьНУ) КАК СтоимостьНУ,
	|	//ДляРеглУчета СУММА(УчетЗатрат.НДСВходящий) КАК НДСВходящий,
	|	//ДляРеглУчета СУММА(УчетЗатрат.НДСКредит) КАК НДСКредит,
	|
	|	СУММА(УчетЗатрат.Стоимость) КАК Стоимость
	|
	|ПОМЕСТИТЬ ИспользованиеМПЗ
	|
	|ИЗ
	|	РегистрНакопления.УчетЗатрат%СуффиксРегл% КАК УчетЗатрат
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|	ПО 
	|		УчетЗатрат.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаВидаУчета КАК РегистрКорАналитикаВидаУчета
	|	ПО 
	|		УчетЗатрат.КорАналитикаВидаУчета = РегистрКорАналитикаВидаУчета.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|	ПО 
	|		УчетЗатрат.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.Ссылка
	|
	|ГДЕ
	|	УчетЗатрат.Период МЕЖДУ &НачДата И &КонДата
	|	И УчетЗатрат.Активность
	|	И УчетЗатрат.ВидДвижения = &ВидДвиженияРасход
	|	И РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.МПЗ)
	|	И ЕСТЬNULL(РегистрКорАналитикаВидаУчета.РазделУчета, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.РазделыУчета.МПЗ)
	|	И УчетЗатрат.АналитикаВидаУчета В (
	|		ВЫБРАТЬ
	|			Ссылка
	|		ИЗ
	|			ОтборАналитикаВидаУчетаМПЗ
	|		)
	|	И УчетЗатрат.АналитикаУчетаЗатрат В (
	|		ВЫБРАТЬ
	|			Ссылка
	|		ИЗ
	|			ОтборАналитикаУчетаЗатратМПЗ
	|		)
	|
	|СГРУППИРОВАТЬ ПО
	|	УчетЗатрат.АналитикаВидаУчета,
	|	УчетЗатрат.АналитикаУчетаЗатрат,
	|	УчетЗатрат.АналитикаУчетаПартий,
	|	УчетЗатрат.АналитикаРаспределенияЗатрат,
	|
	|	УчетЗатрат.КорАналитикаВидаУчета,
	|	УчетЗатрат.КорАналитикаУчетаЗатрат,
	|	УчетЗатрат.КорАналитикаУчетаПартий,
	|	УчетЗатрат.КорАналитикаРаспределенияЗатрат,
	|
	|	УчетЗатрат.КодОперации,
	|
	|   РегистрАналитикаУчетаЗатрат.Затрата.НоменклатурнаяГруппа
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|// 8 - Использование МПЗ в разрезе номенклатурных групп
	|//     Для БУ + в разрезе налоговых назначений
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|
	|   ИспользованиеМПЗ.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|
	|	//ДляРеглУчета СУММА(ИспользованиеМПЗ.СтоимостьНУ) КАК СтоимостьНУ,
	|	//ДляРеглУчета СУММА(ИспользованиеМПЗ.НДСВходящий) КАК НДСВходящий,
	|	//ДляРеглУчета СУММА(ИспользованиеМПЗ.НДСКредит) КАК НДСКредит,
	|
	|	СУММА(ИспользованиеМПЗ.Стоимость) КАК Стоимость
	|
	|ПОМЕСТИТЬ ИспользованиеМПЗПоНоменклатурнымГруппам
	|
	|ИЗ
	|	ИспользованиеМПЗ КАК ИспользованиеМПЗ
	|
	|СГРУППИРОВАТЬ ПО
	|   ИспользованиеМПЗ.НоменклатурнаяГруппа
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|// 9 - Конечные остатки ТЗР в разрезе номенклатурных групп
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|
	|   КонечныеОстаткиТЗР.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|
	|	//ДляРеглУчета СУММА(КонечныеОстаткиТЗР.СтоимостьНУ) КАК СтоимостьНУ,
	|	//ДляРеглУчета СУММА(КонечныеОстаткиТЗР.НДСВходящий) КАК НДСВходящий,
	|	//ДляРеглУчета СУММА(КонечныеОстаткиТЗР.НДСКредит) КАК НДСКредит,
	|
	|	СУММА(КонечныеОстаткиТЗР.Стоимость) КАК Стоимость
	|
	|ПОМЕСТИТЬ КонечныеОстаткиТЗРПоНоменклатурнымГруппам
	|
	|ИЗ
	|	КонечныеОстаткиТЗР КАК КонечныеОстаткиТЗР
	|
	|СГРУППИРОВАТЬ ПО
	|   КонечныеОстаткиТЗР.НоменклатурнаяГруппа
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|// 10 - Данные для расчета коэффициентов ТЗР в разрезе номенклатурных групп
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|
	|   КонечныеОстаткиТЗРПоНоменклатурнымГруппам.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|
	|	//ДляРеглУчета СУММА(КонечныеОстаткиТЗРПоНоменклатурнымГруппам.СтоимостьНУ) КАК СтоимостьНУТЗР,
	|	//ДляРеглУчета СУММА(КонечныеОстаткиТЗРПоНоменклатурнымГруппам.НДСВходящий) КАК НДСВходящийТЗР,
	|	//ДляРеглУчета СУММА(КонечныеОстаткиТЗРПоНоменклатурнымГруппам.НДСКредит) КАК НДСКредитТЗР,
	|
	|	//ДляРеглУчета СУММА(ЕСТЬNULL(НачальныеОстаткиМПЗ.СтоимостьНУ, 0)) КАК СтоимостьНУНачальныеОстатки,
	|	//ДляРеглУчета СУММА(ЕСТЬNULL(НачальныеОстаткиМПЗ.НДСВходящий, 0)) КАК НДСВходящийНачальныеОстатки,
	|	//ДляРеглУчета СУММА(ЕСТЬNULL(НачальныеОстаткиМПЗ.НДСКредит, 0)) КАК НДСКредитНачальныеОстатки,
	|
	|	//ДляРеглУчета СУММА(ЕСТЬNULL(ПриходМПЗ.СтоимостьНУ, 0)) КАК СтоимостьНУПриход,
	|	//ДляРеглУчета СУММА(ЕСТЬNULL(ПриходМПЗ.НДСВходящий, 0)) КАК НДСВходящийПриход,
	|	//ДляРеглУчета СУММА(ЕСТЬNULL(ПриходМПЗ.НДСКредит, 0)) КАК НДСКредитПриход,
	|
	|	//ДляРеглУчета СУММА(ЕСТЬNULL(ИспользованиеМПЗПоНоменклатурнымГруппам.СтоимостьНУ, 0)) КАК СтоимостьНУИспользование,
	|	//ДляРеглУчета СУММА(ЕСТЬNULL(ИспользованиеМПЗПоНоменклатурнымГруппам.НДСВходящий, 0)) КАК НДСВходящийИспользование,
	|	//ДляРеглУчета СУММА(ЕСТЬNULL(ИспользованиеМПЗПоНоменклатурнымГруппам.НДСКредит, 0)) КАК НДСКредитИспользование,
	|
	|	СУММА(КонечныеОстаткиТЗРПоНоменклатурнымГруппам.Стоимость) КАК СтоимостьТЗР,
	|	СУММА(ЕСТЬNULL(НачальныеОстаткиМПЗ.Стоимость, 0)) КАК СтоимостьНачальныеОстатки,
	|	СУММА(ЕСТЬNULL(ПриходМПЗ.Стоимость, 0)) КАК СтоимостьПриход,
	|	СУММА(ЕСТЬNULL(ИспользованиеМПЗПоНоменклатурнымГруппам.Стоимость, 0)) КАК СтоимостьИспользование
	|
	// |ПОМЕСТИТЬ ДанныеДляРасчетаКоэффициентовТЗР
	|
	|ИЗ
	|	КонечныеОстаткиТЗРПоНоменклатурнымГруппам КАК КонечныеОстаткиТЗРПоНоменклатурнымГруппам
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		НачальныеОстаткиМПЗ КАК НачальныеОстаткиМПЗ
	|	ПО 
	|		КонечныеОстаткиТЗРПоНоменклатурнымГруппам.НоменклатурнаяГруппа = НачальныеОстаткиМПЗ.НоменклатурнаяГруппа
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПриходМПЗ КАК ПриходМПЗ
	|	ПО 
	|		КонечныеОстаткиТЗРПоНоменклатурнымГруппам.НоменклатурнаяГруппа = ПриходМПЗ.НоменклатурнаяГруппа
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ИспользованиеМПЗПоНоменклатурнымГруппам КАК ИспользованиеМПЗПоНоменклатурнымГруппам
	|	ПО 
	|		КонечныеОстаткиТЗРПоНоменклатурнымГруппам.НоменклатурнаяГруппа = ИспользованиеМПЗПоНоменклатурнымГруппам.НоменклатурнаяГруппа
	|
	|СГРУППИРОВАТЬ ПО
	|   КонечныеОстаткиТЗРПоНоменклатурнымГруппам.НоменклатурнаяГруппа
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|// 11 - Конечные остатки ТЗР (выбираем уже из готовой таблицы)
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|
	|	КонечныеОстаткиТЗР.АналитикаВидаУчета,
	|	КонечныеОстаткиТЗР.АналитикаУчетаЗатрат,
	|	КонечныеОстаткиТЗР.АналитикаУчетаПартий,
	|	КонечныеОстаткиТЗР.АналитикаРаспределенияЗатрат,
	|
	|   КонечныеОстаткиТЗР.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|
	|	//ДляРеглУчета КонечныеОстаткиТЗР.СтоимостьНУ  КАК СтоимостьНУ,
	|	//ДляРеглУчета КонечныеОстаткиТЗР.НДСВходящий  КАК НДСВходящий,
	|	//ДляРеглУчета КонечныеОстаткиТЗР.НДСКредит    КАК НДСКредит,
	|
	|	КонечныеОстаткиТЗР.Стоимость КАК Стоимость
	|
	|ИЗ
	|	КонечныеОстаткиТЗР КАК КонечныеОстаткиТЗР
	|
	|УПОРЯДОЧИТЬ ПО
	|
	|   КонечныеОстаткиТЗР.НоменклатурнаяГруппа,
	|
	|	КонечныеОстаткиТЗР.АналитикаВидаУчета,
	|	КонечныеОстаткиТЗР.АналитикаУчетаЗатрат,
	|	КонечныеОстаткиТЗР.АналитикаУчетаПартий,
	|	КонечныеОстаткиТЗР.АналитикаРаспределенияЗатрат
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|// 12 - Использование МПЗ (выбираем уже из готовой таблицы, с итогами по НоменклатурнаяГруппа)
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|
	|	ИспользованиеМПЗ.АналитикаВидаУчета,
	|	ИспользованиеМПЗ.АналитикаУчетаЗатрат,
	|	ИспользованиеМПЗ.АналитикаУчетаПартий,
	|	ИспользованиеМПЗ.АналитикаРаспределенияЗатрат,
	|
	|	ИспользованиеМПЗ.КорАналитикаВидаУчета,
	|	ИспользованиеМПЗ.КорАналитикаУчетаЗатрат,
	|	ИспользованиеМПЗ.КорАналитикаУчетаПартий,
	|	ИспользованиеМПЗ.КорАналитикаРаспределенияЗатрат,
	|
	|	ИспользованиеМПЗ.КодОперации,
	|
	|   ИспользованиеМПЗ.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|
	|	//ДляРеглУчета ИспользованиеМПЗ.СтоимостьНУ КАК СтоимостьНУ,
	|	//ДляРеглУчета ИспользованиеМПЗ.НДСВходящий КАК НДСВходящий,
	|	//ДляРеглУчета ИспользованиеМПЗ.НДСКредит   КАК НДСКредит,
	|
	|	ИспользованиеМПЗ.Стоимость КАК Стоимость
	|
	|ИЗ
	|	ИспользованиеМПЗ КАК ИспользованиеМПЗ
	|
	|УПОРЯДОЧИТЬ ПО
	|
	|   ИспользованиеМПЗ.НоменклатурнаяГруппа,
	|
	|	ИспользованиеМПЗ.АналитикаВидаУчета,
	|	ИспользованиеМПЗ.АналитикаУчетаЗатрат,
	|	ИспользованиеМПЗ.АналитикаУчетаПартий,
	|	ИспользованиеМПЗ.АналитикаРаспределенияЗатрат,
	|
	|	ИспользованиеМПЗ.КорАналитикаВидаУчета,
	|	ИспользованиеМПЗ.КорАналитикаУчетаЗатрат,
	|	ИспользованиеМПЗ.КорАналитикаУчетаПартий,
	|	ИспользованиеМПЗ.КорАналитикаРаспределенияЗатрат
	|
	|ИТОГИ 
	|
	|	//ДляРеглУчета СУММА(СтоимостьНУ),
	|	//ДляРеглУчета СУММА(НДСВходящий),
	|	//ДляРеглУчета СУММА(НДСКредит),
	|
	|	СУММА(Стоимость)
	|
	|ПО
	|   НоменклатурнаяГруппа
	|";
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
		ТекстЗапросаСКомментариями, 
		СтруктураШапкиДокумента.ВидОтраженияВУчете
	);
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("НачДата",     СтруктураШапкиДокумента.мНачДата);
	Запрос.УстановитьПараметр("КонДата",     СтруктураШапкиДокумента.мКонДата);
	Запрос.УстановитьПараметр("НачГраница",  СтруктураШапкиДокумента.мНачГраница);
	Запрос.УстановитьПараметр("КонГраница",  СтруктураШапкиДокумента.мКонГраница);
	
	Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", ВидДвиженияНакопления.Расход);
	
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		Если ВидТЗР = "Материалы" Тогда
			МассивСчетовЗатратТЗР = ПроцедурыРасчетаСебестоимостиВыпуска.СформироватьМассивСчетовВИерархии(ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыМатериалы);
			МассивСчетовЗапасов   = ПроцедурыРасчетаСебестоимостиВыпуска.СформироватьМассивСчетовВИерархии(ПланыСчетов.Хозрасчетный.ПроизводственныеЗапасы);
		Иначе
			МассивСчетовЗатратТЗР = ПроцедурыРасчетаСебестоимостиВыпуска.СформироватьМассивСчетовВИерархии(ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыТовары);
			МассивСчетовЗапасов   = ПроцедурыРасчетаСебестоимостиВыпуска.СформироватьМассивСчетовВИерархии(ПланыСчетов.Хозрасчетный.Товары);
		КонецЕсли; 
		Запрос.УстановитьПараметр("МассивСчетовЗатратТЗР", МассивСчетовЗатратТЗР);
		Запрос.УстановитьПараметр("МассивСчетовЗапасов"  , МассивСчетовЗапасов);
	КонецЕсли;	
	
	Результат = Запрос.ВыполнитьПакет();
	
	ПоказатьВременныеТаблицыРаспределенияТЗР(СтруктураШапкиДокумента, Запрос.МенеджерВременныхТаблиц);
	
	// выгрузка таблицы остатков ТЗР
	ТаблицаОстатковТЗР = Результат[11].Выгрузить();
	
	ТаблицаКоэффициентовТЗР = Результат[10].Выгрузить();
	
	ТаблицаКоэффициентовТЗР.Колонки.Добавить("КоэффициентТЗРСтоимость", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(23, 15)));
	ТаблицаКоэффициентовТЗР.Колонки.Добавить("РаспределениеТЗРСтоимость", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
	  	ТаблицаКоэффициентовТЗР.Колонки.Добавить("РаспределениеТЗРСтоимостьНУ", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	  	ТаблицаКоэффициентовТЗР.Колонки.Добавить("РаспределениеТЗРНДСВходящий", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	 	ТаблицаКоэффициентовТЗР.Колонки.Добавить("РаспределениеТЗРНДСКредит", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	КонецЕсли; 
	
	// расчет коэффициентов ТЗР
	Для Каждого СтрокаКоэффициентовТЗР Из ТаблицаКоэффициентовТЗР Цикл
		
		КоэффициентТЗРСтоимость = ?(СтрокаКоэффициентовТЗР.СтоимостьПриход + СтрокаКоэффициентовТЗР.СтоимостьНачальныеОстатки = 0, 
									0, 
									СтрокаКоэффициентовТЗР.СтоимостьИспользование / (СтрокаКоэффициентовТЗР.СтоимостьПриход + СтрокаКоэффициентовТЗР.СтоимостьНачальныеОстатки));
									
									
		РаспределениеТЗРСтоимость = ОКР(СтрокаКоэффициентовТЗР.СтоимостьТЗР * КоэффициентТЗРСтоимость, 2);
		
		СтрокаКоэффициентовТЗР.КоэффициентТЗРСтоимость   = КоэффициентТЗРСтоимость;
		СтрокаКоэффициентовТЗР.РаспределениеТЗРСтоимость = РаспределениеТЗРСтоимость;
		
		Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
			
										
										
			РаспределениеТЗРСтоимостьНУ = ОКР(СтрокаКоэффициентовТЗР.СтоимостьНУТЗР * КоэффициентТЗРСтоимость, 2);
			
			СтрокаКоэффициентовТЗР.РаспределениеТЗРСтоимостьНУ = РаспределениеТЗРСтоимостьНУ;
			
										
										
			РаспределениеТЗРНДСВходящий = ОКР(СтрокаКоэффициентовТЗР.НДСВходящийТЗР * КоэффициентТЗРСтоимость, 2);
			
			СтрокаКоэффициентовТЗР.РаспределениеТЗРНДСВходящий = РаспределениеТЗРНДСВходящий;
			
										
			РаспределениеТЗРНДСКредит = ОКР(СтрокаКоэффициентовТЗР.НДСКредитТЗР * КоэффициентТЗРСтоимость, 2);
			
			СтрокаКоэффициентовТЗР.РаспределениеТЗРНДСКредит = РаспределениеТЗРНДСКредит;
			
		КонецЕсли;
		
		// в таблице остатков ТЗР умножим все остатки по текущей ном. группе на коэфициент распределения
		СтруктураПоиска = Новый Структура("НоменклатурнаяГруппа", СтрокаКоэффициентовТЗР.НоменклатурнаяГруппа);
		МассивСтрок = ТаблицаОстатковТЗР.НайтиСтроки(СтруктураПоиска);
		Для Каждого СтрокаОстатковТЗР Из МассивСтрок Цикл
			СтрокаОстатковТЗР.Стоимость = ОКР(СтрокаОстатковТЗР.Стоимость * СтрокаКоэффициентовТЗР.КоэффициентТЗРСтоимость, 2);
			Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
				СтрокаОстатковТЗР.СтоимостьНУ = ОКР(СтрокаОстатковТЗР.СтоимостьНУ * СтрокаКоэффициентовТЗР.КоэффициентТЗРСтоимость, 2);
				СтрокаОстатковТЗР.НДСВходящий = ОКР(СтрокаОстатковТЗР.НДСВходящий * СтрокаКоэффициентовТЗР.КоэффициентТЗРСтоимость, 2);
				СтрокаОстатковТЗР.НДСКредит   = ОКР(СтрокаОстатковТЗР.НДСКредит   * СтрокаКоэффициентовТЗР.КоэффициентТЗРСтоимость, 2);
			КонецЕсли;	
		КонецЦикла; 
		
	КонецЦикла; 
	
	
	// выгрузка таблицы остатков ТЗР
	ТаблицаОстатковТЗР = Результат[11].Выгрузить();
	
	// досписание по направлениям использования и списание остатков ТЗР
	ВыборкаПоНГ = Результат[12].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоНГ.Следующий() Цикл
		
		
		
		// найдем сумму распределения ТЗР 
		СтруктураПоиска = Новый Структура("НоменклатурнаяГруппа", ВыборкаПоНГ.НоменклатурнаяГруппа);
		
		ТЗРСтоимость   = 0;
		ТЗРСтоимостьНУ = 0;
		ТЗРНДСВходящий = 0;
		ТЗРНДСКредит   = 0;
		
		МассивСтрок = ТаблицаКоэффициентовТЗР.НайтиСтроки(СтруктураПоиска);
		Если МассивСтрок.Количество() > 0 Тогда
			ТЗРСтоимость = МассивСтрок[0].РаспределениеТЗРСтоимость;
			Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
				ТЗРСтоимостьНУ = МассивСтрок[0].РаспределениеТЗРСтоимостьНУ;
				ТЗРНДСВходящий = МассивСтрок[0].РаспределениеТЗРНДСВходящий;
				ТЗРНДСКредит   = МассивСтрок[0].РаспределениеТЗРНДСКредит;
			КонецЕсли;	
		КонецЕсли; 
		
		Если ТЗРСтоимость = 0 И ТЗРСтоимостьНУ = 0 И ТЗРНДСВходящий = 0 И ТЗРНДСКредит = 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		ВсегоСтоимостьИспользование      = ВыборкаПоНГ.Стоимость;
		
		ВсегоСтоимостьНУИспользование    = 0;
		ВсегоНДСВходящийИспользование    = 0;
		ВсегоНДСКредитИспользование      = 0;
		
		Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
			ВсегоСтоимостьНУИспользование    = ВсегоСтоимостьИспользование;
			ВсегоНДСВходящийИспользование    = ВсегоСтоимостьИспользование;
			ВсегоНДСКредитИспользование      = ВсегоСтоимостьИспользование;
		КонецЕсли;		
		
		Если ВсегоСтоимостьИспользование = 0 И ВсегоСтоимостьНУИспользование = 0 И ВсегоНДСВходящийИспользование = 0 И ВсегоНДСКредитИспользование = 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		ОсталосьСтоимостьИспользование   = ВсегоСтоимостьИспользование;
		ОсталосьСтоимостьНУИспользование = ВсегоСтоимостьНУИспользование;
		ОсталосьНДСВходящийИспользование = ВсегоНДСВходящийИспользование;
		ОсталосьНДСКредитИспользование   = ВсегоНДСКредитИспользование;
		
		ОсталосьДосписатьТЗРСтоимость   = ТЗРСтоимость;
		ОсталосьДосписатьТЗРСтоимостьНУ = ТЗРСтоимостьНУ;
		ОсталосьДосписатьТЗРНДСВходящий = ТЗРНДСВходящий;
		ОсталосьДосписатьТЗРНДСКредит   = ТЗРНДСКредит;
		
		Выборка = ВыборкаПоНГ.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ДосписатьСтоимость = ОпределитьСтоимостьДосписания(
												Выборка.Стоимость, 
												ВсегоСтоимостьИспользование, 
												ОсталосьСтоимостьИспользование, 
												ТЗРСтоимость, 
												ОсталосьДосписатьТЗРСтоимость
											  );
			
			ДосписатьСтоимостьНУ = 0;
			ДосписатьНДСВходящий = 0;
			ДосписатьНДСКредит   = 0;
			
			Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
				ДосписатьСтоимостьНУ = ОпределитьСтоимостьДосписания(
													Выборка.Стоимость, 
													ВсегоСтоимостьНУИспользование, 
													ОсталосьСтоимостьНУИспользование, 
													ТЗРСтоимостьНУ, 
													ОсталосьДосписатьТЗРСтоимостьНУ
												  );
												  
				ДосписатьНДСВходящий = ОпределитьСтоимостьДосписания(
													Выборка.Стоимость, 
													ВсегоНДСВходящийИспользование, 
													ОсталосьНДСВходящийИспользование, 
													ТЗРНДСВходящий, 
													ОсталосьДосписатьТЗРНДСВходящий
												  );
												  
				ДосписатьНДСКредит = ОпределитьСтоимостьДосписания(
													Выборка.Стоимость, 
													ВсегоНДСКредитИспользование, 
													ОсталосьНДСКредитИспользование, 
													ТЗРНДСКредит, 
													ОсталосьДосписатьТЗРНДСКредит
												  );
												  
			КонецЕсли;	
			
			МассивСтруктурАналитикиТЗР = НайтиАналитикуТЗР(
				СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете,
				ТаблицаОстатковТЗР,
				Выборка.НоменклатурнаяГруппа,
				ДосписатьСтоимость,
				ДосписатьСтоимостьНУ,
				ДосписатьНДСВходящий,
				ДосписатьНДСКредит
				);
				
			Для Каждого СтруктураАналитикаТЗР Из МассивСтруктурАналитикиТЗР Цикл
			
				// Движения "Расход" по аналитике учета ТЗР, кор. аналитика - по направлению использования
				РасширеннаяАналитикаУчета.СформироватьДвиженияПоРегиструУчетЗатрат(
					СтруктураШапкиДокумента,
					
					СтруктураАналитикаТЗР.АналитикаВидаУчета,
					СтруктураАналитикаТЗР.АналитикаУчетаЗатрат, 
					СтруктураАналитикаТЗР.АналитикаУчетаПартий,
					СтруктураАналитикаТЗР.АналитикаРаспределенияЗатрат,
					
					Выборка.КорАналитикаВидаУчета,
					Выборка.КорАналитикаУчетаЗатрат, 
					Выборка.КорАналитикаУчетаПартий,
					Выборка.КорАналитикаРаспределенияЗатрат,
					
					ВидДвиженияНакопления.Расход,
					
					Выборка.КодОперации, // КодОперации
					
					0, // Количество
					СтруктураАналитикаТЗР.Стоимость,
					0, // КоличествоНУ
					СтруктураАналитикаТЗР.СтоимостьНУ,
					СтруктураАналитикаТЗР.НДСВходящий,
					СтруктураАналитикаТЗР.НДСКредит,
					СтруктураНаборыЗаписей.УчетЗатрат
				);
				
				
				// Движения "Приход" по аналитике - направлению использования, кор. аналитика - ТЗР
				РасширеннаяАналитикаУчета.СформироватьДвиженияПоРегиструУчетЗатрат(
					СтруктураШапкиДокумента,
					
					Выборка.КорАналитикаВидаУчета,
					Выборка.КорАналитикаУчетаЗатрат, 
					Выборка.КорАналитикаУчетаПартий,
					Выборка.КорАналитикаРаспределенияЗатрат,
					
					СтруктураАналитикаТЗР.АналитикаВидаУчета,
					СтруктураАналитикаТЗР.АналитикаУчетаЗатрат, 
					СтруктураАналитикаТЗР.АналитикаУчетаПартий,
					СтруктураАналитикаТЗР.АналитикаРаспределенияЗатрат,
					
					ВидДвиженияНакопления.Приход,
					Перечисления.КодыОперацийЗатраты.СписаниеТранспортноЗаготовительныеРасходы_Регламент, // КодОперации
					0, // Количество
					СтруктураАналитикаТЗР.Стоимость,
					0, // КоличествоНУ
					СтруктураАналитикаТЗР.СтоимостьНУ,
					СтруктураАналитикаТЗР.НДСВходящий,
					СтруктураАналитикаТЗР.НДСКредит,
					СтруктураНаборыЗаписей.УчетЗатрат
				);
				
				Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
					// расчет и движения по корректировкам использования в НУ
					СтруктураПараметров = Новый Структура;
					СтруктураПараметров.Вставить("Ссылка", СтруктураШапкиДокумента.Ссылка);
					БылиКорректировкиНУ = УправлениеЗапасамиРасширеннаяАналитика.СформироватьДвиженияПоКорректировкамИспользованияНУ(
						СтруктураПараметров,
						СтруктураШапкиДокумента.Дата,
						
						СтруктураАналитикаТЗР.АналитикаВидаУчета,
						СтруктураАналитикаТЗР.АналитикаУчетаЗатрат, 
						СтруктураАналитикаТЗР.АналитикаУчетаПартий,
						СтруктураАналитикаТЗР.АналитикаРаспределенияЗатрат,
						
						Выборка.КорАналитикаВидаУчета,
						Выборка.КорАналитикаУчетаЗатрат, 
						Выборка.КорАналитикаУчетаПартий,
						Выборка.КорАналитикаРаспределенияЗатрат,
						
						СтруктураНаборыЗаписей.УчетЗатрат,
						
						СтруктураАналитикаТЗР.Стоимость,
						СтруктураАналитикаТЗР.НДСВходящий,
						СтруктураАналитикаТЗР.НДСКредит,
						, // НалоговоеНазначение
						0 // КоличествоНУ
					);
				КонецЕсли;	
				
				
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры // РаспределитьТЗРПоВиду

Функция НайтиАналитикуТЗР(
	ОтражатьВБУ, 
	ТаблицаОстатковТЗР, 
	НоменклатурнаяГруппа, 
	ДосписатьСтоимость, 
	ДосписатьСтоимостьНУ, 
	ДосписатьНДСВходящий, 
	ДосписатьНДСКредит
	)
	
	// ищем строки с указанной номенклатурной группой
	СтруктураПоиска = Новый Структура("НоменклатурнаяГруппа", НоменклатурнаяГруппа);
	
	МассивСтрок = ТаблицаОстатковТЗР.НайтиСтроки(СтруктураПоиска);
	
	МассивКоэфСтоимость = Новый Массив;
	Если ОтражатьВБУ Тогда
		МассивКоэфСтоимостьНУ = Новый Массив;
		МассивКоэфНДСВходящий = Новый Массив;
		МассивКоэфНДСКредит   = Новый Массив;
	КонецЕсли;	
	Для Каждого СтрокаОстатковТЗР Из МассивСтрок Цикл
		МассивКоэфСтоимость.Добавить(СтрокаОстатковТЗР.Стоимость);
		Если ОтражатьВБУ Тогда
			МассивКоэфСтоимостьНУ.Добавить(СтрокаОстатковТЗР.СтоимостьНУ);
			МассивКоэфНДСВходящий.Добавить(СтрокаОстатковТЗР.НДСВходящий);
			МассивКоэфНДСКредит.Добавить(СтрокаОстатковТЗР.НДСКредит);
		КонецЕсли;	
	КонецЦикла;	
	
	МассивСтоимость = ОбщегоНазначения.РаспределитьПропорционально(ДосписатьСтоимость, МассивКоэфСтоимость, 2, Ложь);
	Если ОтражатьВБУ Тогда
		МассивСтоимостьНУ = ОбщегоНазначения.РаспределитьПропорционально(ДосписатьСтоимостьНУ, МассивКоэфСтоимостьНУ, 2, Ложь);
		МассивНДСВходящий = ОбщегоНазначения.РаспределитьПропорционально(ДосписатьНДСВходящий, МассивКоэфНДСВходящий, 2, Ложь);
		МассивНДСКредит   = ОбщегоНазначения.РаспределитьПропорционально(ДосписатьНДСКредит,   МассивКоэфНДСКредит,   2, Ложь);
	КонецЕсли;	
	
	МассивСтруктурАналитикиТЗР = Новый Массив;
	
	
	Индекс = 0;
	
	Для Каждого СтрокаОстатковТЗР Из МассивСтрок Цикл
		
		СтруктураАналитикиТЗР = Новый Структура;
		
		СтруктураАналитикиТЗР.Вставить("АналитикаВидаУчета", СтрокаОстатковТЗР.АналитикаВидаУчета);
		СтруктураАналитикиТЗР.Вставить("АналитикаУчетаЗатрат", СтрокаОстатковТЗР.АналитикаУчетаЗатрат);
		СтруктураАналитикиТЗР.Вставить("АналитикаУчетаПартий", СтрокаОстатковТЗР.АналитикаУчетаПартий);
		СтруктураАналитикиТЗР.Вставить("АналитикаРаспределенияЗатрат", СтрокаОстатковТЗР.АналитикаРаспределенияЗатрат);
		
		СтоимостьТЗР = ?(МассивСтоимость = Неопределено, 0, МассивСтоимость[Индекс]);
		
		Если ОтражатьВБУ Тогда
			СтоимостьНУТЗР = ?(МассивСтоимостьНУ = Неопределено, 0, МассивСтоимостьНУ[Индекс]);
			НДСВходящийТЗР = ?(МассивНДСВходящий = Неопределено, 0, МассивНДСВходящий[Индекс]);
			НДСКредитТЗР   = ?(МассивНДСКредит   = Неопределено, 0, МассивНДСКредит[Индекс]);
		Иначе
			СтоимостьНУТЗР = 0;
			НДСВходящийТЗР = 0;
			НДСКредитТЗР   = 0;
		КонецЕсли;	
		
		СтруктураАналитикиТЗР.Вставить("Стоимость"  , СтоимостьТЗР);
		СтруктураАналитикиТЗР.Вставить("СтоимостьНУ", СтоимостьНУТЗР);
		СтруктураАналитикиТЗР.Вставить("НДСВходящий", НДСВходящийТЗР);
		СтруктураАналитикиТЗР.Вставить("НДСКредит"  , НДСКредитТЗР);
		
		МассивСтруктурАналитикиТЗР.Добавить(СтруктураАналитикиТЗР);
		
		Индекс = Индекс + 1;
		
	КонецЦикла; 
	
	Возврат МассивСтруктурАналитикиТЗР;
	
КонецФункции // НайтиАналитикуТЗР

Функция ОпределитьСтоимостьДосписания(
	ВыборкаСтоимостьИспользование, 
	ВсегоСтоимостьИспользование, 
	ОсталосьСтоимостьИспользование, 
	ТЗРСтоимость, 
	ОсталосьДосписатьТЗРСтоимость
	)
	
	Если ОсталосьСтоимостьИспользование <= 0 ИЛИ ОсталосьДосписатьТЗРСтоимость <= 0 Тогда
		Возврат 0;
	КонецЕсли; 
	
	СтоимостьДосписания = 0;
	Если ВыборкаСтоимостьИспользование = ОсталосьСтоимостьИспользование Тогда
		СтоимостьДосписания = ОсталосьДосписатьТЗРСтоимость;
	Иначе
		СтоимостьДосписания = ОКР(ВыборкаСтоимостьИспользование / ВсегоСтоимостьИспользование * ТЗРСтоимость, 2);
	КонецЕсли;	
	
	ОсталосьСтоимостьИспользование = ОсталосьСтоимостьИспользование - ВыборкаСтоимостьИспользование;
	ОсталосьДосписатьТЗРСтоимость  = ОсталосьДосписатьТЗРСтоимость - СтоимостьДосписания;
	
	Возврат СтоимостьДосписания;
	
КонецФункции // ОпределитьСтоимостьДосписания

Процедура ПоказатьВременныеТаблицыРаспределенияТЗР(
	СтруктураШапкиДокумента, 
	МенеджерВременныхТаблиц
	)
	
	// СтруктураШапкиДокумента.Вставить("ОтладочныйРежим", Истина);
	
	РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
		СтруктураШапкиДокумента,
		МенеджерВременныхТаблиц,
		"КонечныеОстаткиТЗР"
	);
	
	РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
		СтруктураШапкиДокумента,
		МенеджерВременныхТаблиц,
		"НачальныеОстаткиМПЗ"
	);
	
	РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
		СтруктураШапкиДокумента,
		МенеджерВременныхТаблиц,
		"ПриходМПЗ"
	);
	
	РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
		СтруктураШапкиДокумента,
		МенеджерВременныхТаблиц,
		"ИспользованиеМПЗ"
	);
	
	РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
		СтруктураШапкиДокумента,
		МенеджерВременныхТаблиц,
		"ИспользованиеМПЗПоНоменклатурнымГруппам"
	);
	
	РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
		СтруктураШапкиДокумента,
		МенеджерВременныхТаблиц,
		"КонечныеОстаткиТЗРПоНоменклатурнымГруппам"
	);
	
КонецПроцедуры // ПоказатьВременныеТаблицыРаспределенияТЗР
 

