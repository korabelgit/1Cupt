// Функция применяется при необходимости получить сведения об учетной политике организации.
//
// Параметры:
// Учет - строка. Определяет регистр сведений, из которого будут получены данные:
//   "Упр"	- регистр "УчетнаяПолитика"
//   "Регл"	- регистр "УчетнаяПолитикаОрганизаций"
//
// Возвращаемое значение - таблица значений. Таблица, каждая строка которой 
//  соответствует записи регистра. 
//
Функция СоздатьКЭШУчетнойПолитики(Учет)
	
	Если НРег(Учет) = "регл" Тогда
		ИмяРегистра = "УчетнаяПолитикаОрганизаций";
	Иначе
		ИмяРегистра = "УчетнаяПолитика";
	КонецЕсли;
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		| //ДляРеглУчета СхемаНалогообложения.НалогНаПрибыль КАК ЕстьНалогНаПрибыльНал,
		| //ДляРеглУчета СхемаНалогообложения.НДС КАК ЕстьНДСНал,
		| //ДляРеглУчета СхемаНалогообложения.ЕдиныйНалог КАК ЕстьЕдиныйНалогНал,
		|	*
		|ИЗ
		|	РегистрСведений." + ИмяРегистра +" КАК УчетнаяПолитика" + Учет + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";
		
	Если НРег(Учет) = "регл" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ДляРеглУчета", "");
	КонецЕсли;
		
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // СоздатьКЭШУчетнойПолитики()

// Функция предназначена для получения значения по имени переменной
// Вызывается из функции глЗначениеПеременной
//
// Параметры:
//	Имя - Строка, имя переменной
//
// Возвращаемое значение: 
//  Произвольное значение
//
Функция ПолучитьЗначениеПеременной(ИмяПараметра) Экспорт
	
	ИмяПараметраВР = ВРег(ИмяПараметра);
	Если ИмяПараметраВР = ВРег("глУчетнаяПолитикаПоПерсоналу")	Тогда
		Возврат ОбщегоНазначенияЗУП.ЗаполнениеУчетнойПолитикиПоПерсоналу();
		
	ИначеЕсли ИмяПараметраВР = ВРег("глУчетнаяПолитикаПоПерсоналуОрганизации") Тогда
		Возврат ОбщегоНазначенияЗУП.ЗаполнениеУчетнойПолитикиПоПерсоналуОрганизации();      
		
	ИначеЕсли ИмяПараметраВР = ВРег("УчетнаяПолитикаУпр") Тогда
		Возврат СоздатьКЭШУчетнойПолитики("Упр");
		
	ИначеЕсли ИмяПараметраВР = ВРег("УчетнаяПолитикаРегл") Тогда
		Возврат СоздатьКЭШУчетнойПолитики("Регл");
		
	ИначеЕсли ИмяПараметраВР = ВРег("ПараметрыПартионногоУчета") Тогда	
		Возврат УправлениеЗапасамиПартионныйУчет.ПолучитьПараметрыПартионногоУчета();
		
	ИначеЕсли ИмяПараметраВР = ВРег("ИспользоватьБлокировкуДанных") Тогда	
		Возврат Метаданные.РежимУправленияБлокировкойДанных <> Метаданные.СвойстваОбъектов.РежимУправленияБлокировкойДанныхПоУмолчанию.Автоматический;
		
	ИначеЕсли ИмяПараметраВР = ВРег("ПрофильПолномочийПользователя") Тогда 
		Возврат ПараметрыСеанса.ТекущийПользователь.ПрофильПолномочийПользователя;
		
	ИначеЕсли ИмяПараметраВР = ВРег("глКоличествоСекундОпросаОбмена") Тогда
		Возврат ПолучитьКоличествоСекундОпросаОбмена();
		
	
	ИначеЕсли ИмяПараметраВР = ВРег("НомерВерсииКонфигурации") Тогда
		
		Возврат ОбновлениеИнформационнойБазы.ВерсияИБ(Метаданные.Имя);
	
	ИначеЕсли ИмяПараметраВР =  ВРег("ДатаНачалаУчетаНКУ") Тогда
		Возврат '20110401';
	
	ИначеЕсли ИмяПараметраВР = ВРег("ДатаНачалаРаспределенияОПЗвНУ") Тогда
		// констната введена ТОЛЬКО для возможности влиять на значение параметра 
		// с использованием внешней обработки, которую может написать любой партнер 1С.
		// разработчики считают правильным использование даты  - 01.08.2011. 
		// если пользователь не согласен - необходимо изменить значение константы внешней обработкой
		НайденноеЗначение = Константы.ДатаНачалаРаспределенияОПЗвНУ.Получить();
		Если НЕ ЗначениеЗаполнено(НайденноеЗначение) Тогда
			НайденноеЗначение = '20110801';
		КонецЕсли;
		
		Возврат НайденноеЗначение;

	ИначеЕсли ВРег(ИмяПараметра) = ВРег("ДатаВступленияВСилуПриказа1379") Тогда
		// константа введена ТОЛЬКО для возможности влиять на значение параметра 
		// с использованием внешней обработки, которую может написать любой партнер 1С.
		НайденноеЗначение = Константы.ДатаВступленияВСилуПриказа1379.Получить();
		Если НЕ ЗначениеЗаполнено(НайденноеЗначение) Тогда
			НайденноеЗначение = '20111216';
		КонецЕсли;
		
		Возврат НайденноеЗначение;


	ИначеЕсли ИмяПараметраВР =  ВРег("ДатаВступленияВСилуЗакона4014") Тогда
		Возврат '20120101';
	
	ИначеЕсли ВРег(ИмяПараметра) = ВРег("ДатаНачалаПереоценкиВалютыПоПравиламПриказа627") Тогда
		// константа введена ТОЛЬКО для возможности влиять на значение параметра 
		// с использованием внешней обработки, которую может написать любой партнер 1С.
		НайденноеЗначение = Константы.ДатаНачалаПереоценкиВалютыПоПравиламПриказа627.Получить();
		Если НЕ ЗначениеЗаполнено(НайденноеЗначение) Тогда
			НайденноеЗначение = '20131001';
		КонецЕсли;
		
		Возврат НайденноеЗначение;
		
	
	ИначеЕсли ВРег(ИмяПараметра) = ВРег("ДатаВступленияВСилуПриказа10") Тогда
		НайденноеЗначение = '20140301';
		Возврат НайденноеЗначение;
	
	ИначеЕсли ВРег(ИмяПараметра) = ВРег("ДатаВступленияВСилуПриказа957") Тогда
		НайденноеЗначение = '20141201';
		Возврат НайденноеЗначение;
	
	
	ИначеЕсли ВРег(ИмяПараметра) = ВРег("ДатаНачалаИспользованияФормыННПоЛекарствам") Тогда
		// константа используется для перехода на новую форму НН с 7% НДС и работы конфигурации
		НайденноеЗначение = Константы.ДатаНачалаИспользованияФормыННПоЛекарствам.Получить();
		Если НЕ ЗначениеЗаполнено(НайденноеЗначение) Тогда
			НайденноеЗначение = '20141201';
		КонецЕсли;
		
		Возврат НайденноеЗначение;
		

	ИначеЕсли ВРег(ИмяПараметра) = ВРег("ДатаВступленияВСилуПриказа754") Тогда
		НайденноеЗначение = '20140819';
		Возврат НайденноеЗначение;
	
	ИначеЕсли ВРег(ИмяПараметра) = ВРег("ДатаВступленияВСилуПостановленияНБУ194") Тогда
		НайденноеЗначение = '20150331';
		Возврат НайденноеЗначение;
	
	ИначеЕсли ВРег(ИмяПараметра) = ВРег("ДатаВступленияВСилуЗУ643") Тогда
		НайденноеЗначение = '20150701';
		Возврат НайденноеЗначение;
	
	
	Иначе
		
		// Если имя переменной ни одно из перечисленных выше, считаем что это имя константы.
		// Если константы с таким именем в конфигурации нет, то произойдет ошибка времени выполнения,
		// это значит, что в функцию передали неверное имя переменной
		Возврат Константы[ИмяПараметраВР].Получить();
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьКоличествоСекундОпросаОбмена()
	
	НайденноеЗначение = Константы.ИнтервалДляОпросаРегламентныхЗаданийВФайловомВарианте.Получить();
	
	Если НайденноеЗначение = 0 Тогда
		НайденноеЗначение = 60;
	КонецЕсли;
	
	Возврат НайденноеЗначение;
	
КонецФункции // 
